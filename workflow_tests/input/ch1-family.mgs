// orig script JSON (228) + dialog JSON (69) = 297 => 77 (74% reduction)
settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

on_tick-family {
	if entity "%PLAYER%" is inside geometry door-family then goto leave-family
}
leave-family {
	set warp state to exit_from-family
	load map main
}
watch-kid {
	turn entity "%SELF%" toward entity Kid
}
kid-ballwatch {
	if entity Kid type is blitzball then goto kid-now-ball
}
kid-now-ball {
	set flag storyflag-sportskid to true
	copy calculate-hint-tracking-series
	if (variable hint-tracking-check is 8) {
		copy set-hint-none
	}
	goto null_script
}

on_load-family {
	if (flag storyflag-sportskid is true) {
		set entity Kid type to blitzball
	}
}

show_dialog-kid {
	set entity "%PLAYER%" tick_script to watch-kid
	if (entity "%SELF%" is glitched) {
		show dialog {
			SELF "Stb N'r ns fstymjw inrjsxnts!"
		}
	} else if (entity Kid type is blitzball) {
		show dialog dialog-kid-ball {
			SELF "I'm the wind! Whoosh!"
		}
	} else {
		show dialog {
			SELF "I'm gonna be a Blitzball when I grow up!"
		}
		copy set-hint-sportskid
	}
	// RESET
	set entity "%PLAYER%" tick_script to null_script
	set entity "%SELF%" interact_script to show_dialog-kid
}

show_dialog-smith {
	if (entity "%SELF%" is glitched) {
		show dialog { SELF "Naa, ptl matm lttz itgxxk, hk lxz ytnem itgxxk?" }
	} else {
		turn entity Smith toward entity "%PLAYER%"
		if (flag smith-backstory is true) {
			show dialog { SELF "Real talk, don't eat memory leek soup. It'll mess with your head." }
		} else {
			show dialog {
				SELF "What's for dinner?"
				"Memory leek soup!"
				"...."
				"...Nah, just kidding. It's saag paneer."
			}
			set flag smith-backstory to true
		}
		turn entity Smith east
	}
	// RESET
	set entity "%SELF%" interact_script to show_dialog-smith
}

show_dialog-smith-dinner {
	show dialog { PLAYER "Smells good!" }
}
