//TODO

settings for dialog {
	parameters for global defaults { alignment BR
	}
	parameters for label PLAYER {
		entity "%PLAYER%"
	}
}

on_tick-mtn-xa_room {
	if flag saw-bert-secret is true then goto on_tick-mtn-xa_room-watch-only-front-door
	goto on_tick-mtn-xa_room-watch-for-xa-too
}

on_tick-mtn-xa_room-watch-for-xa-too {
	if entity "%PLAYER%" is inside geometry near-xa then goto start-cutscene-bert-secret
	if entity "%PLAYER%" is inside geometry entrance1-hitbox then goto leave-mtn-xa_room
	if button HAX then goto on_tick-mtn-hax-attempt-check
}

on_tick-mtn-hax-attempt-check {
	if flag ch2-hax-restored is false then goto on_tick-mtn-hax-attempt
	goto on_tick-mtn-xa_room
}

start-cutscene-bert-secret {
	set entity "%PLAYER%" animation to 0
	copy cutscene-bert-secret
	goto on_tick-mtn-xa_room-watch-only-front-door
}

on_tick-mtn-xa_room-watch-only-front-door {
	if entity "%PLAYER%" is inside geometry entrance1-hitbox then goto leave-mtn-xa_room
	if button HAX then goto on_tick-mtn-hax-attempt-check
}

leave-mtn-xa_room {
	load map main
}

show_dialog-mtn-door-attempt {
	if flag mtn-serial-toot-done is true then goto show_dialog-mtn-door-serial-reminder
	if flag mtn-door-attempted is false then goto show_dialog-mtn-door-attempt-first
	goto show_dialog-mtn-door-serial-naive
}

show_dialog-mtn-door-serial-naive {
	show dialog dialog-mtn-door-serial-naive {
		PLAYER
		"How am I supposed to get around if the\ndoors don't work?"
	}
	goto show_dialog-mtn-door-wrapup
}

show_dialog-mtn-door-attempt-first {
	if flag mtn-hax-attempted is true then goto show_dialog-mtn-door-attempt-second
	show dialog dialog-mtn-door-attempt-first {
		PLAYER
		"The doors don't work? This is going to be\na short quest."
	}
	goto show_dialog-mtn-door-wrapup
}

show_dialog-mtn-door-attempt-second {
	show dialog dialog-mtn-door-attempt-second {
		PLAYER
		"The doors don't work, either? This is\ngoing to be a short quest."
	}
	goto show_dialog-mtn-door-wrapup
}

show_dialog-mtn-door-serial-reminder {
	show dialog dialog-mtn-door-serial-reminder {
		PLAYER
		"Looks like the doors don't work the normal\nway. I guess I'll need to move between\nrooms with the serial console instead."
	}
	goto show_dialog-mtn-door-wrapup
}

show_dialog-mtn-door-wrapup {
	set flag mtn-door-attempted to true
	set entity "%SELF%" interact_script to show_dialog-mtn-door-attempt
}

on_tick-mtn-hax-attempt {
	if flag mtn-serial-toot-done is true then goto show_dialog-mtn-hax-attempt-reminder
	if flag mtn-hax-attempted is false then goto show_dialog-mtn-hax-attempt-first
	goto on_tick-mtn-hax-attempt-naive
}

on_tick-mtn-hax-attempt-naive {
	show dialog dialog-mtn-hax-serial-naive {
		PLAYER
		"Why doesn't Ring Zero work?"
	}
	goto on_tick-mtn-hax-attempt-wrapup
}

show_dialog-mtn-hax-attempt-first {
	if flag mtn-door-attempted is true then goto on_tick-mtn-hax-attempt-second
	show dialog dialog-mtn-hax-attempt-first {
		PLAYER
		"Weird. I can't get Ring Zero to work."
	}
	goto on_tick-mtn-hax-attempt-wrapup
}

on_tick-mtn-hax-attempt-second {
	show dialog dialog-mtn-hax-attempt-second {
		PLAYER
		"Weird. I can't get Ring Zero to work,\neither."
	}
	goto on_tick-mtn-hax-attempt-wrapup
}

show_dialog-mtn-hax-attempt-reminder {
	show dialog dialog-mtn-hax-attempt-reminder {
		PLAYER
		"Oh, yeah. Ring Zero won't work in here\nuntil I fix the mainframe."
	}
	goto on_tick-mtn-hax-attempt-wrapup
}

on_tick-mtn-hax-attempt-wrapup {
	set flag mtn-hax-attempted to true
	set map tick_script to on_tick-mtn-xa_room
}

watch-bert {
	turn entity "%SELF%" toward entity Bert
}

cutscene-bert-secret {
	teleport entity Bert to geometry bert-spot
	turn entity Bert north
	show dialog dialog-mtn-bertsecret1 {
		Bert
		"%PLAYER%, wait."
	}
	set player control to off
	wait 100ms
	set entity "%PLAYER%" tick_script to watch-bert
	wait 400ms
	walk entity Bert along geometry bert-spot over 1200ms
	wait 100ms
	turn entity Bert toward entity "%PLAYER%"
	wait 200ms
	show dialog dialog-mtn-bertsecret2 {
		PLAYER
		"Bert? But how did you get in? I thought\nnone of you could pass the gate!"

		Bert
		"I remembered back door. But...."
	}
	set entity Bert tick_script to spin_self_clockwise
	wait 400ms
	show dialog dialog-mtn-bertsecret3 {
		Bert
		"No. I can go no further. I could, once.\nPerhaps."
	}
	set entity Bert tick_script to null_script
	turn entity Bert toward entity "%PLAYER%"
	show dialog dialog-mtn-bertsecret4 {
		Bert
		"%PLAYER%, I must not stay long. But I\nmust tell you this. Is important."
		"Something is wrong."
		"That book we wanted to give you... it was\nnot gone yesterday. It was on shelf, in\nlibrary."
		"It is only gone today. This morning."

		PLAYER
		"What? What are you saying? The book only\nwent missing this morning?"

		Bert
		"Yes. I am sure of it."
		"%PLAYER%, something wants to stop you.\nOr delay you."

		PLAYER
		"Are we too late, then? Is the Big Bad\nhere?"

		Bert
		"No. We would know. Big Bad would not hide."
	}
	turn entity Bert east
	show dialog dialog-mtn-bertsecret5 {
		Bert
		"But perhaps he can still reach into our\nvillage. Small hacks. Subtle things."
		"Perhaps he left traps. Protocols. Magic\nthat waits for his signal before working."
		"I once knew more. But I am old, and...."
		"...."

		PLAYER
		"...?"
	}
	turn entity Bert south
	show dialog dialog-mtn-bertsecret6 {
		Bert
		"I must go. There is something I must\nlearn. Quickly. But I will return."
		"Do not wait for me. Go and get artifact."
		"If I find answer, I will leave message\nwith XA."
		"Good luck."
	}
	set flag saw-bert-secret to true
	wait 100ms
	walk entity Bert to geometry entrance1-path over 600ms
	wait 80ms
	teleport entity Bert to geometry hiding-spot
	set entity "%PLAYER%" tick_script to null_script
	set player control to on
}

on_load-mtn-xa_room {
	teleport camera to geometry camera-spot
	goto on_load-mtn-xa_room-chapter-check
}

on_load-mtn-xa_room-chapter-check {
	copy boot-out-if-necessary
	goto on_load-mtn-xa_room-hax-check
}

on_load-mtn-xa_room-hax-check {
	if flag can-use-hax-in-mtn is false then goto on_load-mtn-xa_room-disable-hax
	goto on_load-mtn-xa_room-warp-check
}

on_load-mtn-xa_room-disable-hax {
	copy disable-hax
	goto on_load-mtn-xa_room-warp-check
}

on_load-mtn-xa_room-warp-check {
	if warp state is warping then goto on_load-mtn-xa_room-warp-in
	goto on_load-mtn-xa_room-wrapup
}

on_load-mtn-xa_room-warp-in {
	copy warping-in-fade-in
	goto on_load-mtn-xa_room-wrapup
}

on_load-mtn-xa_room-wrapup {
	goto null_script
}

on_go-mtn-xa_room-to-castle_entrance-check {
	goto warp_to-mtn-castle_entrance
}