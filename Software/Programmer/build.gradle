plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.21.1'
}

repositories {
    mavenCentral()
	maven { url  "https://jetbrains.bintray.com/pty4j" }
}

sourceCompatibility = "14"
targetCompatibility = "14"

configurations {
	all.collect { configuration ->
		configuration.exclude group: 'org.mockito', module: 'mockito-all'
	}
}

dependencies {
	// https://mvnrepository.com/artifact/com.google.guava/guava
	implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
	// https://mvnrepository.com/artifact/com.google.inject/guice
	implementation group: 'com.google.inject', name: 'guice', version: '5.0.1'
	// https://mvnrepository.com/artifact/org.controlsfx/controlsfx
	implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.3' exclude group: 'org.openjfx'
	// https://mvnrepository.com/artifact/org.jfxtras/jfxtras-controls
	implementation group: 'org.jfxtras', name: 'jfxtras-controls', version: '11-r1' exclude group: 'org.openjfx'
}

javafx {
    version = 14
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.web']
}

application {
	mainModule.set("programmer")
	mainClass.set("org.dc801.programmer.Main")
	applicationName = "DC801 Badge Programmer"
}

//jar {
//	manifest {
//		attributes 'Main-Class': "org.dc801.programmer.Main",
//			'Implementation-Title': 'DC801 Badge Programmer',
//			'Implementation-Version': getArchiveVersion(),
//			'Bundle-ClassPath' : '.'
//	}
//}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'DC801 Badge Programmer'
    }
	addExtraDependencies('javafx')
    forceMerge('controlsfx')
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
			imageOptions += [
			    '--icon', 'src/main/resources/org/dc801/programmer/style/icon.ico'
			]
            installerOptions += [
				'--win-per-user-install',
				'--vendor', 'DC801',
				'--win-dir-chooser',
				'--app-version', '1.0.0',
				'--win-menu',
				'--win-menu-group', 'DC801',
				'--win-shortcut']
        }
    }
}

tasks.jpackageImage.doLast {
	//file("$buildDir/jpackage/$applicationName/data").mkdirs()
	copy {
		from "$project.rootDir/data"
		into "$buildDir/jpackage/$applicationName/data"
	}
}

tasks.jpackage.doLast {
	copy {
		from "$buildDir/jpackage"
		into "$project.rootDir/Release"
	}
}
