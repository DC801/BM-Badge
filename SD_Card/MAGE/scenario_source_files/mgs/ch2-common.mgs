settings for serial dialog { wrap 60 }

serial dialog spacer {
	" "
	"------------"
	" "
}

/* ---------- CASTLE ON_LOAD INIT ---------- */
// only room 1 does not use this shared behavior

ch2-castle-init {
	// misc game state
	if (flag ch2-ring-zero-restored is false) {
		copy disable-hax
	}
	// serial features
	set serial control on
	copy ch2-register-manual
	if (flag ch2-map-granted is true) {
		copy ch2-map-enable
	}
	// warp-in animation
	if (warp state is warping) {
		copy warping-in-fade-in
	}
}

/* ---------- NON-CASTLE ON_LOAD INIT ---------- */

ch2-noncastle-init {
	// serial features
	if (flag ch2-has-artifact is true) {
		set serial control on
	} else {
		set serial control off
	}
	if (flag ch2-toot-done is true) {
		copy ch2-register-manual
	}
	if (flag ch2-map-granted is true) {
		copy ch2-map-enable
	}
	// CALLBACK MAGIC
	if (variable CALLBACK is 98) {
		goto on_load-ch2-castle-outside-RESUME
	} else if (variable CALLBACK is 202) {
		goto on_load-ch2-oldcouplehouse-RESUME
	} else {
		show serial dialog {
			"<r>ERROR: invalid callback value ($CALLBACK$)</>"
			"<r>   script: 'ch2-noncastle-init'</>"
			"<r>   file: 'ch2-common.mgs'</>"
		}
	}
}

/* ---------- MAN PAGES ---------- */

ch2-register-manual {
	// the default set
	register man -> ch2-command-man
	// BUG: man+man breaks solo man
	// register man + man -> ch2-man-man
	register man + look -> ch2-man-look
	register man + go -> ch2-man-go
	register man + help -> ch2-man-help
	register man fail -> ch2-man-fail
}

ch2-man-man {
	show serial dialog spacer
	show serial dialog {
		"MGE General Commands Manual - \"MAN\""
		" "
		"Displays manual pages for various MGE serial commands. Type \"MAN\" followed by the name of the command you want to read about, e.g. \"MAN LOOK\""
		" "
		"NOTE: Hi, there, birthday mage! I've rigged this thing to call me if you use \"MAN\" as a single word. I \"hacked\" the man page to tell you that!"
	}
}
ch2-man-fail {
	show serial dialog spacer
	show serial dialog {
		"MAN PAGE NOT FOUND: There is no documentation for that command, or that command does not exist."
	}
}
ch2-man-look {
	show serial dialog spacer
	show serial dialog {
		"MGE General Commands Manual - \"LOOK\""
		" "
		"Describes the room you're in. Will describe entities instead if an entity name is provided after the word LOOK."
		" "
		"Importantly, it can list a room's exits, even if they are invisible."
	}
}
ch2-man-go {
	show serial dialog spacer
	show serial dialog {
		"MGE General Commands Manual - \"GO\""
		" "
		"Lets you travel to an adjacent room, even if the actual, physical door is blocked, locked, or hidden. Type \"GO\" followed by the name of the exit, e.g. \"GO NORTH\""
		" "
		"To learn the names of the current room's exits, use \"LOOK\"."
	}
}
ch2-man-help {
	show serial dialog spacer
	show serial dialog {
		"MGE General Commands Manual - \"HELP\""
		" "
		"Provides general information for the serial terminal, including a list of valid serial commands."
	}
}
ch2-man-map {
	show serial dialog spacer
	show serial dialog {
		"MGE General Commands Manual - \"MAP\""
		" "
		"Displays an ASCII map of the castle, though only while you're inside the castle."
		" "
		"Rooms are added to the map as you visit them."
		" "
		"The \"@\" symbol indicates which room you're in."
	}
}

/* ---------- INITIALIZING: map command ---------- */

ch2-map-enable {
	// workaround for local script mapping:
	register map -> draw-ch2-serial-map-check
	unregister map
	// the real mccoy:
	register map -> draw-ch2-serial-map-check

	// man page
	register man + map -> ch2-man-map
	unregister man + map
	register man + map -> ch2-man-map
}

draw-ch2-serial-map-check {
	if (variable ch2-in-room is 0) {
		show serial dialog {
			"You don't seem to have a map for this place."
		}
	} else {
		goto draw-ch2-serial-map
	}
}

/* ---------- INITIALIZING: other ---------- */

disable-hax {
	set hex control to off
	set hex clipboard to off
}

/* ---------- ON_WARP & ON_GO ANIMATION ---------- */

spin-quickly {
	rotate entity "%SELF%" -1
	wait 150ms
}

warping-out-fade-out {
	set warp state to warping
	set player control to off
	set entity "%PLAYER%" tick_script to spin-quickly
	fade out camera to #000000 over 500ms
}

warping-in-fade-in {
	teleport entity "%PLAYER%" to geometry warp-spot
	set entity "%PLAYER%" tick_script to spin-quickly
	fade in camera from #000000 over 300ms
	set entity "%PLAYER%" tick_script to null_script
	turn entity "%PLAYER%" south
	set player control to on
}

/* ---------- why did I make this? ---------- */

boot-out-if-necessary {
	if variable current-chapter is 1 then goto boot-to-chapter-1
}

boot-to-chapter-1 {
	set warp state to was-booted
	load map main
}

/* ---------- ALL-IN-ONE WARP LIST ---------- */

// to be deprecated (if not already)

warp_to-mtn-xa_room {
	copy warping-out-fade-out
	show serial dialog spacer
	show serial dialog { "Traveling to 'xa-room'!" }
	load map xa-room
}

warp_to-mtn-castle_entrance {
	copy warping-out-fade-out
	show serial dialog spacer
	show serial dialog { "Traveling to 'castle-door'!" }
	load map castle-door
}
