include!("header.mgs")

show_dialog-timewarp {
	register rtfm -> command-rtfm
	if (flag demo-cutseen-timewarp is false) {
		fade out camera to #000000 over 1ms
		wait 200ms
		set player control to off
		turn entity "%PLAYER%" south
		show dialog dialog-sleep {
			name " "
			"...."
			"Am I asleep?"
		}
		fade in camera from #000000 over 2000ms
		show dialog dialog-timewarp {
			PLAYER alignment BL
			"Huh? Is this the village?"
			"No, this is the village of the past. But\nI still have Ring Zero...."
			"This has got to be a dream."
		}
		set player control to on
		set flag demo-cutseen-timewarp to true
	}
	set hex control to on
}

show_dialog-bob-first-demo_map {
	show dialog dialog-bob1-demo_map {
		SELF
		"Name's Bob."
		"Stone Cold Bob Austin."
		"Right, what's yours, then?"

		PLAYER
		"You don't know who I am?\nWe've both lived here for years!"

		SELF
		"Don't know nobody. All I know is,\ngotta keep everyone out."
		"You know, unless their name is Bob."

		PLAYER
		"Bob?"

		SELF
		"Bob. Camel case.\nNice and plain.\nTraditional."
		"'Cause this club is Bobs-only.\nYou know, the Bobs-Only Club."

		PLAYER
		"Why is it Bobs only?"

		SELF
		"Why?\nWhy is it Bobs only?"
		"Why, it wouldn't be the Bobs-Only Club\nif I just let anyone in!"
	}
	set entity Bob on_interact to show_dialog-bob-bounce-demo_map
}

show_dialog-bob-bounce-demo_map {
	show dialog dialog-bob2-demo_map {
		SELF "So, what's your name then?"
	}
	if entity "%PLAYER%" name is Bob then goto show_dialog-bob-yesbob-demo_map
	show dialog dialog-bob3-demo_map {
		PLAYER "Um.... it's not Bob."
		SELF "Sorry, can't unlock the door for ya.\nBobs only."
	}
}

show_dialog-bob-yesbob-demo_map {
	show dialog dialog-bob4-demo_map {
		PLAYER "Why, what luck!\nMy name happens to be Bob!"
		SELF "Nice. Great name.\nI'll unlock the door."
	}
	walk entity Bob to geometry "%ENTITY_PATH%" over 666ms
	turn entity Bob west
	wait 666ms
	turn entity Bob south
	show dialog dialog-bob5-demo_map {
		SELF "There you go. It's open."
	}
	walk entity "%PLAYER%" to geometry predoor over 666ms
	walk entity "%PLAYER%" to geometry door over 1333ms
	wait 666ms
	turn entity "%PLAYER%" east
	show dialog dialog-bob6-demo_map {
		PLAYER "Hey, what gives?\nI still can't get in."
	}
	turn entity Bob west
	show dialog dialog-bob7-demo_map {
		SELF "Oh, it's unlocked."
		PLAYER "Then why won't it open?"
		SELF "Oh. The door's not implemented yet."
		PLAYER "...."
		"You could've said that from the start!"
		SELF "Not my job. My only job is to\nkeep everyone out who's\nnot named Bob."
	}
	turn entity Bob south
	set entity Bob on_interact to show_dialog-bob-job-demo_map
}

show_dialog-bob-job-demo_map {
	show dialog dialog-bob8-demo_map {
		SELF "So, what's your name, then?"
		PLAYER "The door doesn't work!\nWhat does it matter what\nmy name is?"
		SELF "Not my job.\nMy only job is to keep everyone out\nwho's not named Bob."
	}
}

show_dialog-sheep-demo_map {
	show dialog dialog-sheep-demo_map {
		SELF "...."
	}
}

show_dialog-dsheep-demo_map {
	show dialog dialog-dsheep-demo_map {
		SELF "...?"
	}
}

show_dialog-max-demo_map {
	copy face-player
	show dialog dialog-max1-demo_map {
		SELF "Ah, a citizen!"
		"I wondered if you could help me!"
		PLAYER "What do you need?"
		SELF "My name's Swagger!\nMax Swagger!"
		"I was doing a bit of reconnoissance,\nlooking for a place where I can\nopen my new fashion shop."
		"The board here says there's space\navailable, but I can't seem to find\nthe building it's talking about."
		"The only thing actually here is\nthe Bobs-Only Club!"
		PLAYER "Well, the town isn't fully implemented\nyet, so... you might have to wait\na few more weeks for the rest of\nthe buildings to return."
	}
	set player control to off
	rotate entity "%SELF%" -2
	wait 800ms
	rotate entity "%SELF%" 1
	wait 800ms
	set player control to on
	copy face-player
	show dialog dialog-max2-demo_map {
		SELF "Not fully implemented?"
		PLAYER "Sorry."
		"But I can personally guarantee\nyou can have a building all to yourself\nwhen the time comes."
		SELF "Oh! That's good news! All to myself?"
		"I'm picturing two stories... no, three!"
		"Lush woven carpets... vaulted ceilings...\nbeautiful oak doors...\nhand-carved decorative moulding...."
		PLAYER "Um, well....\nYou'll definitely get a building."
		SELF "Excellent! Then I shall return\nwhen the time comes!"
		"Until that glorious day, farewell!"
	}
	copy walk_on_path_9s-demo_map
	teleport entity "%SELF%" to geometry hiding
}

show_dialog-timmy-demo_map {
	copy pause-walk-and-face-player
	show dialog dialog-timmy-demo_map {
		SELF "I'm practicing for my triathlon!"
		"Running is my weakest event, so I'm\nworking hard to improve my time!"
		PLAYER "Don't you need legs to run?"
		SELF "...?"
		"No?"
	}
	copy unface-player-and-resume-walk
}

show_dialog-kid-demo_map {
	copy pause-walk-and-face-player
	show dialog dialog-kid-demo_map {
		SELF "I'm gonna be a Blitzball when I grow up!"
	}
	copy unface-player-and-resume-walk
}

show_dialog-goose-demo_map {
	show dialog dialog-goose-demo_map {
		SELF "Honk!"
	}
}

show_dialog-beatrice-demo_map {
	set player control to off
	copy face-player
	show dialog dialog-beatrice1-demo_map {
		SELF "Back when he was a young man,\nmy husband Delmar hand-carved\nall the statues on the fountains\naround town."
		"Such a shame that this one isn't working.\nI wonder if something is clogging\nthe pump."
	}
	rotate entity "%SELF%" 1
	wait 600ms
	rotate entity "%SELF%" -2
	wait 600ms
	copy face-player
	show dialog dialog-beatrice2-demo_map {
		SELF "Between you and me....\nI think the goose has something\nto do with it."
	}
	set player control to on
	turn entity "%SELF%" south
}

show_dialog-trekkie-demo_map {
	show dialog dialog-trekkie-demo_map {
		SELF "Me heard you will soon have a birthday!"
		PLAYER "That's right! I'll be turning 16,\nand I'll get to be an official\nvillage mage!"
		SELF "Me hope you have happy birthday!"
		PLAYER "Thanks very much, Trekkie!"
	}
}

show_dialog-verthandi-demo_map {
	copy face-player
	show dialog dialog-verthandi-demo_map {
		SELF "I'm so glad the goats made up\nand are friends again!"
	}
	turn entity "%SELF%" south
}

show_dialog-goat-demo_map {
	show dialog dialog-goat-demo_map {
		SELF "Baaahhh!"
	}
}

show_dialog-cleo1-demo_map {
	show dialog dialog-cleo1-demo_map {
		Cat "Meowrrow."
		Cleo "Oh, you don't say!"
		Cat "Meow!"
		Cleo "He didn't!"
	}
	set entity "%SELF%" on_interact to show_dialog-cleo2
}

show_dialog-cleo2-demo_map {
	show dialog dialog-cleo2-demo_map {
		Cat "Meowwowow."
		Cleo "That's what I've been saying\nthis whole time!"
		Cat "Meowwrrrr!"
		Cleo "Oh, I know!"
	}
	set entity "%SELF%" on_interact to show_dialog-cleo1-demo_map
}

show_dialog-cat-demo_map {
	show dialog dialog-cat-demo_map {
		PLAYER "Hey there, cat!"
		Cat "...."
		Cleo "Excuse me, but we were in the middle\nof a conversation."
		PLAYER "Oh, sorry. Carry on."
	}
}

show_dialog-smith-demo_map {
	copy face-player
	show dialog dialog-smith-demo_map {
		SELF "I came here on a blacksmithing\nscholarship, but there's not much\nblacksmithing to do at the moment."
		"I think I'll stick around anyway.\nSee what happens."
	}
}

check_if_player_is_goat_high-demo_map {
	if entity "%PLAYER%" is inside geometry high then goto move_goats_to_low-demo_map
}

check_if_player_is_goat_low-demo_map {
	if entity "%PLAYER%" is inside geometry low then goto move_goats_to_high-demo_map
}

move_goats_to_low-demo_map {
	set entity goat1 on_tick to move_goat1_to_low-demo_map
	set entity goat2 on_tick to move_goat2_to_low-demo_map
	set entity "%PLAYER%" on_tick to check_if_player_is_goat_low-demo_map
}

move_goats_to_high-demo_map {
	set entity goat1 on_tick to move_goat1_to_high-demo_map
	set entity goat2 on_tick to move_goat2_to_high-demo_map
	set entity "%PLAYER%" on_tick to check_if_player_is_goat_high-demo_map
}

move_goat1_to_low-demo_map {
	walk entity goat1 to geometry low1 over 600ms
	loop entity goat1 along geometry low1 over 2000ms
}

move_goat2_to_low-demo_map {
	walk entity goat2 to geometry low2 over 733ms
	loop entity goat2 along geometry low2 over 2266ms
}

move_goat1_to_high-demo_map {
	walk entity goat1 to geometry high1 over 600ms
	loop entity goat1 along geometry high1 over 2000ms
}

move_goat2_to_high-demo_map {
	walk entity goat2 to geometry high2 over 733ms
	loop entity goat2 along geometry high2 over 2266ms
}

loop_on_path_30s-demo_map {
	loop entity "%SELF%" along geometry "%ENTITY_PATH%" over 20000ms
}

loop_on_path_10s-demo_map {
	loop entity "%SELF%" along geometry "%ENTITY_PATH%" over 6666ms
}

loop_on_path_3s-demo_map {
	loop entity "%SELF%" along geometry "%ENTITY_PATH%" over 2000ms
}

walk_on_path_9s-demo_map {
	walk entity "%SELF%" along geometry "%ENTITY_PATH%" over 9000ms
}

reload {
	load map demo
}

show_dialog-demo-end-dream-q {
	show dialog dialog-demo-end-dream-q {
		SELF
		portrait goldfish
		"End this dream?"
		> "Yes (save and quit)" : goto "demo-end-dream-yes-save"
		> "Yes (quit without saving)" : goto "demo-end-dream-yes"
		> "No (continue dream)" : goto "demo-end-dream-no"
	}
}

demo-end-dream-yes {
	load map main_menu
}
demo-end-dream-yes-save {
	save slot
	load map main_menu
}

demo-end-dream-no {
	set entity "%SELF%" on_interact to show_dialog-demo-end-dream-q
}

dialog dialog-max3-demo_map {
	PLAYER "Not much of a market for fashion here,\nI would've thought."
	"Best of luck, Max Swagger!"
}
