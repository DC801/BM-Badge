settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label GBob { entity "Guardian Bob" }
}
settings for serial dialog { wrap 60 }

// (NOT LIVE YET)

/* ---------- LOOK SCRIPTS ---------- */

on_look-ch2-bobsclub-basement {
	show serial dialog spacer
	show serial dialog {
		"   TODO: Bob's Club basement text"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-bobsclub-basement {
	// do unique stuff first (but turn serial / player control off first, if you do)
	if (flag ch2-cutseen-bob-party is false) {
		goto ch2-cutscene-bob-party
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-bobsclub-basement {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_go-ch2-bobsclub-basement-stairs {
	copy warping-out-fade-out
	load map ch2-bobsclub
}

/* ---------- CUTSCENE ---------- */

ch2-cutscene-bob-party {
	set hex control off
	set serial control off
	set player control off
	// Fade in.
	// The player takes a couple steps forward and looks around.
	show dialog {
		PLAYER "What the...."
	}
	// Guardian Bob pops out from somewhere
	show dialog {
		GBob "Hey! You're not allowed in here!"
	}
	// He rushes to you but then slows
	show dialog {
		GBob "Oh, wait. I remember you. Your name was Bob, wasn't it?"
	}
	if (entity "%PLAYER%" name is "Bob") {
		show dialog {
			PLAYER "Oh, I guess it is."
		}
	} else {
		show dialog {
			PLAYER "Um, technically, it WAS Bob...."
			"(At one point....)"
		}
	}
	show dialog {
		GBob "Oh, well. Too late, regardless."
	}
	// GBob looks around
	show dialog {
		GBob "Welcome to the annual Bob's Club party."
		PLAYER "I thought the party was canceled this year."
	}
	// GBob stops looking around; looks at player
	wait 80ms
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	wait 150ms
	show dialog {
		GBob "It was, but... we seem to be experiencing a time anomaly."
		PLAYER "What? A time anomaly?"
		GBob "Yeah. The flow of time is being simultaneously compressed and expanded."
		"The Bob's Club party was canceled this year, but simultaneously, this is a year where it WASN'T canceled."
		"Your 16th birthday, for another example. When did you get chosen by Ring Zero?"
		PLAYER "Yesterday."
		GBob "Was it yesterday? Or was it actually a few years ago?"
	}
	// player steps backward
	show dialog {
		PLAYER "Wait. What?"
	}
	turn entity "Guardian Bob" south
	show dialog {
		GBob "That's what makes this an anomaly. It wasn't yesterday, and it wasn't a few years ago. It was BOTH."
	}
	turn entity "%PLAYER%" south
	show dialog {
		PLAYER "That's... impossible."
		"What does that even mean?"
		GBob "It means someone is messing with reality."
	}
	turn entity "%PLAYER%" toward entity "Guardian Bob"
	show dialog {
		PLAYER "The Big Bad?"
	}
	wait 50ms
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	wait 30ms
	show dialog {
		GBob "Probably."
	}
	// player looks around
	show dialog {
		PLAYER "This is freaking me out. How do we stop it?"
	}
	turn entity "Guardian Bob" south
	show dialog {
		GBob "Depends on the exact cause. I've been keeping my eye on it with my keytool, Glitch. Trying to investigate from several angles."
		"But I don't have a lot of power in here."
	}
	wait 40ms
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	wait 180ms
	show dialog {
		GBob  "Honestly, Bob, the sooner you can stop the Big Bad, the better."
		"No pressure!"
	}
	wait 40ms
	turn entity "Guardian Bob" north
	wait 150ms
	show dialog {
		GBob "Anyway, I'll keep watch here."
		"Help yourself to the food. There's plenty."
	}
	if (flag ch2-want-cactuscooler is true) { // TODO set this flag when you hear request from heat sink guys
		show dialog {
			PLAYER "Oh, do you have any Cactus Cooler?"
		}
		turn entity "Guardian Bob" toward entity "%PLAYER%"
		show dialog {
			GBob "Sure. We've got a lifetime supply in the ice chest back there. Help yourself."
		}
	}
	// GBob walks a short ways away then slowly looks around the room
	goto ch2-map-init
}

/* ---------- ENTITIES ---------- */

interact-ch2-guardianbob {
	show dialog {
		SELF "TODO"
	}
}
interact-ch2-marathonbob {
	// capture original facing direction
	if (entity "Entity Name" direction is north) {
		mutate tempvar = 0
	} else if (entity "Entity Name" direction is east) {
		mutate tempvar = 1
	} else if (entity "Entity Name" direction is south) {
		mutate tempvar = 2
	} else if (entity "Entity Name" direction is west) {
		mutate tempvar = 3
	}
	// normal behavior
	copy face-player
	mutate random ? 6
	// https://www.youtube.com/watch?v=7dpxLPbamnA for BoB quotes
	if (variable random is 1) {
		show dialog { SELF "They're everywhere!" "Us Bobs, that is. We're everywhere!" }
	} else if (variable random is 2) {
		show dialog { SELF "Ah! Watch it!" "You stepped on my foot!" }
	} else if (variable random is 3) {
		show dialog { SELF "Follow me!" "Let's start a conga line!" }
	} else if (variable random is 4) {
		show dialog { SELF "AAHHHH!" "I bit my tongue!" }
	} else if (variable random is 5) {
		show dialog { SELF "Get me outta here." "This dubstep is too much." }
	} else if (variable random is 5) {
		show dialog { SELF "Hey! He's shooting at us!" "That DSLR looks nice. Wonder where he got it." }
	} else if (variable random is 6) {
		show dialog { SELF "Perimeter secured." "No non-Bobs here!" }
	} else {
		show dialog { SELF "Frog blast the vent core!" "That's the name of my favorite drink." }
	}
	// turn back to the original facing direction
	if (variable tempvar is 0) {
		turn entity "%SELF%" north
	} else if (variable tempvar is 1) {
		turn entity "%SELF%" east
	} else if (variable tempvar is 2) {
		turn entity "%SELF%" south
	} else if (variable tempvar is 3) {
		turn entity "%SELF%" west
	}
	mutate tempvar = 0 // courtesy reset
	set entity "%SELF%" interact_script to interact-ch2-marathonbob // reset
}

interact-ch2-icechest {
	if (flag ch2-carrying-cactuscooler is true) {
		if (
			flag interrupt is false // you didn't just pick it up
			|| flag ch2-want-cactuscooler is false // OR you don't know you need it
		) {
			show dialog {
				PLAYER "I won't need more Cactus Cooler than this. I'll leave the rest for everyone else."
			}
		} else {
			// if you did just pick it up AND you know you need it
			show dialog {
				PLAYER "Got my can of Cactus Cooler. Time to bring it to the server room guys."
			}
		}
	} else if (
		flag ch2-carrying-heatsink is true
		|| flag ch2-installed-heatsink is true
	) {
		// you've already turned the soda in
		show dialog {
			PLAYER "The server room guys already have their Cactus Cooler. I'll leave the rest for everyone else."
		}
	} else { // You get to pick up a Cactus Cooler!
		if (flag ch2-want-cactuscooler is false) {
			show dialog { PLAYER "This really is a lot of Cactus Cooler. Guess I'll grab a can for the road." }
		} else {
			show dialog { PLAYER "Bingo! Cactus Cooler!" }
		}
		set flag ch2-carrying-cactuscooler to true
		set flag interrupt to true // you just picked it up
	}
	set entity "%SELF%" interact_script to interact-ch2-icechest // reset
}
