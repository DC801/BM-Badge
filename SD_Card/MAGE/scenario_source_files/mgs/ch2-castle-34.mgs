settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-34 {
	show serial dialog spacer
	show serial dialog {
		"TODO: PANTRY on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-34 {
	// ch2 room state
	mutate ch2-in-room = 34

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-34 is false) {
		show serial dialog { "Discovered the <bold><c>PANTRY</>! Room added to map!" }
		set flag ch2-seen-room-34 to true
	} else {
		show serial dialog { "Entering the <bold>PANTRY</>..." }
	}
	if (
		flag ch2-installed-harddrive is true
		|| flag ch2-carrying-harddrive is true
	) {
		copy script ch2-hide-harddrive
	}
	
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-34 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-34 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-34-south {
	copy on_exit-ch2-castle-34
	copy warping-out-fade-out
	load map ch2-castle-32
}

/* ---------- HARDDRIVE (ADMIN) ---------- */

ch2-hide-harddrive {
	teleport entity "Hard Drive" to geometry hiding-spot
	set entity "Hard Drive" name to " "
}

ch2-touch-harddrive {
	if (flag ch2-harddrive-firstlook is false) {
		show dialog {
			PLAYER "TODO harddrive1"
		}
		set flag ch2-harddrive-firstlook to true
	}
	show dialog {
		PLAYER "TODO harddrive2."
	}
	if (variable ch2-storyflag-round is >= 2) {
		show dialog {
			name "" "(Pick up the hard drive?)"
			> "Yes" : ch2-touch-harddrive-yes
			> "No" : ch2-touch-harddrive-no
		}
	}
	set entity "%SELF%" interact_script to ch2-touch-harddrive // RESET
}
ch2-touch-harddrive-yes {
	show dialog {
		name "" "(You pick up the hard drive!)"
	}
	copy script ch2-hide-harddrive
	set flag ch2-carrying-harddrive to true
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-harddrive-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-harddrive // RESET
}

ch2-install-harddrive {
	set flag ch2-carrying-harddrive to false
	set flag ch2-installed-harddrive to true
}
look-harddrive {
	show serial dialog spacer
	show serial dialog {
		"on_look harddrive TODO"
	}
}
