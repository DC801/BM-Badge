settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-22 {
	show serial dialog spacer
	show serial dialog {
		"TODO: SERVER ROOM on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-22 {
	// ch2 room state
	mutate ch2-in-room = 22

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-22 is false) {
		show serial dialog { "Discovered <bold><c>SERVER ROOM</>! Room added to map!" }
		set flag ch2-seen-room-22 to true
	} else {
		show serial dialog { "Entering <bold>SERVER ROOM</>..." }
	}
	if (
		flag ch2-installed-powersupply is true
		|| flag ch2-carrying-powersupply is true
	) {
		copy script ch2-hide-powersupply
	}
	
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-22 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-22 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-22-north {
	copy on_exit-ch2-castle-22
	copy warping-out-fade-out
	load map ch2-castle-23
}
on_go-castle-22-south {
	copy on_exit-ch2-castle-22
	copy warping-out-fade-out
	load map ch2-castle-21
}

/* ---------- ENTITIES ---------- */

interact-ch2-scuzzy {
	copy face-player
	show dialog {
		SELF "You know, the castle is a computer, too."
		"Each room has its specialized function, and resources and information may flow between rooms when they are connected."
		"In isolation each room, and each of us, are capable of little. But together, we can collaborate and build amazing things."
	}
	set flag ch2-namelessnpc-backstory to true
	turn entity "%SELF%" west
}

/* ---------- POWER SUPPLY (ADMIN) ---------- */

ch2-hide-powersupply {
	teleport entity "Power Supply" to geometry hiding-spot
	set entity "Power Supply" name to " "
}

ch2-touch-powersupply {
	if (flag ch2-powersupply-firstlook is false) {
		show dialog {
			PLAYER "TODO powersupply1"
		}
		set flag ch2-powersupply-firstlook to true
	}
	show dialog {
		PLAYER "TODO powersupply2."
	}
	show dialog {
		name "" "(Pick up the power supply?)"
		> "Yes" : ch2-touch-powersupply-yes
		> "No" : ch2-touch-powersupply-no
	}
	set entity "%SELF%" interact_script to ch2-touch-powersupply // RESET
}

ch2-touch-powersupply-yes {
	show dialog {
		name "" "(You pick up the powersupply!)"
	}
	copy script ch2-hide-powersupply
	set flag ch2-carrying-powersupply to true
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-powersupply-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-powersupply // RESET
}

ch2-install-powersupply {
	set flag ch2-carrying-powersupply to false
	set flag ch2-installed-powersupply to true
}
look-powersupply {
	show serial dialog spacer
	show serial dialog {
		"on_look powersupply TODO"
	}
}
