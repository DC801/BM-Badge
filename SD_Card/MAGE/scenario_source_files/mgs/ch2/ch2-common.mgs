settings for serial dialog { wrap 60 }

serial dialog spacer {
	" "
	"------------"
	" "
}
serial dialog lambda-over {
	// check if there's a blank line just before using this dialog (there should be one)
	"<bold>lambda-talk</> connection closed succesfully"
}

// QUICK DEBUGS

ch2-debug-storyflag {
	show serial dialog spacer
	show serial dialog {
		"<g>DEBUG</>: Set flag 'ch2-storyflag-round' to what value? (0-5)"
		_ "0" : ch2-debug-storyflag-0
		_ "1" : ch2-debug-storyflag-1
		_ "2" : ch2-debug-storyflag-2
		_ "3" : ch2-debug-storyflag-3
		_ "4" : ch2-debug-storyflag-4
		_ "5" : ch2-debug-storyflag-5
	}
	show serial dialog {
		"Nothing changed."
	}
}

ch2-debug-storyflag-0 {
	mutate ch2-storyflag-round = 0
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-1 {
	mutate ch2-storyflag-round = 1
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-2 {
	mutate ch2-storyflag-round = 2
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-3 {
	mutate ch2-storyflag-round = 3
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-4 {
	mutate ch2-storyflag-round = 4
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-5 {
	mutate ch2-storyflag-round = 5
	goto ch2-debug-storyflag-wrapup
}
ch2-debug-storyflag-wrapup {
	show serial dialog spacer
	show serial dialog {
		"Flag 'ch2-storyflag-round' is now $ch2-storyflag-round$"
	}
}

/* ---------- CASTLE ON_LOAD INIT ---------- */

ch2-do-init-on-end {
	// This is so you can run the init stuff at an arbitrary time, usually at the end of a big involved thing. The flag here should be false most of the time.
	if (flag ch2-do-init-on-end is true) {
		set flag ch2-do-init-on-end to false
		goto ch2-map-init
	}
	goto null_script
}

ch2-map-init {
	set flag interrupt to false // flag set when something happens in the room and you haven't left the room yet
	set flag interrupt2 to false // for when you need a second one
	set flag interrupt3 to false // OwO

	// misc game state
	if (
		flag ch2-ring-zero-restored is true // you've fixed Ring Zero
		|| variable ch2-in-room is 0 // OR you're not in the castle at all
	) {
		set hex control on
	} else {
		set hex control to off
		set hex clipboard to off
	}

	// serial features
	if (flag ch2-has-artifact is false) { // no artifact at all
		set serial control off
	} else { // you have the artifact
		if (
			variable ch2-in-room is not 0 // if you're in the castle
			|| flag ch2-toot-done is true // or if the toot is all the way done
		) {
			set serial control on
			copy register-rtfm
			copy register-inventory
			copy register-manual
			if (flag ch2-map-granted is true) {
				copy register-map
			}
			if (flag ch2-permission-to-fuse is true) {
				copy register-fuse
			}
			if (variable ch2-storyflag-round is > 0) {
				copy register-parts
			}
		} else { // you're not in the castle and the toot isn't done yet
			set serial control off
		}
	}
	
	// warp-in animation
	if (flag ch2-manual-warp is true) { // workaround for WOPR
		set flag ch2-manual-warp to false
	} else {
		if (warp state is warping) {
			copy warping-in-fade-in
		}
	}
	set player control on

	// DEBUG
	register storyflag -> ch2-debug-storyflag
	register storyflag + "0" -> ch2-debug-storyflag-0
	register storyflag + "1" -> ch2-debug-storyflag-1
	register storyflag + "2" -> ch2-debug-storyflag-2
	register storyflag + "3" -> ch2-debug-storyflag-3
	register storyflag + "4" -> ch2-debug-storyflag-4
	register storyflag + "5" -> ch2-debug-storyflag-5
	copy register-warp-debug
	// END DEBUG

	// For anything that needs to take time (like fade ins and walking animations) or anything that needs unique handling (hiding entities after checking game state) do that stuff BEFORE jumping to this script. That way this script can be a dead end. (The if/else "zigzags" require this to be a deadend, unless you use a callback variable to branch back again)
	// IF SO, be sure to turn off serial control and player control before doing any of that, and let it come back on again with this script.
}

/* ---------- ON_WARP & ON_GO ANIMATION ---------- */

spin-quickly {
	rotate entity "%SELF%" -1
	wait 150ms
}

warping-out-fade-out {
	set player control to off
	set serial control off
	set warp state to warping
	set entity "%PLAYER%" tick_script to spin-quickly
	fade out camera to #000000 over 500ms
}

warping-in-fade-in {
	teleport entity "%PLAYER%" to geometry warp-spot
	set entity "%PLAYER%" tick_script to spin-quickly
	fade in camera from #000000 over 300ms
	set entity "%PLAYER%" tick_script to null_script
	turn entity "%PLAYER%" south
	set warp state to warped // it needs to be set to SOMETHING...
	set player control to on // TODO: remove after shared behavior is fully merged
	set serial control on // TODO: remove after shared behavior is fully merged
}
