settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-21 {
	show serial dialog spacer
	show serial dialog {
		"TODO: WORKSHOP on_look text\n "
	}
}

look-abacus {
	show serial dialog spacer
	show serial dialog {
		"on_look abacus TODO"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-21 {
	// ch2 room state
	mutate ch2-in-room = 21

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-21 is false) {
		show serial dialog { "Discovered <bold><c>WORKSHOP</>! Room added to map!" }
		set flag ch2-seen-room-21 to true
	} else {
		show serial dialog { "Entering <bold>WORKSHOP</>..." }
	}
	if (
		flag ch2-installed-abacus is true
		|| flag ch2-carrying-abacus is true
	) {
		copy script ch2-hide-abacus
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-21 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-21 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-21-north {
	copy on_exit-ch2-castle-21
	copy warping-out-fade-out
	load map ch2-castle-22
}
on_go-castle-21-east {
	copy on_exit-ch2-castle-21
	copy warping-out-fade-out
	load map ch2-castle-11
}

/* ---------- ABACUS (ADMIN) ---------- */

ch2-touch-abacus {
	if (flag ch2-abacus-firstlook is false) {
		show dialog {
			PLAYER "TODO abacus1"
		}
		set flag ch2-abacus-firstlook to true
	}
	show dialog {
		PLAYER "TODO abacus2."
	}
	if (variable ch2-storyflag-round is >= 3) {
		show dialog {
			name "" "(Pick up the abacus?)"
			> "Yes" : ch2-touch-abacus-yes
			> "No" : ch2-touch-abacus-no
		}
	}
	set entity "%SELF%" interact_script to ch2-touch-abacus // RESET
}

ch2-hide-abacus {
	teleport entity "abacus" to geometry hiding-spot
	set entity "abacus" name to " "
}

ch2-touch-abacus-yes {
	show dialog {
		name "" "(You pick up the abacus!)"
	}
	copy script ch2-hide-abacus
	copy script ch2-pickup-abacus
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-abacus-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-abacus // RESET
}

ch2-install-abacus {
	set flag ch2-carrying-abacus to false
	set flag ch2-installed-abacus to true
}

/* ---------- ENTITIES ---------- */

interact-ch2-jeanpaul {
	copy face-player
	if (variable ch2-storyflag-round is < 3) {
		show dialog {
			SELF "TODO (generic)"
		}
	} else if (variable ch2-storyflag-round is 3) {
		if (flag ch2-want-ramchips is false) {
			mutate CALLBACK = 1
			goto interact-ch2-room21-split
		} else {
			if (flag interrupt is true) {
				show dialog {
					SELF "Sea Moss is an odd duck."
					PLAYER "Wait, he's a duck?"
					SELF "Erm, no, he's a stone golem, but let's just say he's a creative character."
				}
			} else {
				copy interact-ch2-room21-split-s
			}
		}
	} else {
		show dialog {
			SELF "TODO (Good luck building computer)"
		}
	}
	goto interact-ch2-jeanpaul-wrapup
}
interact-ch2-jeanpaul-wrapup {
	set entity "%SELF%" interact_script to interact-ch2-jeanpaul
}

interact-ch2-frankie {
	copy face-player
	if (variable ch2-storyflag-round is < 3) {
		show dialog {
			SELF "TODO (generic)"
		}
	} else if (variable ch2-storyflag-round is 3) {
		if (flag ch2-want-ramchips is false) {
			mutate CALLBACK = 2
			goto interact-ch2-room21-split
		} else {
			if (flag interrupt is true) {
				show dialog {
					SELF "Sea Moss sure loves his snacks."
					"Because... you know."
				}
			} else {
				copy interact-ch2-room21-split-s
			}
		}
	} else {
		show dialog {
			SELF "TODO (Good luck building computer)"
		}
	}
	goto interact-ch2-frankie-wrapup
}
interact-ch2-frankie-wrapup {
	set entity "%SELF%" interact_script to interact-ch2-frankie
}

interact-ch2-room21-split {
	turn entity Frankie toward entity "%PLAYER%"
	turn entity Jean-Paul toward entity "%PLAYER%"
	show dialog {
		PLAYER "So I'm rebuilding the castle mainframe, and I heard you guys might be able to help me get some RAM. Lambda couldn't find any to spare."
		Jean-Paul "Lambda? Who's that?"
		PLAYER "He's someone helping me.\n(TODO re: Lambda)"
		"Anyway, if you don't have any RAM I can use, do you have any ideas for a substitute? It's okay if it's... abstract."
		Jean-Paul "\"Abstract?\""
		PLAYER "Yeah, so like what even is RAM?"
		Jean-Paul "It's a computer's working memory. Any programs that are running, or images or text that is loaded, or anything at all...."
		"All that data needs somewhere to sit while it's being worked on."
		Frankie "Or shown on a screen."
		PLAYER "Right, so for an... abstract... computer, what could work instead? A notebook? A jelly bean jar?"
		Jean-Paul "How abstract are we talking?"
		PLAYER "Well, so far I've made a hard disk from a dinner plate and a phonograph needle."
		Jean-Paul "...Yikes."
	}
	// They all turn toward each other
	show dialog {
		Jean-Paul "To be honest, the guy who can probably help best isn't here right now."
		Frankie "He's a bit distractable."
		Jean-Paul "A bit?!"
		Frankie "Yeah, maybe that was an understatement! But he was away when the earthquake hit. Don't know where he is, but wherever he is, he's stuck in that room."
		PLAYER "Well, who is he? What does he look like?"
		Jean-Paul "His name is Sea Moss, and he's a giant stone golem. Can't miss him."
	}
	if (flag ch2-seamoss-backstory is true) {
		show dialog {
			PLAYER "Wait, I think I saw that guy. He was in the grand hall."
			Frankie "No surprise there!"
			Jean-Paul "Yep, that's him."
		}
	} else {
		show dialog {
			Frankie "He's almost certainly near the kitchen. East wing. That's the first place I'd look."
		}
	}
	show dialog {
		Frankie "He'll definitely be able to find you an... abstract... solution."
		Jean-Paul "Good luck, kid."
	}
	set flag ch2-want-ramchips to true
	copy ch2-handoff-ramchips-to-seamoss
	set flag interrupt to true
	if (variable CALLBACK is 1) {
		mutate CALLBACK = 0
		goto interact-ch2-jeanpaul-wrapup
	} else if (variable CALLBACK is 2) {
		mutate CALLBACK = 0
		goto interact-ch2-frankie-wrapup
	} else {
		show serial dialog {
			"<r>DEBUG</>: Something went wrong splitting into entity wrapup scripts after shared behavior in 'interact-ch2-room21-split'! 'CALLBACK' value: $CALLBACK$"
		}
	}
}

interact-ch2-room21-split-s {
	turn entity Jean-Paul toward entity "%PLAYER%"
	turn entity Frankie toward entity "%PLAYER%"
	show dialog {
		PLAYER "So... I may have forgotten where I can find Sea Moss."
		Jean-Paul "Best place to look is in the grand hall, or near the kitchen, or somewhere like that. All those rooms are in the east wing."
		PLAYER "And he'll be able to find me a substitute for RAM?"
		Frankie "If anyone can, he can. He's good at being... abstract."
	}
}
