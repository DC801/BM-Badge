include!("header.mgs")

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-34 {
	show serial dialog spacer;
	show serial dialog {
		"TODO: CASTLE PANTRY on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-34 {
	// ch2 room state
	mutate ch2-in-room = 34;

	// entrance text
	show serial dialog spacer;
	if (flag ch2-seen-room-34 is false) {
		show serial dialog { "Discovered the <bold><c>CASTLE PANTRY</>! Room added to map!" }
		set flag ch2-seen-room-34 to true;
	} else {
		show serial dialog { "Entering the <bold>CASTLE PANTRY</>..." }
	}
	if (
		flag ch2-installed-plate is true
		|| flag ch2-carrying-plate is true
	) {
		copy script ch2-hide-plate;
	}
	
	copy ch2-map-init;
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-34 {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to walk_from-northeast;
		load map ch2-castle-32;
	}
}

tick-ch2-clara {
	wait 13s;
	walk entity "%SELF%" to geometry walk1 over 600ms;
	wait 5s;
	turn entity "%SELF%" west;
	wait 9s;
	turn entity "%SELF%" south;
	wait 5s;
	walk entity "%SELF%" to geometry walk2 over 600ms;
	wait 4s;
	turn entity "%SELF%" west;
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-34 { // sanitize ch2 room state
	mutate ch2-in-room = 0;
}

// SERIAL: go

on_go-castle-34-south {
	copy on_exit-ch2-castle-34;
	copy warping-out-fade-out;
	load map ch2-castle-32;
}

/* ---------- ENTITIES ---------- */

snake_box-interact {
	show dialog {
		PLAYER "Snake, is that you?"
		name "Solid Snek" portrait "codec-snek" border_tileset "codec"
		"There's no snake here. Check a different box. Just not this one."
		PLAYER "But this is the only talking box here."
	}
}

ch2-interact-pantryfridge {
	show dialog {
		PLAYER "Meats and fruits and vegetables... oh my."
	}
}
ch2-interact-pantryshelf {
	show dialog {
		PLAYER "Looks like a lot of dry and canned goods."
	}
}
ch2-interact-dishshelf {
	show dialog {
		PLAYER "Plates, cups, and tablecloths. How many place settings is this?"
	}
}
ch2-interact-winerack {
	show dialog {
		PLAYER "Is this just bottles of wine? It all looks very fancy."
	}
}

interact-ch2-clara {
	copy interrupt-walk;
	if (flag ch2-clara-backstory is true) {
		show dialog {
			SELF "Holy crap -- a new person!"
			PLAYER "Yup, that's me."
		}
		set flag ch2-clara-backstory to true;
	} else {
		show dialog {
			SELF "(TODO)"
		}
	}
	copy resume-walk;
}

/* ---------- HARDDRIVE (ADMIN) ---------- */

ch2-hide-plate {
	teleport entity "Plate" to geometry hiding-spot;
	set entity "Plate" name to " ";
}

ch2-touch-plate {
	if (flag ch2-plate-firstlook is false) {
		show dialog {
			PLAYER "TODO plate1"
		}
		set flag ch2-plate-firstlook to true;
	}
	show dialog {
		PLAYER "TODO plate2."
	}
	if (variable ch2-storyflag-round is >= 2) {
		show dialog {
			name "" "(Pick up the plate?)"
			> "Yes" : ch2-touch-plate-yes
			> "No" : ch2-touch-plate-no
		}
	}
}
ch2-touch-plate-yes {
	show dialog {
		name "" "(You pick up the plate!)"
	}
	copy script ch2-hide-plate;
	copy script ch2-pickup-plate;
	set entity "%SELF%" on_interact to null_script;
}
ch2-touch-plate-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" on_interact to ch2-touch-plate;  // reset after script jump
}
