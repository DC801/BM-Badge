settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-34 {
	show serial dialog spacer
	show serial dialog {
		"TODO: CASTLE PANTRY on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-34 {
	// ch2 room state
	mutate ch2-in-room = 34

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-34 is false) {
		show serial dialog { "Discovered the <bold><c>CASTLE PANTRY</>! Room added to map!" }
		set flag ch2-seen-room-34 to true
	} else {
		show serial dialog { "Entering the <bold>CASTLE PANTRY</>..." }
	}
	if (
		flag ch2-installed-plate is true
		|| flag ch2-carrying-plate is true
	) {
		copy script ch2-hide-plate
	}
	
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-34 {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to walk_from-northeast
		load map ch2-castle-32
	}
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-34 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-34-south {
	copy on_exit-ch2-castle-34
	copy warping-out-fade-out
	load map ch2-castle-32
}

/* ---------- HARDDRIVE (ADMIN) ---------- */

ch2-hide-plate {
	teleport entity "Plate" to geometry hiding-spot
	set entity "Plate" name to " "
}

ch2-touch-plate {
	if (flag ch2-plate-firstlook is false) {
		show dialog {
			PLAYER "TODO plate1"
		}
		set flag ch2-plate-firstlook to true
	}
	show dialog {
		PLAYER "TODO plate2."
	}
	if (variable ch2-storyflag-round is >= 2) {
		show dialog {
			name "" "(Pick up the plate?)"
			> "Yes" : ch2-touch-plate-yes
			> "No" : ch2-touch-plate-no
		}
	}
}
ch2-touch-plate-yes {
	show dialog {
		name "" "(You pick up the plate!)"
	}
	copy script ch2-hide-plate
	copy script ch2-pickup-plate
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-plate-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-plate  // reset after script jump
}


snake_box-interact {
	show dialog {
		PLAYER "Snake is that you?"
		name "Solid Snek"
		portrait "codec-snek"
		border_tileset "codec"
			"There's no snake here. Check a different box. Just not this one."
		PLAYER "But this is the only talking box here."
	}
}
