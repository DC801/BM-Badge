settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
	parameters for label GBob { entity "Guardian Bob" }
}
settings for serial dialog { wrap 60 }

// (NOT LIVE YET)

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-bobsclub-basement {
	show serial dialog spacer
	show serial dialog {
		"TODO: Bob's Club basement text"
	}
}

look-ch2-bobaustin {
	show serial dialog spacer
	show serial dialog {
		"You looked at <m>%Bob Austin%</>."
		" "
		"TODO: Bob Austin on_look"
	}
}
look-ch2-bobmoss {
	show serial dialog spacer
	show serial dialog {
		"You looked at <m>%Bob Moss%</>."
		" "
		"TODO: Bob Moss on_look"
	}
}
look-ch2-icechest {
	show serial dialog spacer
	show serial dialog {
		"You looked at the <m>%ice chest%</>."
		" "
		"TODO: Ice Chest on_look"
	}
}
look-ch2-marathonbob {
	show serial dialog spacer
	show serial dialog {
		"You looked at one of the <m>%Marathon Bob%s</>."
		" "
		"TODO: Marathon Bob on_look"
	}
}
look-ch2-guardianbob {
	show serial dialog spacer
	show serial dialog {
		"You looked at <m>%Guardian Bob%</>."
		" "
		"TODO: Guardian Bob on_look"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-bobsclub-basement {
	// do unique stuff first (but turn serial / player control off first, if you do)
	if (flag ch2-cutseen-bob-party is false) {
		goto ch2-cutscene-bob-party
	} else {
		teleport entity "Guardian Bob" to geometry bob-walk2
		set entity "Guardian Bob" tick_script to loiter-slow
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-bobsclub-basement {
	goto null_script
}

on_tick-turn-right-and-down-kinda {
	wait 4500ms
	turn entity "%SELF%" south
	wait 2400ms
	turn entity "%SELF%" east
	wait 800ms
}
on_tick-turn-right-and-down-kinda2 {
	wait 3300ms
	turn entity "%SELF%" south
	wait 3820ms
	turn entity "%SELF%" east
	wait 3800ms
}
on_tick-turn-left-and-down-kinda {
	wait 2200ms
	turn entity "%SELF%" south
	wait 3200ms
	turn entity "%SELF%" west
	wait 2800ms
}
on_tick-turn-left-and-down-kinda2 {
	wait 1310ms
	turn entity "%SELF%" south
	wait 2700ms
	turn entity "%SELF%" west
	wait 4900ms
}
on_tick-turn-right-and-up-kinda {
	wait 400ms
	turn entity "%SELF%" east
	wait 3100ms
	turn entity "%SELF%" north
	wait 5840ms
}


/* ---------- EXIT STUFF ---------- */

on_go-ch2-bobsclub-basement-stairs {
	copy warping-out-fade-out
	load map ch2-bobsclub
}

/* ---------- CUTSCENE ---------- */

ch2-cutscene-bob-party {
	// Fade in.
	teleport camera to geometry gasp-spot
	if (warp state is warping) {
		// this is here because the cutscene must come after the warp-in thing
		copy warping-in-fade-in
		set flag ch2-manual-warp to true // this will skip the "init" script's warp stuff
	}
	set hex control off
	// set serial control off
	set player control off
	wait 1500ms
	set entity "%PLAYER%" tick_script to look-left-and-right-fast
	wait 700ms
	show dialog {
		PLAYER "Huh. Well, I found the source of the noise...."
	}
	set entity "%PLAYER%" tick_script to null_script
	turn entity "%PLAYER%" toward entity "Guardian Bob"
	show dialog {
		GBob "Hey! You're not allowed in here!"
	}
	walk entity "Guardian Bob" along geometry bob-walk over 1200ms
	wait 800ms
	show dialog {
		GBob "Oh, wait. I remember you. Your name was Bob, wasn't it?"
	}
	if (entity "%PLAYER%" name is "Bob") {
		show dialog {
			PLAYER "Oh, I guess it is."
		}
	} else {
		show dialog {
			PLAYER "Um, technically, it WAS Bob...."
			"(At one point....)"
		}
	}
	show dialog {
		GBob "Oh, well. Too late, regardless."
	}
	set entity "Guardian Bob" tick_script to spin_self_clockwise
	show dialog {
		GBob "Welcome to the annual Bob's Club party."
		PLAYER "I thought the party was canceled this year."
	}
	set entity "Guardian Bob" tick_script to null_script
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	show dialog {
		GBob "It was, but... we seem to be experiencing a time anomaly."
		PLAYER "What? A time anomaly?"
		GBob "Yeah. The flow of time is being simultaneously compressed and expanded."
		"The Bob's Club party was canceled this year, but simultaneously, this is a year where it WASN'T canceled."
		"Your 16th birthday, for another example. When did you get chosen by Ring Zero?"
		PLAYER "Yesterday."
		GBob "Was it yesterday? Or was it actually a few years ago?"
	}
	wait 100ms
	// (turn animations backward)
	set entity "%PLAYER%" relative_direction to 2
	walk entity "%PLAYER%" to geometry gasp-spot over 200ms
	// (turn animations forward)
	set entity "%PLAYER%" relative_direction to 0
	turn entity "%PLAYER%" toward entity "Guardian Bob"
	show dialog {
		PLAYER "Wait. What?"
	}
	turn entity "Guardian Bob" south
	show dialog {
		GBob "That's what makes this an anomaly. It wasn't yesterday, and it wasn't a few years ago. It was BOTH."
	}
	turn entity "%PLAYER%" south
	show dialog {
		PLAYER "That's... impossible."
		"What does that even mean?"
		GBob "It means someone is messing with reality."
	}
	turn entity "%PLAYER%" toward entity "Guardian Bob"
	show dialog {
		PLAYER "The Big Bad?"
	}
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	show dialog {
		GBob "Probably."
	}
	wait 100ms
	set entity "%PLAYER%" tick_script to spin_self_clockwise
	wait 240ms
	show dialog {
		PLAYER "This is freaking me out. How do we stop it?"
	}
	turn entity "Guardian Bob" south
	set entity "%PLAYER%" tick_script to null_script
	turn entity "%PLAYER%" toward entity "Guardian Bob"
	show dialog {
		GBob "Depends on the exact cause. I've been keeping my eye on it with my keytool, Glitch. Trying to investigate from several angles."
		"But I don't have a lot of power in here."
	}
	turn entity "Guardian Bob" toward entity "%PLAYER%"
	show dialog {
		GBob  "Honestly, Bob, the sooner you can stop the Big Bad, the better."
		"No pressure!"
	}
	wait 80ms
	turn entity "Guardian Bob" north
	wait 400ms
	show dialog {
		GBob "Anyway, I'll keep watch here."
		"Help yourself to the food. There's plenty."
	}
	if (flag ch2-want-cactuscooler is true) { // TODO set this flag when you hear request from heat sink guys
		show dialog {
			PLAYER "Oh, do you have any Cactus Cooler?"
		}
		turn entity "Guardian Bob" toward entity "%PLAYER%"
		show dialog {
			GBob "Sure. We've got a lifetime supply in the ice chest back there. Help yourself."
		}
	}
	set flag ch2-cutseen-bob-party to true
	wait 150ms
	walk entity "Guardian Bob" to geometry bob-walk2 over 800ms
	set entity "Guardian Bob" tick_script to loiter-slow
	make camera follow entity "%PLAYER%"
	goto ch2-map-init
}

loiter-slow {
	turn entity "%SELF%" south /**/ wait 3200ms
	turn entity "%SELF%" east /**/ wait 2000ms
	turn entity "%SELF%" west /**/ wait 4400ms
}

/* ---------- ENTITIES ---------- */

interact-ch2-guardianbob {
	copy face-player
	show dialog {
		PLAYER "So why is the party in the basement? I thought it was going to be upstairs. There were decorations half set up and everything."
		SELF "Well, we WERE going to have the party upstairs, but someone broke in and pretended to be named Bob. After that we upped our security."
		PLAYER "...Oh yeah...?"
		SELF "Yeah, this guy Strong Bad. The photo on his fake ID looked nothing like him, but Bob Austin let him in anyway."
		PLAYER "...No kidding."
		SELF "I've been manually teleporting Bobs down here ever since just to be extra sure. My teleport program is very strict. It won't let non-Bobs through at all."
		PLAYER "Good to know."
	}
}

interact-ch2-bobaustin {
	copy face-player
	show dialog {
		SELF "I'm the MC for Bob Jeopardy."
		"TODO"
	}
	turn entity "%SELF%" south
}

interact-ch2-bobmoss {
	copy face-player
	show dialog {
		SELF "TODO"
	}
	turn entity "%SELF%" south
}

interact-ch2-marathonbob {
	// capture original facing direction
	copy pause-walk-and-face-player
	// normal behavior
	mutate random ? 10
	// https://www.youtube.com/watch?v=7dpxLPbamnA for BoB quotes
	if (variable random is 1) {
		show dialog { SELF "They're everywhere!" "Us Bobs, that is. We're everywhere!" }
	} else if (variable random is 2) {
		show dialog { SELF "Ah! Watch it!" "You stepped on my foot!" }
	} else if (variable random is 3) {
		show dialog { SELF "Follow me!" "Let's start a conga line!" }
	} else if (variable random is 4) {
		show dialog { SELF "AAHHHH!" "I bit my tongue!" }
	} else if (variable random is 5) {
		show dialog { SELF "Get me outta here." "This dubstep is too much." }
	} else if (variable random is 6) {
		show dialog { SELF "Hey! He's shooting at us!" "That DSLR looks nice. Wonder where he got it." }
	} else if (variable random is 7) {
		show dialog { SELF "Perimeter secured." "No non-Bobs here!" }
	} else if (variable random is 8) {
		show dialog { SELF "All right!" "This is the best Bob's Club party yet!" }
	} else if (variable random is 9) {
		show dialog { SELF "Thank god it's you!" "Another Bob!" }
	} else {
		show dialog { SELF "Frog blast the vent core!" "That's the name of my favorite drink." }
	}
	// turn back to the original facing direction
	copy unface-player-and-resume-walk
	mutate tempvar = 0 // courtesy reset
}

interact-ch2-icechest {
	if (flag ch2-carrying-cactuscooler is true) {
		if (
			flag interrupt is false // you didn't just pick it up
			|| flag ch2-want-cactuscooler is false // OR you don't know you need it
		) {
			show dialog {
				PLAYER "I won't need more Cactus Cooler than this. I'll leave the rest for everyone else."
			}
		} else {
			// if you did just pick it up AND you know you need it
			show dialog {
				PLAYER "Got my can of Cactus Cooler. Time to bring it to the power plant guys."
			}
		}
	} else if (flag ch2-cactuscooler-delivered is true) {
		// you've already turned the soda in
		show dialog {
			PLAYER "The power plant guys already have their Cactus Cooler. I'll leave the rest for everyone else."
		}
	} else { // You get to pick up a Cactus Cooler!
		if (flag ch2-want-cactuscooler is false) {
			show dialog { PLAYER "This really is a lot of Cactus Cooler. Guess I'll grab a can for the road." }
		} else {
			show dialog { PLAYER "Bingo! Cactus Cooler!" }
		}
		show dialog {
			name "" "(You pick up the can of Cactus Cooler!)"
		}
		copy ch2-pickup-cactuscooler
		set flag interrupt to true // you just picked it up
	}
}
