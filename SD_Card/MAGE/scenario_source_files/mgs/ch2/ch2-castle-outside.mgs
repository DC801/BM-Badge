include!("header.mgs")

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-outside {
	show serial dialog spacer;
	show serial dialog {
		"You looked around the <c>CASTLE EXTERIOR</>."
		"\tThe building itself is undeniably ominous with its stark, charcoal-colored stone. You can just make out that the green orbs of light embedded in its walls are flickering slightly. A subtle humming emanates from the nearby pillars. It is no surprise that the townsfolk avoid this place."
		" "
	}
}
look-ch2-entrancesign {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tA worn wooden sign inscribed with the words \"King Gibson's castle.\""
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-outside {
	turn player control off;
	if (flag ch2-full-escort-cutseen is false) {
		// todo: cutscene: leading you inside
	}
	// entrance text
	show serial dialog spacer;
	show serial dialog { "Entering <bold>CASTLE EXTERIOR</>..." }
	// Village elders hiding
	if (variable ch2-storyflag-round is not 0) {
		set entity Bert on_tick to yeet;
	}
	if (
		variable ch2-storyflag-round is 1
		|| variable ch2-storyflag-round is 2
		|| variable ch2-storyflag-round is 3
		|| variable ch2-storyflag-round is >= 5
	) {
		set entity Jackob on_tick to yeet;
		set entity Alfonso on_tick to yeet;
	}
	// unique stuff done
	copy ch2-map-init; // (player control comes back here)
	// animations
	if (warp state is walk_from-north) {
		walk entity "%PLAYER%" along geometry walk_from-north over 200ms
	} else if (warp state is walk_from-east) {
		set entity "%PLAYER%" path to walk_from-east;
		set entity "%PLAYER%" on_tick to on_tick-ch2-town-walkfadein;
			// borrowing this from town scripts
		fade in camera from #000000 over 400ms;
	} else if (warp state is ch2-ending) {
		goto ch2-post-credits;
	}
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-outside {
	if (entity "%PLAYER%" is inside geometry north-hitbox) {
		copy on_exit-ch2-castle-outside;
		set warp state to walk_from-south;
		copy ch2-load-castle-1-special;
	}
	if (entity "%PLAYER%" is inside geometry east-hitbox) {
		copy on_exit-ch2-castle-outside;
		set warp state to walk_from-west;
		set entity "%PLAYER%" path to east-point;
		set entity "%PLAYER%" on_tick to on_tick-ch2-town-walkfadeout;
			// borrowing this from town scripts
		fade out camera to #000000 over 500ms;
		load map ch2-town;
	}
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-outside {
	// universal behavior
}

// SERIAL: go

on_go-castle-outside-north {
	copy on_exit-ch2-castle-outside;
	copy warping-out-fade-out;
	copy ch2-load-castle-1-special;
}
on_go-castle-outside-east {
	copy on_exit-ch2-castle-outside;
	copy warping-out-fade-out;
	load map ch2-town;
}

/* ---------- ENTITIES ---------- */

interact-ch2-entrancesign {
	show dialog {
		name "" "King Gibson's castle"
	}
}

interact-ch2-alfonso-outside {
	if (variable ch2-storyflag-round is 4) {
		if (flag ch2-alfonso-almostdone is false) {
			show dialog {
				SELF "Word on the street is that you're almost done repairing the artifact." "Very well done so far, but keep going!"
			}
			set flag ch2-alfonso-almostdone to true;
		} else {
			show dialog {
				SELF "Keep going! We need that artifact working!"
			}
		}
	} else {
		show dialog {
			SELF "Go on in! You'll do fine!"
		}
	}
}

interact-ch2-bert-outside {
	show dialog {
		SELF "Only hackers may cross door into castle."
	}
}

interact-ch2-jackob-outside {
	if (variable ch2-storyflag-round is 4) {
		if (flag ch2-jackob-almostdone is false) {
			show dialog {
				SELF "There's a buzz around town that you're nearly finished. Very impressive."
			}
			set flag ch2-jackob-almostdone to true;
		}
		show dialog {
			SELF "Don't stop now. Get that software installed quick!"
		}
	} else {
		show dialog {
			SELF "XA will be able to help you on the inside. Good luck."
		}
	}
}

interact-ch2-pedestal {
	show dialog {
		PLAYER "What a strange symbol. Like a trident, but with different shapes on the tips of the tines: a square, triangle, and circle."
		"But what does it mean?" // Frontier psychiatry
	}
}

interact-ch2-pillar {
	show dialog {
		PLAYER "What do these things do?"
	}
}
