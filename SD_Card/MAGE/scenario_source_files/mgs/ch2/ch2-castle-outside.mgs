settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-outside {
	show serial dialog spacer
	show serial dialog {
		"   TODO: castle room exterior on_look text"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-outside {
	set player control to off
	if (flag ch2-full-escort-cutseen is false) {
		// TODO: cutscene: leading you inside
	}
	// Village elders hiding
	if (variable ch2-storyflag-round is not 0) {
		set entity Bert tick_script to yeet
		}
	if (
		variable ch2-storyflag-round is 1
		|| variable ch2-storyflag-round is 2
		|| variable ch2-storyflag-round is 3
		|| variable ch2-storyflag-round is >= 5
	) {
		set entity Jackob tick_script to yeet
		set entity Alfonso tick_script to yeet
		}
	if (debug mode is on) {
		show serial dialog {
			"<g>DEBUG</>: ch2-storyflag-round = $ch2-storyflag-round$"
		}
	}
	// animations
	if (warp state is walk_from-north) {
		walk entity "%PLAYER%" along geometry walk_from-north over 200ms
	} else if (warp state is walk_from-east) {
		set entity "%PLAYER%" path to walk_from-east
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
			// borrowing this from town scripts
		fade in camera from #000000 over 400ms
	}
	// unique stuff done
	goto ch2-map-init // (player control comes back here)
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-outside {
	if (entity "%PLAYER%" is inside geometry north-hitbox) {
		copy on_exit-ch2-castle-outside
		set warp state to walk_from-south
		load map ch2-castle-1
	}
	if (entity "%PLAYER%" is inside geometry east-hitbox) {
		copy on_exit-ch2-castle-outside
		set warp state to walk_from-west
		set entity "%PLAYER%" path to east-point
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
			// borrowing this from town scripts
		fade out camera to #000000 over 500ms
		load map main2
	}
	set map tick_script to on_tick-ch2-castle-outside // reset
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-outside {
	// universal behavior
}

// SERIAL: go

on_go-castle-outside-north {
	copy on_exit-ch2-castle-outside
	copy warping-out-fade-out
	load map ch2-castle-1
}
on_go-castle-outside-east {
	copy on_exit-ch2-castle-outside
	copy warping-out-fade-out
	load map main2
}

/* ---------- ENTITIES ---------- */

interact-ch2-alfonso-outside {
	if (variable ch2-storyflag-round is 4) {
		if (flag ch2-alfonso-almostdone is false) {
			show dialog {
				SELF "Word on the street is that you're almost done repairing the artifact." "Very well done so far, but keep going!"
			}
			set flag ch2-alfonso-almostdone to true
		} else {
			show dialog {
				SELF "Keep going! We need that artifact working!"
			}
		}
	} else {
		show dialog {
			SELF "Go on in! You'll do fine!"
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-alfonso-outside // RESET
}

interact-ch2-bert-outside {
	show dialog {
		SELF "Only hackers may cross door."
	}
}

interact-ch2-jackob-outside {
	if (variable ch2-storyflag-round is 4) {
		if (flag ch2-jackob-almostdone is false) {
			show dialog {
				SELF "There's a buzz around town that you're nearly finished. Very impressive."
			}
			set flag ch2-jackob-almostdone to true
		}
		show dialog {
			SELF "Don't stop now. Get that software installed quick!"
		}
	} else {
		show dialog {
			SELF "XA will be able to help you on the inside. Good luck."
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-jackob-outside // RESET
}

