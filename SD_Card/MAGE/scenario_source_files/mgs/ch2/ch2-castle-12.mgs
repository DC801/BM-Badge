include!("header.mgs")

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-12 {
	show serial dialog spacer;
	show serial dialog {
		"You looked around the <c>CASTLE HALLWAY BACK</>."
		"\tThe rubble from the ceiling has split this room into two, but the air is a little stuffier here than it had been in the other half. Perhaps more was destroyed on this side, more dust scattered and more moisture entrapped, or perhaps it's a sign that you're come deeper into the mountain."
		" "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-12 {
	// ch2 room state
	mutate ch2-in-room = 12;
	
	// entrance text
	show serial dialog spacer;
	if (flag ch2-seen-room-12 is false) {
		show serial dialog { "Discovered <bold><c>CASTLE HALLWAY BACK</>! Room added to map!" }
		set flag ch2-seen-room-12 to true;
	} else {
		show serial dialog { "Entering <bold>CASTLE HALLWAY BACK</>..." }
	}
	
	if (
		flag ch2-installed-needle is true
		|| flag ch2-carrying-needle is true
	) {
		copy script ch2-hide-needle;
	}
	goto ch2-map-init;
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-12 {
	goto null_script;
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-12 { // sanitize ch2 room state
	mutate ch2-in-room = 0;
}

// SERIAL: go

on_go-castle-12-north {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-13;
}
on_go-castle-12-east {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-31;
}
on_go-castle-12-south {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-11;
}


/* ---------- NEEDLE (ADMIN) ---------- */

ch2-hide-needle {
	teleport entity "Needle" to geometry hiding-spot;
	set entity "Needle" name to " ";
}

ch2-touch-needle {
	if (flag ch2-needle-firstlook is false) {
		show dialog {
			PLAYER "TODO needle1"
		}
		set flag ch2-needle-firstlook to true;
	}
	show dialog {
		PLAYER "TODO needle2."
	}
	if (variable ch2-storyflag-round is >= 2) {
		show dialog {
			name "" "(Pick up the phonograph needle?)"
			> "Yes" : ch2-touch-needle-yes
			> "No" : ch2-touch-needle-no
		}
	}
}

ch2-touch-needle-yes {
	show dialog {
		name "" "(You pick up the needle!)"
	}
	copy script ch2-hide-needle;
	copy script ch2-pickup-needle;
	set entity "%SELF%" on_interact to null_script;
}
ch2-touch-needle-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" on_interact to ch2-touch-needle;  // reset after script jump
}
