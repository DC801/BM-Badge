include!("header.mgs")
settings for dialog {
	label Concierge {
		entity Concierge
		portrait concierge
	}
}

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-12 {
	show serial dialog spacer;
	show serial dialog {
		"You looked around the <c>CASTLE HALLWAY BACK</>."
		"\tThe rubble from the ceiling has split this room into two, but the air is a little stuffier here than it had been in the other half. Perhaps more was destroyed on this side, more dust scattered and more moisture entrapped, or perhaps it's a sign that you're come deeper into the mountain."
		" "
	}
}

look-ch2-concierge {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tThe man is professionalism itself, from his well-fitten suit and his professional body language -- apart from the expression on his face, which is incongruously aggressive. You think you can make out the sound of growling."
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-12 {
	// ch2 room state
	mutate ch2-in-room = 12;
	
	// entrance text
	show serial dialog spacer;
	if (flag ch2-seen-room-12 is false) {
		show serial dialog { "Discovered <bold><c>CASTLE HALLWAY BACK</>! Room added to map!" }
		set flag ch2-seen-room-12 to true;
	} else {
		show serial dialog { "Entering <bold>CASTLE HALLWAY BACK</>..." }
	}

	// yeet hidden things
	if (
		flag ch2-installed-needle is true
		|| flag ch2-carrying-needle is true
	) {
		copy script ch2-hide-needle;
	}
	
	// initialize everything
	copy ch2-map-init;

	// check if you do the cutscene or not
	if (flag ch2-cutseen-castle12 is false) {
		show dialog {
			Concierge "Welcome!"
		}
		set flag interrupt to true;
		set flag ch2-cutseen-castle12 to true;
	}
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-12 {
	// nothing yet
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-12 { // sanitize ch2 room state
	mutate ch2-in-room = 0;
}

// SERIAL: go

on_go-castle-12-north {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-13;
}
on_go-castle-12-east {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-31;
}
on_go-castle-12-south {
	copy on_exit-ch2-castle-12;
	copy warping-out-fade-out;
	load map ch2-castle-11;
}

/* ---------- ENTITIES ---------- */

ch2-interact-guestbook {
	if (flag ch2-guestbook-signed is false) {
		if (flag interrupt3 is false) {
			show dialog {
				PLAYER "Huh? It's just a book with a bunch of people's names."
				Concierge "Oh, yes! Do sign our guest book before you go!"
			}
			set flag interrupt3 to true;
		}
		show dialog {
			name "" "(Sign the guest book?)"
			> "Yes" : ch2-interact-guestbook-signyes
			> "No" : ch2-interact-guestbook-signno
		}
	} else {
		show dialog {
			PLAYER "Ah, there's my name, logged for the ages."
			Concierge "Thank you for signing the guest book! I hope you enjoy the remainder of your stay!"
		}
	}
}
ch2-interact-guestbook-signyes {
	show dialog {
		PLAYER "All right, here I go...."
		name "" "(scribble scribble scribble)"
		Concierge "Thank you for signing!"
	}
	set flag ch2-guestbook-signed to true;
	set entity "%SELF%" on_interact to ch2-interact-guestbook;
}
ch2-interact-guestbook-signno {
	show dialog {
		PLAYER "Maybe later."
	}
	set entity "%SELF%" on_interact to ch2-interact-guestbook;
}

interact-ch2-concierge {
	if (flag ch2-concierge-backstory is false) {
		if (flag interrupt is true) {
			show dialog {
				PLAYER "Welcome?"
				Concierge "Yes, welcome to King Gibson's castle. I do hope you enjoy your visit!"
			}
		} else {
			show dialog {
				PLAYER "Welcome to King Gibson's castle. I do hope you enjoy your visit!"
			}
		}
		show dialog {
			PLAYER "Um, thanks. Listen, I know things are messed up right now...."
			Concierge "Fear not! I am confident everyone is working to clear the doors and restore the intercom system."
			"You, at least, seem to have overcome the door problem on your own, and should be applauded for your resourcefulness."
			"You have my full support -- though alas, the best I can do to help is keep this phonograph turning."
			"You'll have moral support music, if nothing else!"
		}
		set flag ch2-concierge-backstory to true;
	} else if (
		variable ch2-storyflag-round is < 2
		|| flag ch2-needle-permission is true
	) {
		show dialog {
			Concierge "In trying times, we may find ourselves with limited resources or a constrictive environment."
			"How admirable it is to acquire and leverage one's limited resources and perform great work anyway!"
			"I'll be cheering you on from here. Good luck!"
		}
	} else {
		show dialog {
			PLAYER "(TODO: I want needle)"
			Concierge "(TODO: well, you've convinced me)"
		}
		set flag interrupt2 to true;
		set flag ch2-needle-permission to true;
	}
}

/* ---------- NEEDLE (ADMIN) ---------- */

ch2-hide-needle {
	set entity Phonograph secondary_id to 0;
}

ch2-touch-needle {
	if (flag ch2-carrying-needle is true || flag ch2-installed-needle is true) {
		show dialog ch2-concierge-old-timey { // there's a second reference to this dialog below!
			PLAYER "Definitely an old-timey look, isn't it?"
			Concierge "If you like this, you should see King Gibson's bedroom."
		}
		return;
	}
	if (flag interrupt2 is false) {
		if (flag ch2-needle-firstlook is false) {
			show dialog {
				PLAYER "That's a funny looking trumpet."
				Concierge "It mechanically amplifies the vibrations captured by the needle upon the vinyl record."
				"I got this old thing out after the earthquake damaged the intercom system. I couldn't bear how quiet it was in here."
			}
			set flag ch2-needle-firstlook to true;
		} else {
			show dialog ch2-concierge-old-timey; // dialog defined above
		}
	}
	if (variable ch2-storyflag-round is >= 2) {
		if (flag ch2-needle-permission is true) {
			show dialog {
				name "" "(Pick up the phonograph needle?)"
				> "Yes" : ch2-touch-needle-yes
				> "No" : ch2-touch-needle-no
			}
		} else {
			show dialog {
				PLAYER "(Will the concierge get mad at me if I take the needle from this record player? I guess I should ask.)"
			}
		}
	}
}

ch2-touch-needle-yes {
	show dialog {
		name "" "(You pick up the needle!)"
	}
	copy script ch2-hide-needle;
	copy script ch2-pickup-needle;
	set flag interrupt2 to false;
	set entity "%SELF%" on_interact to ch2-touch-needle;
}
ch2-touch-needle-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" on_interact to ch2-touch-needle;  // reset after script jump
}
