settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-town {
	show serial dialog spacer
	show serial dialog {
		"   TODO: Town text"
	}
}

/* ---------- ON_LOAD ---------- */

ch2-setup-alfonso-verthandi {
	teleport entity Alfonso to geometry alfonso-verthandi-spot
	turn entity Alfonso west
	turn entity Verthandi east
	set entity Alfonso interact_script to interact-ch2-alfonso-verthandi
	set entity Verthandi interact_script to interact-ch2-alfonso-verthandi
}

on_load-ch2-town {
	// cutscene interrupt:
	if (warp state is cutscene-escort) {
		goto ch2-escort-cutscene
	}

	// elders:
	set entity Bert tick_script to yeet // always hiding
	if (variable ch2-storyflag-round is < 5) {
		set entity Jackob tick_script to yeet // always hiding before end
		if (variable ch2-storyflag-round is 2) {
			copy ch2-setup-alfonso-verthandi
		} else {
			set entity Alfonso tick_script to yeet
		}
	}

	// walking out of doorways:
	set player control to off
	if (warp state is exit_from-credits) {
		teleport entity "%PLAYER%" to geometry from-credits-spot
		fade in camera from #000000 over 800ms
	} else if (warp state is walk_from-east) {
		set entity "%PLAYER%" path to walk_from-east
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
		fade in camera from #000000 over 400ms
	} else if (warp state is walk_from-west) {
		set entity "%PLAYER%" path to walk_from-west
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
		fade in camera from #000000 over 400ms
	} else if (warp state is walk_from-bobsclub) {
		walk entity "%PLAYER%" along geometry walk_from-bobsclub over 200ms
	} else if (warp state is walk_from-bakery) {
		walk entity "%PLAYER%" along geometry walk_from-bakery over 200ms
	} else if (warp state is walk_from-lodge) {
		walk entity "%PLAYER%" along geometry walk_from-lodge over 200ms
	} else if (warp state is walk_from-greenhouse) {
		walk entity "%PLAYER%" along geometry walk_from-greenhouse over 200ms
	} else if (warp state is walk_from-magehouse) {
		walk entity "%PLAYER%" along geometry walk_from-magehouse over 200ms
	} else if (warp state is walk_from-oldcouplehouse) {
		walk entity "%PLAYER%" along geometry walk_from-oldcouplehouse over 200ms
	} else if (warp state is walk_from-smithfamily) {
		walk entity "%PLAYER%" along geometry walk_from-smithfamily over 200ms
	} else if (warp state is walk_from-woprhouse) {
		walk entity "%PLAYER%" along geometry walk_from-woprhouse over 200ms
	} else if (warp state is walk_from-bobsclub) {
		walk entity "%PLAYER%" along geometry walk_from-bobsclub over 200ms
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

// map loading scripts

on_tick-ch2-town {
	if (entity "%PLAYER%" is inside geometry door-bakery) {
		load map ch2-bakery
	}
	if (entity "%PLAYER%" is inside geometry door-smithfamily) {
		load map ch2-smithfamily
	}
	if (entity "%PLAYER%" is inside geometry door-greenhouse) {
		load map ch2-greenhouse
	}
	if (entity "%PLAYER%" is inside geometry door-magehouse) {
		load map ch2-magehouse
	}
	if (entity "%PLAYER%" is inside geometry door-lodge) {
		load map ch2-lodge
	}
	if (entity "%PLAYER%" is inside geometry door-oldcouplehouse) {
		load map ch2-oldcouplehouse
	}
	if (entity "%PLAYER%" is inside geometry door-bobsclub) {
		load map ch2-bobsclub
	}
	if (entity "%PLAYER%" is inside geometry door-woprhouse) {
		load map woprhouse // both chapters use the same map
	}
	if (entity "%PLAYER%" is inside geometry east-hitbox) {
		set entity "%PLAYER%" path to east-point
		set warp state to walk_from-west
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
		fade out camera to #000000 over 500ms
		load map main2
	}
	if (entity "%PLAYER%" is inside geometry west-hitbox) {
		set entity "%PLAYER%" path to west-point
		set warp state to walk_from-east
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
		fade out camera to #000000 over 500ms
		load map ch2-castle-outside
	}
	set map tick_script to on_tick-ch2-town // reset
}

on_tick-ch2-town-walkfadeout {
	set player control to off
	walk entity "%PLAYER%" to geometry "%ENTITY_PATH%" over 3500ms
	goto null_script
}
on_tick-ch2-town-walkfadein {
	walk entity "%PLAYER%" along geometry "%ENTITY_PATH%" over 500ms
	set player control to on
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-town {
	// universal behavior
}

on_go-ch2-town-west {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-castle-outside
}
on_go-ch2-town-home {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-magehouse
}
on_go-ch2-town-bakery {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-bakery
}
on_go-ch2-town-greenhouse {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-greenhouse
}
on_go-ch2-town-wopr {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map woprhouse // both chapters use the same map
}
on_go-ch2-town-beatrice {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-oldcouplehouse
}
on_go-ch2-town-smith {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-smithfamily
}
on_go-ch2-town-bobsclub {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-bobsclub
}

/* ---------- ENTITIES ---------- */

ch2-interact-marta {
	if (variable ch2-storyflag-round is 0) {
		show dialog {
			SELF "Oh! You're heading out west, to the dungeon in the mountain?"
			PLAYER "Yeah, the serial dungeon."
			SELF "That old castle always freaked me out a little bit."
			"It's so ancient, and full of such strange humming and clicking noises. I never really want to stay over there for long."
			"...."
			"Oh, I mean... I'm sure it's perfectly safe. Seriously, I hear King Gibson is actually really nice, and --"
			PLAYER "It's okay, I'll face a spooky old castle to protect the town, even if it is a little creepy."
			SELF "You'll do great. Good luck."
		}
	} else if (variable ch2-storyflag-round is <= 2) {
		show dialog {
			SELF "How's it going over there? Learning anything about computers?"
			PLAYER "I guess so. I'm starting to build one, anyway."
			"I've been getting a lot of help, though."
			SELF "That's okay. I'm glad you found a mentor to help you learn and do these things. It's a great jump start to learning a new skill."
			PLAYER "I suppose so."
		}
	} else if (variable ch2-storyflag-round is <= 4) {
		show dialog {
			PLAYER "Hey, %SELF%, I'm looking for Bert. Have you seen him?"
			SELF "Here and there, but not for a while. Do you need him for something?"
			PLAYER "No. Yeah. I don't know."
			"He just said he was going to investigate something, then come right back and tell me what he found out."
			"But I haven't heard anything yet. I'm getting worried."
			SELF "Well, he can't have gotten far. He'll turn up eventually, I'm sure."
			"I'll keep an eye out, though."
		}
	} else {
		show dialog {
			SELF "You've got two artifacts under your belt now. Save some for the rest of us, eh?"
			"In all seriousness, way to go, %PLAYER%!"
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-marta
}

interact-ch2-shepherd {
	if (variable ch2-storyflag-round is 0) {
		show dialog {
			SELF "So, um, I don't really know what's so special about cereal."
			"Like, is the artifact a prize from inside a cereal box?"
			PLAYER "Apparently it's \"serial,\" not \"cereal.\""
			SELF "???" "That's what I said, isn't it? Cereal. Like, granola?"
			PLAYER "No, it's \"serial,\" like... well, I don't know what it's like, but it's not breakfast."
			SELF "Huh, well, I don't really get it, but good luck in the dungeon."
		}
	} else if (variable ch2-storyflag-round is <= 2) {
		show dialog {
			SELF "You know, it's the weirdest thing, but I could've sworn there were only four sheep before."
		}
	} else if (variable ch2-storyflag-round is <= 4) {
		show dialog {
			PLAYER "Hey, have you seen Bert recently?"
			SELF "I saw him coming in and out of the library a few times."
			PLAYER "When was that?"
			SELF "Let's see.... Not for kind of a while, I guess."
			"He sure looked like he was in a hurry though. Especially that last time."
			"He came racing out of the library like something was on fire."
			PLAYER "(Oh my goodness. Was that when he came to see me in the castle?)"
			"(But what happened after that?)"
		}
	} else {
		show dialog {
			SELF "Hey, I heard you built a computer or something. That's so cool! Way to go!"
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-shepherd
}

interact-ch2-delmar {
	copy face-player
	show dialog { SELF "...." }
	rotate entity "%SELF%" 2
}
interact-ch2-verthandi {
	copy face-player
	show dialog {
		SELF "Oh! You're doing the western dungeon today, aren't you?"
		"I believe in you, %PLAYER%!"
	}
	turn entity "%SELF%" south
}

interact-ch2-alfonso-verthandi {
	turn entity Alfonso toward entity "%PLAYER%"
	turn entity Verthandi toward entity "%PLAYER%"
	show dialog {
		Alfonso "You know, miss Verthandi here is very knowledgeable about the nature of reality and our relationship to our digital environment."
	}
	turn entity Verthandi toward entity Alfonso
	show dialog {
		Verthandi "Oh, you're too kind! I am only a hobbyist when it comes to such topics. There is always more to learn."
	}
	turn entity Verthandi toward entity "%PLAYER%"
	show dialog {
		Alfonso "In any case, we are brainstorming things we might do to help you on your quest."
		Verthandi "We will at least wish you luck!"
	}
	turn entity Alfonso toward entity Verthandi
	turn entity Verthandi toward entity Alfonso
}

interact-ch2-jackob-end {
	// can't use a zigzag for this kind of safety check!
	if variable ch2-storyflag-round is not >= 5
		then goto null_script
	copy face-player
	show dialog {
		SELF "Ah, %PLAYER%, well done!"
	}
	if (flag wonthedemo is false) {
		// need to check this again because it's a new script (due to zigzagging)
		if variable ch2-storyflag-round is not >= 5
			then goto null_script
		show dialog {
			SELF "But you know, %PLAYER%, it seems like you still haven't finished chapter 1 in this save file."
			"I'm not angry. Just disappointed."
			"...Just kidding! If the game didn't want you to beat chapter 2 before chapter 1, it should have tried harder to stop you."
			"In all seriousness, do feel free to give chapter 1 a go."
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-jackob-end // RESET
}

interact-ch2-alfonso-end {
	// can't use a zigzag for this kind of safety check!
	if variable ch2-storyflag-round is not >= 5
		then goto null_script
	copy face-player
	show dialog {
		SELF "Good job, %PLAYER%!"
		"Looks like you can have a well-deserved break while we prepare things for the next dungeon."
	}
}
