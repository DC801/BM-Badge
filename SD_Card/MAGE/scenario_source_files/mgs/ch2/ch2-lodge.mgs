include!("header.mgs");

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-lodge {
	show serial dialog spacer;
	show serial dialog {
		"   TODO: Lodge text"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-lodge {
	// do unique stuff first (but turn serial / player control off first, if you do)
	if (variable ch2-storyflag-round is not 1) {
		set entity Alfonso on_tick to yeet;
		set entity Jackob on_tick to yeet;
	}

	if (warp state is walk_from-north) {
		set player control to off;
		walk entity "%PLAYER%" along geometry walk_from-north over 200ms;
	}
	// player control comes on after this next script is over:
	goto ch2-map-init;
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-lodge {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to walk_from-lodge;
		load map main2;
	}
	if (entity "%PLAYER%" is inside geometry north-hitbox) {
		set warp state to walk_from-south;
		load map ch2-lodge-rtfm;
	}
}

look-left-sometimes {
	turn entity "%SELF%" west;
	wait 800ms;
	turn entity "%SELF%" north;
	wait 3800ms;
	turn entity "%SELF%" west;
	wait 500ms;
	turn entity "%SELF%" north;
	wait 2500ms;
}

/* ---------- EXIT STUFF ---------- */

on_go-ch2-lodge-south {
	copy warping-out-fade-out;
	load map main2;
}
on_go-ch2-lodge-north {
	copy warping-out-fade-out;
	load map ch2-lodge-rtfm;
}

/* ---------- ENTITIES ---------- */

interact-ch2-timmy {
	copy face-player;
	if (variable ch2-storyflag-round is 0) {
		show dialog {
			SELF "I love the library! There's so many books here it would take a hundred years to read them all!"
		}
	} else if (variable ch2-storyflag-round is <= 2) {
		show dialog {
			SELF "If you had a race car, what color would you want?"
			"I think red cars are faster than blue cars, usually."
		}
	} else if (variable ch2-storyflag-round is <= 4) {
		show dialog {
			PLAYER "Hey, %SELF%, do you know if Bert came in here recently?"
			SELF "Bert? Who's that?"
			PLAYER "Well, he's one of the village elders. He's the big one, the one who --"
			SELF "Oh yeah, Bert is the one that looks like a bear, right?"
			PLAYER "Bear?"
			SELF "Yeah, bear-Bert!"
			"But you see, I actually don't know if I saw him or not."
			"I was reading."
			PLAYER "Oh, I see, you were busy reading. Yeah, I guess that makes sense. Thanks anyway."
		}
	} else {
		show dialog {
			SELF "So you see... I think... bees are kind of scary."
			"I wasn't stung before, but I don't want them to sting me!"
			"Bees and bugs -- but especially bees -- really freak me out."
		}
	}
	turn entity "%SELF%" east;
}
interact-ch2-cat-lodge {
	show dialog {
		SELF "..."
	}
}

interact-ch2-jackob-library {
	show dialog {
		SELF "I hate geese. Especially that one."
		"It keeps staring at me. I can't concentrate on what I'm doing."
	}
}

const!( $bookcount = 4)

interact-ch2-alfonso-library {
	mutate ch2-alfonso-library-count + 1;
	mutate ch2-alfonso-library-count % $bookcount;
	if (variable ch2-alfonso-library-count is 1) {
		show dialog {
			SELF "Oh, goodness! It's \"The Importance of Being Earnest.\" I remember this play. Twenty years ago, Jackob and I did a reading of it with...."
		}
	} else if (variable ch2-alfonso-library-count is 2) {
		show dialog {
			SELF "Aha, \"A Brief History of Time!\" You know, I've always wanted learn about time and space and the universe and all that. And it isn't very long...."
		}
	} else if (variable ch2-alfonso-library-count is 3) {
		show dialog {
			SELF "Oho, \"The Complete Plays of Gilbert and Sullivan.\" I think I memorized one of their patter songs once. But was it from Pinafore or Penzance...?"
		}
	} else {
		show dialog {
			SELF "Oh, look at that. \"Rendezvous with Rama,\" but what a stunning cover! I don't recognize this edition. Who publizhed this one? Gollancz...?"
		}
	}
	copy face-player;
	show dialog {
		SELF "Yes, yes, I'm getting work done! What does it look like I'm doing?"
	}
	turn entity "%SELF%" north;
}
