settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-32 {
	show serial dialog spacer
	show serial dialog {
		"TODO: CASTLE KITCHEN on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-32 {
	// ch2 room state
	mutate ch2-in-room = 32

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-32 is false) {
		show serial dialog { "Discovered <bold><c>CASTLE KITCHEN</>! Room added to map!" }
		set flag ch2-seen-room-32 to true
	} else {
		show serial dialog { "Entering <bold>CASTLE KITCHEN</>..." }
	}
	if (
		flag ch2-ramchips-dispensed is false
	) {
		copy script ch2-hide-ramchips
	}

	// DEBUG
	if (debug mode is on) {
		register want + chips -> debug-want-chips
		show serial dialog {
			"<g>Debug</> command available: <c>WANT CHIPS</>"
		}
	}
	// END DEBUG
	
	// New shape of scripting: no need to walk along a path, just teleport there! The mage won't go back into the door immediately (apparently)!
	// TODO: update others? Or is it not worth the time?
	if (warp state is walk_from-northeast) {
		teleport entity "%PLAYER%" to geometry walk_from-northeast
	}
	if (warp state is walk_from-northwest) {
		teleport entity "%PLAYER%" to geometry walk_from-northwest
	}
	
	goto ch2-map-init
}

debug-want-chips {
	set flag ch2-want-ramchips to true
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-32 {
	if (entity "%PLAYER%" is inside geometry northwest-hitbox) {
		set warp state to walk_from-south
		load map ch2-castle-33
	}
	if (entity "%PLAYER%" is inside geometry northeast-hitbox) {
		set warp state to walk_from-south
		load map ch2-castle-34
	}
	set map tick_script to on_tick-ch2-castle-32 // reset
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-32 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-32-northeast {
	copy on_exit-ch2-castle-32
	copy warping-out-fade-out
	load map ch2-castle-34
}
on_go-castle-32-northwest {
	copy on_exit-ch2-castle-32
	copy warping-out-fade-out
	load map ch2-castle-33
}
on_go-castle-32-south {
	copy on_exit-ch2-castle-32
	copy warping-out-fade-out
	load map ch2-castle-31
}

/* ---------- ENTITIES ---------- */

interact-ch2-vendingmachine {
	if (
		flag ch2-want-cactuscooler is false
		|| flag ch2-carrying-cactuscooler is true
		|| flag ch2-cactuscooler-delivered is true
	) {
		show dialog {
			PLAYER "There's sure a lot of snacks in there. Too bad I'm out of pocket change."
		}
	} else if (flag ch2-want-bobparty is false) {
		show dialog {
			PLAYER "Hmm. I thought there might be Cactus Cooler in here, but it's just chips and stuff. No soda."
		}
		turn entity "Sea Moss" toward entity "%PLAYER%"
		show dialog {
			entity "Sea Moss" "Oh, you're looking for Cactus Cooler? My cousin Bob Austin loves that stuff."
		}
		turn entity "%PLAYER%" toward entity "Sea Moss"
		show dialog {
			entity "Sea Moss" "Every time he throws a party, he brings loads and loads...."
			PLAYER "Stone Cold Bob Austin brings Cactus Cooler to his parties?"
			PLAYER "(But there's no Bob's Club party this year. Maybe I could go back to town and ask him if he has any extra from last year?)"
		}
		turn entity "Sea Moss" toward entity "%PLAYER%"
		set flag ch2-want-bobparty to true
		copy ch2-handoff-cactuscooler-to-bobaustin
	} else {
		show dialog {
			PLAYER "Oh, right. No soda in here."
		}
		turn entity "%PLAYER%" toward entity "Sea Moss"
		show dialog {
			PLAYER "Hey, you -- you said that Bob Austin likes Cactus Cooler?"
		}
		turn entity "Sea Moss" toward entity "%PLAYER%"
		show dialog {
			entity "Sea Moss" "Oh, sure. He's always got loads."
			PLAYER "(Well then, I should go back to the Bob's Club in town and ask him for some.)"
		}
		turn entity "Sea Moss" south
	}
	set entity "%SELF%" interact_script to interact-ch2-vendingmachine
}

interact-ch2-seamoss {
	copy face-player
	if (flag ch2-want-ramchips is false) {
		if (flag ch2-seamoss-backstory is false) {
			show dialog {
				PLAYER "You know, you remind me of Bob Moss."
				SELF "Oh sure. Bob Moss is my cousin. I'm Sea Moss."
				"Nice to meetcha."
			}
			set flag ch2-seamoss-backstory to true
		} else {
			show dialog {
				SELF "I've been stuck in here for a while now."
				"I don't mind much, though, since this is where the vending machine is."
			}
		}
	} else {
		if (flag ch2-cutseen-ramchips is false) {
			if (flag ch2-seamoss-backstory is false) {
				show dialog {
					PLAYER "Say, are you Sea Moss?"
					SELF "Yup, that's me. How did you know?"
					PLAYER "Lucky guess. Anyway, I heard you knew something about computer memory."
				}
				set flag ch2-seamoss-backstory to true
			} else {
				show dialog {
					PLAYER "Hey, Sea Moss. I heard you knew something about computer memory."
				}
			}
			// The above can't be part of the copy script below, since script jumps are involved
			// (spun off into its own script for length reasons)
			copy interact-ch2-seamoss-cutscene
		} else {
			show dialog {
				SELF "I do declare... chips are the greatest invention on the planet."
				"Bar none."
			}
		}
	}
	turn entity "%SELF%" south
	set entity "%SELF%" interact_script to interact-ch2-seamoss //RESET
}

ch2-walk-to-vend {
	wait 400ms
	walk entity "%PLAYER%" to geometry vending-watch over 1900ms
	wait 200ms
	turn entity "%PLAYER%" north
	goto null_script
}

interact-ch2-seamoss-cutscene {
	set player control off
	show dialog {
		SELF "Oh, sure. I remember all sorts of stuff about that."
		PLAYER "Well, I need some RAM to build a computer, but Lambda can't think of anything around here that would work."
		SELF "RAM, like RAM chips? You could try the vending machine over yonder."
		PLAYER "...Because it dispenses chips?"
		SELF "You gotta keep your mind open for this stuff, man. Feel like a computer. Breathe like one. Understand what a computer wants."
		"It wants to contain the whole universe within itself, to build and share its stories with each and every one of us at a keyboard or network port."
		"For a computer to live, it must remember itself and allow itself to exist from moment to moment."
		PLAYER "Uh...."
		SELF "And if you need RAM chips, the sky's the limit, my dude. Anything is possible. Chips is chips!"
		PLAYER "I really don't think that's true."
		SELF "It'll be fine. Come here, I'll show you."
	}
	wait 100ms
	set entity "%PLAYER%" tick_script to ch2-walk-to-vend
	walk entity "Sea Moss" to geometry vending-spot over 1800ms
	wait 800ms
	show dialog {
		SELF "I know a secret code for the ultimate cache of cache. Just need some cash...."
	}
	wait 850ms
	// bag of RAM chips appears in front of the vending machine
	set entity "RAM Chips" name to "RAM Chips"
	teleport entity "RAM Chips" to geometry ch2-chips-slide
	// hovers for a moment
	wait 500ms
	// then slides downward gently
	walk entity "RAM Chips" to geometry ch2-chips-land over 400ms
	wait 1300ms
	show dialog {
		PLAYER "Oh my god."
	}
	copy script face-player
	show dialog {
		SELF "There you are, friend! Some spicy nacho DDR5 RAM chips, on the house."
		PLAYER "...Thanks...?"
		SELF "Any time."
	}
	set player control on
	// Sea Moss returns to his table
	turn entity "Sea Moss" south
	set flag ch2-cutseen-ramchips to true
	set flag ch2-ramchips-dispensed to true
}

/* ---------- RAMCHIPS (ADMIN) ---------- */

ch2-hide-ramchips {
	teleport entity "RAM Chips" to geometry hiding-spot
	set entity "RAM Chips" name to " "
	// ^^ when viewed with hex editor its name will appear blank
	// (you must still target this entity with its original name, though)
}

ch2-touch-ramchips {
	if (flag ch2-ramchips-firstlook is false) {
		show dialog {
			PLAYER "That's the biggest bag of chips I've ever seen in my life."
			"...And it's probably only a quarter full."
		}
		set flag ch2-ramchips-firstlook to true
	} else {
		show dialog {
			PLAYER "That's an obscene amount of chips."
		}
	}
	show dialog {
		name "" "(Pick up the RAM chips?)"
		> "Yes" : ch2-touch-ramchips-yes
		> "No" : ch2-touch-ramchips-no
	}
	set entity "%SELF%" interact_script to ch2-touch-ramchips // RESET
}
ch2-touch-ramchips-yes {
	show dialog {
		name "" "(You pick up the RAM chips!)"
	}
	copy script ch2-hide-ramchips
	copy script ch2-pickup-ramchips
	set flag ch2-ramchips-dispensed to false
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-ramchips-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-ramchips // RESET
}
