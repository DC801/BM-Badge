include!("header.mgs")

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-23 {
	show serial dialog spacer;
	show serial dialog {
		"You looked around the <c>POWER PLANT</>."
		"\tThe room is filled with hisses and a whirring or rumbling noise. Scores of pipes sprawl everywhere. Some of the gauges are labeled \"pressure\" -- and some of the pipes seem to be warm when you draw a hand close."
		" "
	}
}

look-ch2-tracy {
	show serial dialog spacer;
	show serial dialog {
		"You looked at <m>%SELF%</>."
		"\tTODO: Tracy on_look"
	}
}
look-ch2-rocco {
	show serial dialog spacer;
	show serial dialog {
		"You looked at <m>%SELF%</>."
		"\tTODO: Rocco on_look"
	}
}
look-ch2-helvetica {
	show serial dialog spacer;
	show serial dialog {
		"You looked at <m>%SELF%</>."
		"\tHer attention is fixed on the numerous gauges and dials at her workstation. She seems to be concentrating fiercely -- if the twitch of her tail is any indication."
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-23 {
	// ch2 room state
	mutate ch2-in-room = 23;

	// DEBUG
	if (debug mode is on) {
		register get + thing -> ch2-pickup-cactuscooler;
		show serial dialog { "\t<g>DEBUG option</>: 'get thing' to acquire Cactus Cooler" }
	}

	// entrance text
	show serial dialog spacer;
	if (flag ch2-seen-room-23 is false) {
		show serial dialog { "Discovered <bold><c>POWER PLANT</>! Room added to map!" }
		set flag ch2-seen-room-23 to true;
	} else {
		show serial dialog { "Entering <bold>POWER PLANT</>..." }
	}

	// yeet hidden things
	if (
		flag ch2-installed-heatsink is true
		|| flag ch2-carrying-heatsink is true
	) {
		copy script ch2-hide-heatsink;
	}

	// initialize everything
	copy ch2-map-init;
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-23 {
	goto null_script;
}

ch2-powerplant-supervise {
	turn entity "%SELF%" north;
	copy tick-rand-mini; // from bob's club basement
	walk entity "%SELF%" to geometry walk2 over 600ms;
	turn entity "%SELF%" north;
	copy tick-rand-broad; // from bob's club basement
	walk entity "%SELF%" to geometry walk1 over 600ms;
	turn entity "%SELF%" north;
	copy tick-rand-broad; // from bob's club basement
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-23 { // sanitize ch2 room state
	mutate ch2-in-room = 0;
}

// SERIAL: go

on_go-castle-23-south {
	copy on_exit-ch2-castle-23;
	copy warping-out-fade-out;
	load map ch2-castle-22;
}

/* ---------- ENTITIES ---------- */

interact-ch2-helvetica {
	copy interrupt-walk;
	show dialog {
		SELF
		"At this power plant, we capture heat from deep below the earth and use it to turn water into steam, which we send through turbines to generate electricity."
		"But this isn't just the power plant control room. It also contains a lot of intrastructure for the geothermal heating throughout the castle."
	}
	copy resume-walk;
}

interact-ch2-tracy {
	copy face-player;
	show dialog {
		SELF "Our entire work revolves around moving heat from one place to another. It's often more efficient to move heat than it is to create it."
	}
}

interact-ch2-rocco {
	copy face-player;
	if (flag ch2-cactuscooler-delivered is true) {
		show dialog {
			SELF "Now the hard part: figure out how to cool this thing with Cactus Cooler."
		}
	}
	else if (flag ch2-rocco-backstory is false) {
		show dialog {
			PLAYER "So... I was told you guys could help me find a heat sink."
			SELF "A heat sink? What's the use case?"
			PLAYER "A computer. Like a normal-looking kind of boxy computer."
		}
		turn player control off;
		wait 100ms;
		if (entity Rocco direction is south) {
			turn entity Rocco west;
		} else {
			turn entity Rocco toward entity "heat sink";
		}
		wait 1000ms;
		copy face-player;
		wait 100ms;
		turn player control on;
		show dialog {
			SELF "Well, there's this heat sink here, but it's not extra."
			PLAYER "Do you have any extras anywhere, maybe \"in the back\" or something?"
			SELF "In the \"back?\" We don't have a \"back.\""
			PLAYER "It's really important. Lambda and I are trying to put the Gibson mainframe back together, and...."
			SELF "Lambda? Lambda who?"
		}
		if (flag ch2-lambda-deletion-backstory is false) {
			show dialog {
				PLAYER "Lambda. He's -- you don't know who he is?"
				SELF "Why would I? Is he famous or something?"
			}
		} else {
			show dialog {
				PLAYER "Lambda. You... really don't know who he is, do you? You actually forgot who he was?"
				SELF "Forget? What are you on about?"
				PLAYER "Wow. Lambda was right. He said none of you would remember him."
				SELF "No, I think I would've remembered someone with the name Lambda."
			}
		}
		rotate entity "%SELF%" 1;
		show dialog {
			SELF "Lambda.... It sounds like an alias. Is he a hacker?"
			PLAYER "Actually, I don't know. Maybe?"
			SELF "...."
			"How strange. A place like this normally has an IT guy, or someone who's a hacker or software tinkerer at least, but... I'm can't think of who ours was."
			"We must have had someone like that. I'm the one who usually fixes the computers, but why can't I remember who fixes the software?"
			"Hmmmm."
		}
		copy face-player;
		show dialog {
			SELF "Regardless, this heat sink is spoken for. Although.... I concede that fixing the mainframe is more important than getting this thing working."
			"Tell you what. Get me a substitute I can use for my build, and you can have this heat sink here."
			PLAYER "Thanks! What kind of substitute?"
			SELF "Let's try something really wacky. I wanted to experiment anyway. Liquid cooling, or -- no, let's be really crazy. Get me a can of Cactus Cooler."
			PLAYER "Cactus Cooler?"
			SELF "It's this rare brand of soda. It'll be extra funny if I can get it involved with cooling the machine, since it's got \"cooler\" in the name. Hilarious!"
		}
		set flag ch2-rocco-backstory to true;
		set flag ch2-want-cactuscooler to true;
		set flag ch2-heatsink-firstlook to true;
		copy ch2-handoff-heatsink-to-cactuscooler;
		if (flag ch2-carrying-cactuscooler is true) {
			show dialog {
				PLAYER "Well, I actually have a can of Cactus Cooler right here."
				SELF "Oh, really? In which case...."
			}
			set flag interrupt to true;
			goto interact-ch2-rocco-give-q; // jump
		} else {
			show dialog {
				PLAYER "Sure. Get you Cactus Cooler. Rare soda. Got it."
			}
		}
	}
	else if (flag ch2-carrying-cactuscooler is true) {
		if (flag interrupt is false) {
			show dialog {
				SELF "Looks like you found a can of Cactus Cooler somewhere! Willing to give it to me?"
			}
			set flag interrupt to true;
		} else {
			show dialog {
				SELF "Change your mind? Willing to give up your can of Cactus Cooler?"
			}
		}
		goto interact-ch2-rocco-give-q; // jump
	}
	else {
		show dialog {
			SELF "Remember, I'll give up this heat sink if you bring me a substitute: a can of Cactus Cooler."
		}
	}
	goto interact-ch2-rocco-wrapup;
}

interact-ch2-rocco-give-q {
	show dialog {
		name "" "(Give %Rocco% the Cactus Cooler)?"
		> "Yes" : interact-ch2-rocco-give
		> "No" : interact-ch2-rocco-nogive
	}
}

interact-ch2-rocco-give {
	show dialog {
		SELF "Hey, thanks, buddy!"
		name "" "(Gave %Rocco% the can of Cactus Cooler!)"
		SELF "Hey, that's swell! Go ahead and take that heat sink now. We'll be all right with this stuff instead."
	}
	set flag ch2-cactuscooler-delivered to true;
	set flag ch2-heatsink-permission to true;
	copy ch2-turn_in-cactuscooler;
	goto interact-ch2-rocco-wrapup;
}

interact-ch2-rocco-nogive {
	show dialog {
		SELF "Aww, come on. No Cactus Cooler, no heat sink."
	}
	goto interact-ch2-rocco-wrapup;
}

interact-ch2-rocco-wrapup {
	set entity "%SELF%" on_interact to interact-ch2-rocco;
}

/* ---------- HEAT SINK (ADMIN) ---------- */

ch2-hide-heatsink {
	teleport entity "heat sink" to geometry hiding-spot;
	set entity "heat sink" name to " ";
}

ch2-touch-heatsink {
	if (flag ch2-heatsink-firstlook is false) {
		show dialog {
			PLAYER "That's a really big tuning fork."
			Rocco "It's a heat sink. It siphons heat away from a processor or whatever. Though I wonder if it would work as a tuning fork, too."
		}
		set flag ch2-heatsink-firstlook to true;
		set flag temp to true;
	} else {
		show dialog {
			PLAYER "Yup, a heat sink."
		}
		set flag temp to false;
	}
	if (flag ch2-heatsink-permission is false) {
		if (flag ch2-rocco-backstory is false) {
			if (flag temp is true) {
				show dialog {
					PLAYER "(I bet I can't just take it away with me though. I should ask first.)"
				}
			} else {
				show dialog {
					PLAYER "I better ask before taking it."
				}
			}
		} else if (flag ch2-carrying-cactuscooler is false) {
			show dialog {
				PLAYER "Better go off to find Cactus Cooler to trade for it."
			}
		} else {
			show dialog {
				PLAYER "Better talk to Rocco to trade the Cactus Cooler for it."
			}
		}
	} else {
		show dialog {
			name "" "(Pick up the heat sink?)"
			> "Yes" : ch2-touch-heatsink-yes
			> "No" : ch2-touch-heatsink-no
		}
	}
}
ch2-touch-heatsink-yes {
	show dialog {
		name "" "(You pick up the heat sink!)"
	}
	copy script ch2-hide-heatsink;
	copy script ch2-pickup-heatsink;
	set entity "%SELF%" on_interact to null_script;
}
ch2-touch-heatsink-no {
	show dialog {
		PLAYER "Another time, perhaps."
	}
	set entity "%SELF%" on_interact to ch2-touch-heatsink;  // reset after script jump
}
