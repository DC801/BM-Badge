settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-31 {
	show serial dialog spacer
	show serial dialog {
		"TODO: GRAND HALL on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-31 {
	// ch2 room state
	mutate ch2-in-room = 31

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-31 is false) {
		show serial dialog { "Discovered <bold><c>GRAND HALL</>! Room added to map!" }
		set flag ch2-seen-room-31 to true
	} else {
		show serial dialog { "Entering <bold>GRAND HALL</>..." }
	}
	if (
		flag ch2-installed-keyboard is true
		|| flag ch2-carrying-keyboard is true
	) {
		copy script ch2-hide-keyboard
	}

	if (debug mode is true) {
		register simon -> debug-simon
		register simon + high -> debug-simon-high
		register simon + low -> debug-simon-low
		register simon + win -> debug-simon-win
		register simon + lose -> debug-simon-lose
		show serial dialog {
			" "
			"<bold><g>DEBUG:</><bold> Play Simon Says!</>"
			"<c>SIMON</>: replay last game type (default: low)"
			"<c>SIMON HIGH</>: play high game (easier on desktop)"
			"<c>SIMON LOW</>: play low game (easier on the real badge)"
			"<c>SIMON WIN</>: play the win animation(s)"
			"<c>SIMON LOSE</>: play the lose animation(s)"
			" "
		}
	}
	goto ch2-map-init
}

debug-simon {
	goto simon-game-launcher
}
debug-simon-low {
	set flag ch2-simon-high to false
	goto simon-game-launcher
}
debug-simon-high {
	set flag ch2-simon-high to true
	goto simon-game-launcher
}
debug-simon-win {
	goto simon-game-win
}
debug-simon-lose {
	goto simon-game-lose
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-31 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-31 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-31-north {
	copy on_exit-ch2-castle-31
	copy warping-out-fade-out
	load map ch2-castle-32
}
on_go-castle-31-west {
	copy on_exit-ch2-castle-31
	copy warping-out-fade-out
	load map ch2-castle-12
}


/* ---------- KEYBOARD (ADMIN) ---------- */

ch2-hide-keyboard {
	teleport entity "Keyboard" to geometry hiding-spot
	set entity "Keyboard" name to " "
}

ch2-touch-keyboard {
	if (flag ch2-keyboard-firstlook is false) {
		show dialog {
			PLAYER "TODO keyboard1"
		}
		set flag ch2-keyboard-firstlook to true
	}
	show dialog {
		PLAYER "TODO keyboard2."
	}
	if (variable ch2-storyflag-round is >= 2) {
		show dialog {
			name "" "(Pick up the keyboard?)"
			> "Yes" : ch2-touch-keyboard-yes
			> "No" : ch2-touch-keyboard-no
		}
	}
	set entity "%SELF%" interact_script to ch2-touch-keyboard // RESET
}
ch2-touch-keyboard-yes {
	show dialog {
		name "" "(You pick up the keyboard!)"
	}
	copy script ch2-hide-keyboard
	set flag ch2-carrying-keyboard to true
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-keyboard-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-keyboard // RESET
}

ch2-install-keyboard {
	set flag ch2-carrying-keyboard to false
	set flag ch2-installed-keyboard to true
}
look-keyboard {
	show serial dialog spacer
	show serial dialog {
		"on_look keyboard TODO"
	}
}
