settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label SELF { entity "%SELF%" }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}

/* --------------- on_tick --------------- */

on_tick-oldcouplehouse {
	if entity "%PLAYER%" is inside geometry door-oldcouplehouse
		then goto leave-oldcouplehouse
}
leave-oldcouplehouse {
	set warp state to exit_from-oldcouplehouse
	if (flag storyflag-bea1 is true) {
		set flag bea-sequel-branch to true
	}
	load map main
}

/* --------------- on_load --------------- */

on_load-oldcouplehouse {
	if (flag tuesdayd is true) {
		register rtfm -> serial-rtfm
	}
	if (flag bea-sequel-branch is true) {
		if (flag storyflag-bea2 is false) {
			copy make-delmar-man
		}
	}
}
make-delmar-man {
	set entity Delmar type to old_man
	set entity Delmar animation to 3
	set entity Delmar tick_script to on_tick-delmar-blab-nonman-watch
}

/* --------------- Delmar --------------- */

show_dialog-delmar {
	if (entity "%SELF%" is glitched) {
		show dialog {
			SELF "Bu, qrne. V zhfg'ir fgbbq hc gbb snfg. Srry n ovg jbbml."
		}
	} else if (entity Delmar type is not old_man) {
		show dialog dialog-dsheep
	} else if (flag bea-sequel-branch is true) {
		show dialog {
			SELF "...I tied an onion to my belt, which was the style at the time."
			"Now, to take the ferry cost a nickle, and in those days, nickles had pictures of bumblebees on 'em."
			"\"Gimme five bees for a quarter,\" you'd say. Now where were we?"
			"Oh, yeah. The important thing was that I had an onion on my belt, which was the style at the time."
			"They didn't have any white onions, because of the war. The only thing you could get was those big yellow ones...."
		}
	} else if (flag storyflag-bea1 is true) {
		show dialog {
			Delmar "Huh? What's goin' on? What'd I miss?"
		}
	} else {
		show dialog {
			SELF "Wha... huh?"
		}
	}
	set entity Delmar interact_script to show_dialog-delmar
}


/* --------------- Delmar --------------- */

show_dialog-bea-start {
	if (entity "%SELF%" is glitched) {
		show dialog { SELF "Ydy D ymjk ht bgvnnzn? Ocz rjmgy'n bjiz v wdo apuut." }
	} else {
		copy script clear-own-tick
		copy script face-player
		if (flag bea-sequel-branch is false) {
			goto show_dialog-bea-part1
		} else {
			goto show_dialog-bea-part2
		}
	}
	set entity Beatrice interact_script to show_dialog-bea-start
}

/* --------------- Beatrice --------------- */

slow-stare-at-delmar {
	set player control to off
	wait 100ms
	turn entity "%SELF%" toward entity Delmar
	wait 500ms
	set player control to on
}
slow-stare-at-player {
	set player control to off
	wait 100ms
	turn entity "%SELF%" toward entity "%PLAYER%"
	wait 500ms
	set player control to on
}

// part 1

show_dialog-bea-part1 {
	if (flag storyflag-bea1 is false) { // you did NOT beat part1
		if (entity Delmar type is old_man) { // win condition met
			// currently no backstory/!backstory difference
			set player control to off
			copy script clear-own-tick
			copy script slow-stare-at-delmar
			set player control to on
			show dialog {
				SELF "Oh, %Delmar%, it's you! You're a man again! At last!"
				Delmar "Whazzat, %Beatrice%? Something seems different."
				SELF "Oh, it's my %Delmar%! Thank you, %PLAYER%!"
			}
			turn entity "%SELF%" toward entity Delmar
			copy script calculate-hint-tracking-series
			if (variable hint-tracking-check is 4) {
				copy script set-hint-none
			}
			set flag bea1-backstory to true
			set flag storyflag-bea1 to true
		} else { // win condition NOT met
			if (flag bea1-backstory is false) { // you did NOT hear the backstory
				if (entity Delmar type is sheep || entity Delmar type is helga) { // is sheep
					show dialog {
						SELF "Forty years ago, things were sure different."
						"I was a lot younger then, and my husband, %Delmar%...."
					}
					copy script clear-own-tick
					copy script slow-stare-at-delmar
					show dialog { SELF "My husband was a real man! Not a farm animal!" }
					copy script slow-stare-at-player
					set player control to on
				} else { // is rando
					copy script clear-own-tick
					copy script slow-stare-at-delmar
					show dialog { SELF "Oh, %Delmar%! You're... oh...." }
					copy script face-player
					show dialog {
						SELF "Oh, goodness. Well, this isn't my %Delmar%, but I suppose it's nice he's not a sheep anymore, at least."
						"He's been a sheep for so long... well, I suppose it's been forty years."
					}
				}
				show dialog { // is sheep OR rando
					SELF "That ghastly hacker ruffian hacked him into a sheep on a whim!"
					"%Delmar% didn't even do anything to him!"
					"...Other than insult his character, and that of his mother."
					"...And eat his lunch."
					"...And cut his brake line."
					"The point is, child, that %Delmar%'s been stuck as a sheep ever since!"
					"But you, now that you have Ring Zero...."
					"You can make things right again, can't you?"
					"I miss my %Delmar%!"
					PLAYER "Make things right? Well, I can change what he looks like, anyway."
					SELF "That works for me!"
				}
				set flag bea1-backstory to true
			} else { // you DID hear the backstory (sheep or rando)
				show dialog { SELF "Oh, %Delmar%... we'll fix you up real soon!" }
			}
			copy script set_tick-watch-player
			copy script set-hint-bea1
		}
	} else { // you DID beat part1
		if (entity Delmar type is old_man) {
			show dialog { SELF "Thank you, %PLAYER%!" }
		} else if (entity Delmar type is sheep || entity Delmar type is helga) {
			show dialog {
				SELF "Oh! That's a nasty trick, %PLAYER%!"
				"How could you?"
				"Although... I sort of get the feeling he'll be back to normal in a few minutes."
			}
		} else {
			show dialog {
				SELF "Oh, %Delmar%, you're... uh...."
			}
		}
	}
	set entity Beatrice interact_script to show_dialog-bea-start
}

// part 2

show_dialog-bea-part2 {
	if (flag storyflag-bea2 is false) { // !part2won
		if (entity Delmar type is sheep || entity Delmar type is helga) {
			if (flag bea2-backstory is false) {
				show dialog {
					SELF "Oh! And I was just thinking how I wanted him to become a sheep again!"
					"So gentle... and so quiet!"
					"Thank you, %PLAYER%!"
				}
			} else { // you DID hear the backstory
				show dialog {
					SELF "Thank you, child!"
					"...And, erm, we need not mention this to anyone! You understand?"
				}
			}
			turn entity "%SELF%" toward entity Delmar
			copy script calculate-hint-tracking-series
			if (variable hint-tracking-check is 9) {
				copy script set-hint-none
			}
			set flag bea2-backstory to true
			set flag storyflag-bea2 to true
		} else { // not a sheep
			if (entity Delmar type is old_man) {
				if (flag bea2-backstory is false) {
					show dialog { SELF "...." "You know, I appreciate what you did, %PLAYER%. I really do." }
					copy script clear-own-tick
					copy script slow-stare-at-delmar
					show dialog { SELF "...." }
					copy script slow-stare-at-player
					show dialog {
						SELF "But now I find myself longing for quiet."
						"It was so peaceful, before. %Delmar% and I used to get along so well."
						"And it's so difficult to fall asleep recently."
						"I hate to ask it, child, but...."
						"I can't hear myself think! Please change him back!"
					}
				} else { // you DID hear the backstory
					show dialog {
						SELF "I'm sure he won't mind being a sheep again. In fact, I'm not sure he'll notice."
						"But I sure will! I won't have peace otherwise!"
					}
				}
			} else { // not a sheep nor a man
				show dialog {
					SELF "Oh, um...."
					"Actually, I was hoping he could become a sheep again."
				}
			}
			copy script set_tick-watch-player
			set flag bea2-backstory to true
			copy script set-hint-bea2
		}
	} else { // part2won
		if (entity Delmar type is sheep || entity Delmar type is helga) {
			show dialog {
				SELF "Peace at last!"
			}
		} else if (entity Delmar type is old_man) {
			show dialog {
				SELF "...." "I suddenly have the strangest feeling of deja vu."
			}
		} else {
			show dialog {
				SELF "Well, I suppose we could give this a try instead...."
			}
		}
	}
	set entity Beatrice interact_script to show_dialog-bea-start
}

/* --------------- blab and unblab --------------- */

delmar-blab {
	set entity Delmar animation to 3
	set entity Delmar tick_script to on_tick-delmar-blab-nonman-watch
}
on_tick-delmar-blab-nonman-watch {
	if entity Delmar type is not old_man then goto delmar-unblab
}
delmar-unblab {
	set entity Delmar animation to 0
	set entity Delmar tick_script to on_tick-delmar-blab-man-watch
}
on_tick-delmar-blab-man-watch {
	if entity Delmar type is old_man then goto delmar-blab
}

/* --------------- debug --------------- */

debug-bea-sequel-q {
	copy script face-player
	show dialog {
		SELF "Set sequel branch?"
		> "Yes (plus man)" : debug-bea-sequel-man
		> "Yes" : debug-bea-sequel-noman
		> "No" : debug-bea-sequel-no
	}
}
debug-bea-sequel-man {
	set flag bea-sequel-branch to true
	set flag storyflag-bea1 to true
	set flag bea1-backstory to true
	set entity Delmar type to old_man
	copy script set-hint-none
	copy script delmar-blab
	show dialog { SELF "Sequel branch activated, and Delmar is a man." }
	set entity "%SELF%" interact_script to debug-bea-sequel-q
}
debug-bea-sequel-noman {
	set flag bea-sequel-branch to true
	set flag storyflag-bea1 to true
	set flag bea1-backstory to true
	copy script set-hint-none
	show dialog { SELF "Sequel branch activated, and Delmar left alone." }
	set entity "%SELF%" interact_script to debug-bea-sequel-q
}
debug-bea-sequel-no {
	show dialog { SELF "Nothing changed." }
	set entity "%SELF%" interact_script to debug-bea-sequel-q
}

dialog dialog-bea-mandud-s { //where was this supposed to go? lol
	SELF "Oh, %Delmar%! We'll make you your normal self soon!"
}
