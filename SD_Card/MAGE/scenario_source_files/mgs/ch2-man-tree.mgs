settings for serial dialog { wrap 60 }

ch2-command-man-normal-convo {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> What do you want to know?"
		" "
	}
	goto ch2-lambda-topic-q
}

ch2-lambda-topic-q {
	show serial dialog {
		"(Ask about:)"
		# "Parts list" : ch2-lambda-topic-parts
		# "Using the artifact" : ch2-lambda-topic-artifact
		# "The mainframe" : ch2-lambda-topic-mainframe
		# "The castle" : ch2-lambda-topic-castle
		# "Someone specific" : ch2-lambda-topic-person-start
		# "(Quit)" : ch2-lambda-topic-nevermind
	}
}

ch2-lambda-topic-nevermind {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> I should go."
		" "
		"<m>LAMBDA:</> No problem! Just let me know if you have a question about anything."
	}
}

/* ---------- ARTIFACT ---------- */

ch2-lambda-topic-parts {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> These are the parts we need:"
		// TODO change this to a MD checklist thingy
	}
	set flag temp to false
	if (flag ch2-installed-clock is false) {
		show serial dialog { "\t- clock" }
		set flag temp to true
	}
	if (flag ch2-installed-heatsink is false) {
		show serial dialog { "\t- heat sink" }
		set flag temp to true
	}
	if (flag ch2-installed-keyboard is false) {
		show serial dialog { "\t- keyboard" }
		set flag temp to true
	}
	if (flag ch2-installed-cpu is false) {
		show serial dialog { "\t- cpu" }
		set flag temp to true
	}
	if (flag ch2-installed-goldfish is false) {
		show serial dialog { "\t- goldfish" }
		set flag temp to true
	}
	if (flag ch2-installed-abacus is false) {
		show serial dialog { "\t- abacus" }
		set flag temp to true
	}
	if (flag ch2-installed-powersupply is false) {
		show serial dialog { "\t- powersupply" }
		set flag temp to true
	}
	if (flag temp is false) {
		goto ch2-lambda-topic-parts-finished
	}
	set flag temp to false
	goto ch2-lambda-topic-parts-q
}

ch2-lambda-topic-parts-q {
	show serial dialog {
		" "
		"(Type the part name for more info, <c>LIST</> to see the parts list again, or <c>QUIT</> to go back)"

		_ "LIST" : ch2-lambda-topic-parts

		_ "Q" : ch2-command-man-normal-convo
		_ "QUIT" : ch2-command-man-normal-convo
		
		_ "CLOCK" : ch2-lambda-parts-clock

		_ "HEAT SINK" : ch2-lambda-parts-heatsink
		_ "HEATSINK" : ch2-lambda-parts-heatsink

		_ "CPU" : ch2-lambda-parts-cpu

		_ "GOLDFISH" : ch2-lambda-parts-goldfish

		_ "POWERSUPPLY" : ch2-lambda-parts-powersupply
		_ "POWER SUPPLY" : ch2-lambda-parts-powersupply
		
		_ "KEYBOARD" : ch2-lambda-parts-keyboard
	}
}

ch2-describe-clock {
	show serial dialog ch2-describe-clock
}
ch2-describe-heatsink {
	show serial dialog ch2-describe-heatsink
}
ch2-describe-keyboard {
	show serial dialog ch2-describe-keyboard
}
ch2-describe-cpu {
	show serial dialog ch2-describe-cpu
}
ch2-describe-goldfish {
	show serial dialog ch2-describe-goldfish
}
ch2-describe-powersupply {
	show serial dialog ch2-describe-powersupply
}

ch2-lambda-parts-clock {
	show serial dialog spacer
	if (flag ch2-installed-clock is false) {
		copy ch2-describe-clock
	} else {
		show serial dialog {
			"<m>LAMBDA:</> The clock's installed already. It's just... you know... clocking away."
		}
	}
	goto ch2-lambda-topic-parts-q
}
ch2-lambda-parts-heatsink {
	show serial dialog spacer
	if (flag ch2-installed-heatsink is false) {
		copy ch2-describe-heatsink
	} else {
		show serial dialog {
			"<m>LAMBDA:</> You've got the heat sink in there already. Oh, I guess this means I can turn off the furnace in there now."
		}
	}
	goto ch2-lambda-topic-parts-q
}
ch2-lambda-parts-keyboard {
	show serial dialog spacer
	if (flag ch2-installed-keyboard is false) {
		copy ch2-describe-keyboard
	} else {
		show serial dialog {
			"<m>LAMBDA:</> TODO keyboard is already installed."
		}
	}
	goto ch2-lambda-topic-parts-q
}
ch2-lambda-parts-cpu {
	show serial dialog spacer
	if (flag ch2-installed-cpu is false) {
		copy ch2-describe-cpu
	} else {
		show serial dialog {
			"<m>LAMBDA:</> The cpu's installed already. It's been computin."
		}
	}
	goto ch2-lambda-topic-parts-q
}
ch2-lambda-parts-goldfish {
	show serial dialog spacer
	if (flag ch2-installed-goldfish is false) {
		copy ch2-describe-goldfish
	} else {
		show serial dialog {
			"<m>LAMBDA:</> TODO goldfish is already installed."
		}
	}
	goto ch2-lambda-topic-parts-q
}
ch2-lambda-parts-powersupply {
	show serial dialog spacer
	if (flag ch2-installed-powersupply is false) {
		copy ch2-describe-powersupply
	} else {
		show serial dialog {
			"<m>LAMBDA:</> TODO powersupply is already installed."
		}
	}
	goto ch2-lambda-topic-parts-q
}

ch2-lambda-topic-parts-finished {
	show serial dialog {
		"\tOh, wait, is that everything? Huzzah! (TODO)"
	}
}

ch2-lambda-topic-artifact {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> You had a question about the artifact?"
		" "
	}
	goto ch2-lambda-topic-artifact-q
}

ch2-lambda-topic-artifact-q {
		show serial dialog {
		"(Ask about:)"
		# "Magic words (Commands)" : ch2-lambda-topic-artifact-words
		# "(Go back)" : ch2-command-man-normal-convo
	}
}

ch2-lambda-topic-artifact-words {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What can you tell me about \"magic words?\" You said they were commands?"
		" "
		"<m>LAMBDA:</> Yeah, so, when I say \"magic words\" I mean serial commands -- that is, things you type in the terminal to make the artifact do something."
		"\tYou can use the command <c>HELP</> to see a list of commands that are valid at the moment. To learn more about a specific command, type <c>MAN</> plus the name of the command. For example, type <c>MAN LOOK</> to see the manual page for the <c>LOOK</> command."
		" "
	}
	goto ch2-lambda-topic-artifact-q
}

/* ---------- MAINFRAME ---------- */

ch2-lambda-topic-mainframe {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> You had a question about the mainframe?"
		" "
	}
	goto ch2-lambda-topic-mainframe-q
}

ch2-lambda-topic-mainframe-q {
		show serial dialog {
		"(Ask about:)"
		# "Why did you break it?" : ch2-lambda-mainframe-broken
		# "How broken is it?" : ch2-lambda-mainframe-broken-1
		# "How do we fix it?" : ch2-lambda-mainframe-fix-1
		# "(Go back)" : ch2-command-man-normal-convo
	}
}

ch2-lambda-mainframe-broken {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Why did you break it?"
		" "
		"<m>LAMBDA:</> Like I said, I broke it to keep it out of the <r>Big Bad</>'s hands."
		"\tTODO: more convo"
		" "
	}
	goto ch2-lambda-topic-mainframe-q
}

ch2-lambda-mainframe-broken-1 {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What exactly about it is broken?"
		" "
		"<m>LAMBDA:</> Well, sort of... everything. It's in a million pieces, to be precise."
		# "Thorough. Nice!" : ch2-lambda-mainframe-broken-1a
		# "Was that necessary?" : ch2-lambda-mainframe-broken-1a
	}
}
ch2-lambda-mainframe-broken-1a {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> Yeah, so, ahhh... it might not have been necessary, strictly speaking. I wiped its software, first. That would've been enough on its own, to be honest...."
		"\tI don't know. I just wanted to be <bold>sure</>. I sort of regret it now."
		" "
	}
	goto ch2-lambda-topic-mainframe-q
}

ch2-lambda-mainframe-fix-1 {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> How do we fix it? What do we need?"
		" "
		"<m>LAMBDA:</> We'll have to build a new computer from scratch. The old bits are... well, just bits -- bits of copper and plastic and silicon debris."
		"\tBut there should be enough <m>computer parts</> scattered throughout the castle. I have my parts list right here. The software though... I'll, um, get back to you about that."
		" "
	}
	goto ch2-lambda-topic-mainframe-q
}

/* ---------- CASTLE ---------- */

ch2-lambda-topic-castle {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> You had a question about the castle?"
		" "
	}
	goto ch2-lambda-topic-castle-q
}

ch2-lambda-topic-castle-q {
		show serial dialog {
		"(Ask about:)"
		# "Whose castle is this?" : ch2-lambda-castle-who
		# "The earthquake caused damage?" : ch2-lambda-castle-damage
		# "(Go back)" : ch2-command-man-normal-convo
	}
}

ch2-lambda-castle-who {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Whose castle is this?"
		" "
		"<m>LAMBDA:</> I'm kind of surprised you didn't know, but it's King Gibson's castle. "
		"\tTODO: more convo"
		" "
	}
	goto ch2-lambda-topic-castle-q
}
ch2-lambda-castle-damage {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> The earthquake caused some damage, then?"
		" "
		"<m>LAMBDA:</> Oh, for sure. A bunch of doors got jammed or caved in. That's one of the reasons I'm letting you have the portable interface -- it'll let you get past blockages, and just about everyone inside the castle is trapped in place."
		"\tIf the mainframe were still working, we would've been able to... well, it would have enabled people to get around, anyway."
		" "
	}
	goto ch2-lambda-topic-castle-q
}

/* ---------- PEOPLE ---------- */

ch2-lambda-topic-person-start {
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> Who did you want to ask about?"
		" "
	}
	goto ch2-lambda-topic-person-q
}
ch2-lambda-topic-person-s {
	show serial dialog {
		"\tUm, did you want to ask about anyone else?"
		" "
	}
	goto ch2-lambda-topic-person-q
}

ch2-lambda-topic-person-q {
	show serial dialog {
		"(Type a name, or type <c>CANCEL</> to go back)"
		
		_ "BIG BAD" : ch2-lambda-person-big-bad

		_ "ME" : ch2-lambda-person-player
		_ "BLACK MAGE" : ch2-lambda-person-player
		_ "MAGE" : ch2-lambda-person-player
		
		_ "YOU" : ch2-lambda-person-lambda
		_ "LAMBDA" : ch2-lambda-person-lambda

		_ "BERT" : ch2-lambda-person-bert
		
		_ "BOB" : ch2-lambda-person-bob

		_ "CANCEL" : ch2-command-man-normal-convo
		_ "QUIT" : ch2-command-man-normal-convo
		_ "STOP" : ch2-command-man-normal-convo
		_ "NEVER MIND" : ch2-command-man-normal-convo
		_ "(Go back)" : ch2-command-man-normal-convo
	}
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> Sorry, not sure who you mean."
		" "
	}
	goto ch2-lambda-topic-person-q
}

// The player

ch2-lambda-person-player {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Tell me about me!"
		" "
		"<m>LAMBDA:</> Oh, um, I don't actually know anything about you."
		"\tI guess... if anything... you've got a cool hat. That's not nothing, I suppose...."
		" "
		# "How did you know I'm wearing a hat?" : ch2-lambda-person-player-2
	}
}
ch2-lambda-person-player-2 {
	set flag ch2-cameras-explanation to true
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> How did you know I'm wearing a hat?"
		" "
		"<m>LAMBDA:</> That's, um... that would be because of the... well, the cameras."
		" "
		# "Cameras? That's creepy!" : ch2-lambda-person-player-2a
		# "Cameras? That's sensible." : ch2-lambda-person-player-2b
	}
}
ch2-lambda-person-player-2a {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Cameras? That's creepy."
		" "
		"<m>LAMBDA:</> Yeah, I realize it doesn't sound great, having cameras everywhere. The cameras made a bit more sense at one point, back when there was a lot more traffic in and out of here, but thinking about it, I guess it's pretty invasive. Funny, the things you just get used to...."
	}
	goto ch2-lambda-topic-person-s
}
ch2-lambda-person-player-2b {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Cameras? That's a sensible measure to take."
		" "
		"<m>LAMBDA:</> You almost sound like you approve. They don't really do much good, certainly not anymore, and it's a little invasive, don't you think?"
		" "
		# "It's not that invasive." : ch2-lambda-person-player-3b
		# "Okay, maybe it's a little invasive." : ch2-lambda-person-player-3a
	}
}
ch2-lambda-person-player-3a {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Okay, maybe it's a little invasive."
		" "
		"<m>LAMBDA:</> Yeah. They've made it easier for me to help people in the castle from time to time, but I'm a little embarrassed to admit that they're everywhere."
	}
	goto ch2-lambda-topic-person-s
}
ch2-lambda-person-player-3b {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> It's not that invasive."
		" "
		"<m>LAMBDA:</> It's true I would feel more nervous about a stranger inside the castle if the cameras were turned off or removed, but I'm not going to argue they're not invasive."
	}
	goto ch2-lambda-topic-person-s
}

// Lambda

ch2-lambda-person-lambda {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Tell me about you!"
		" "
		"<m>LAMBDA:</> Me? Ohh, ummm... what's to know? I mean, I'm not that interesting, really...."
	}
	goto ch2-lambda-person-lambda-q
}

ch2-lambda-person-lambda-q {
	show serial dialog {
		" "
		"(Ask:)"
		# "Where are you?" : ch2-lambda-person-lambda-where
		# "How long have you been here?" : ch2-lambda-person-lambda-time
		# "What's your real name?" : ch2-lambda-person-lambda-name
		# "(Go back)" : ch2-lambda-topic-person-start
	}
}
ch2-lambda-person-lambda-where {
	show serial dialog spacer
	if (flag ch2-seen-room-99 is false) {
		show serial dialog {
			"<c>YOU:</> Where are you?"
			"TODO: convo"
		}
	} else {
		if (variable ch2-in-room is 99) {
			show serial dialog {
				"<c>YOU:</> Where are you? I mean, where are WE? There aren't any doors here."
			}
		} else {
			show serial dialog {
				"<c>YOU:</> Where are you? I mean, spatially, where is that lab of yours? There weren't any doors inside."
			}
		}
		show serial dialog {
			" "
			"<m>LAMBDA:</> I'd say it's a pocket dimension, but it's more mundane than that. It's just its own separate space somewhere under the mountain. I set it all up back before everything went south, back when everyone was using the mainframe for all sorts of trivial nonsense."
		}
	}
	goto ch2-lambda-person-lambda-q
}
ch2-lambda-person-lambda-time {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> How long have you been here?"
		" "
		"<m>LAMBDA:</> Where I am now, or at the castle in general?"
		" "
		# "Where you are now." : ch2-lambda-person-lambda-time-2a
		# "The castle in general." : ch2-lambda-person-lambda-time-2b
		# "Both." : ch2-lambda-person-lambda-time-2c
	}
	goto ch2-lambda-person-lambda-q
}
ch2-lambda-person-lambda-time-2a {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Where you are now."
		"TODO: rest of the convo"
	}
	goto ch2-lambda-person-lambda-q
}
ch2-lambda-person-lambda-time-2b {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> The castle in general."
		"TODO: rest of the convo"
	}
	goto ch2-lambda-person-lambda-q
}
ch2-lambda-person-lambda-time-2c {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Both."
		"TODO: rest of the convo"
	}
	goto ch2-lambda-person-lambda-q
}
ch2-lambda-person-lambda-name {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What's your real name? It's not really \"Lambda,\" is it?"
		" "
		"<m>LAMBDA:</> Well, no. But you see... this is a little embarrassing, but...."
		"\tI... sort of... don't remember my real name."
		" "
		# "Are you kidding? You can't expect me to believe that." : ch2-lambda-person-lambda-name-2a
		# "How could you forget your own name? What happened?" : ch2-lambda-person-lambda-name-2b
	}
}
ch2-lambda-person-lambda-name-2a {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Are you kidding? You can't expect me to believe that."
		" "
		"<m>LAMBDA:</> You have to understand... this place, our memories... it's all ones and zeroes."
		"\tWhen everything is digital, it is <bold>absolutely</> possible for information to disappear permanently. In my case...."
	}
	goto ch2-lambda-person-lambda-name-2c
}
ch2-lambda-person-lambda-name-2b {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> How could you forget your own name? What happened?"
		" "
		"<m>LAMBDA:</> Our world is entirely digital. All ones and zeroes. Even you and me. So what happened was...."
	}
	goto ch2-lambda-person-lambda-name-2c
}
ch2-lambda-person-lambda-name-2c {
	show serial dialog {
		"\t...I found the place in the world where my name linked to my data as an entity... and destroyed it."
		" "
		# "What? But why?" : ch2-lambda-person-lambda-name-3
	}
}
ch2-lambda-person-lambda-name-3 {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What? But why?"
		" "
		"<m>LAMBDA:</> I did it to protect myself from the <r>Big Bad</>. He couldn't target me with hacks without knowing my name, so I made it impossible to learn my name, or even know my name."
		"\tI severed more than I intended, but...."
	}
	goto ch2-lambda-person-lambda-name-followup
}
ch2-lambda-person-lambda-name-followup {
	show serial dialog {
		" "
		"(Ask a followup question:)"
		# "What else was severed?" : ch2-lambda-person-lambda-name-followup-severed
		# "Is that why the Big Bad doesn't have a name?" : ch2-lambda-person-lambda-name-followup-bigbad
		# "Does a nickname not count as a name?" : ch2-lambda-person-lambda-name-followup-nickname
		# "Why \"Lambda\" though?" : ch2-lambda-person-lambda-name-followup-lambda
		# "(Quit)" : ch2-lambda-person-lambda-q
	}
}

ch2-lambda-person-lambda-name-followup-severed {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What else was severed?"
		" "
		"<m>LAMBDA:</> There are gaps in my mind. I... I think they might have appeared when I deleted my name. Hard to tell."
		"\tIt's a lot harder to remember people, for a start. My skills seem to be intact, but I feel... disconnected. It's like everything I <bold>can</> remember is stripped of context."
	}
	goto ch2-lambda-person-lambda-name-followup
}
ch2-lambda-person-lambda-name-followup-bigbad {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Is that why the <r>Big Bad</> doesn't have a name? Did he destroy his name, too, so that other hackers can't target him for hacks?"
		" "
		"<m>LAMBDA:</> Probably. Good insight."
		"\tIn his case, though, I think he likes the mystique, too."
	}
	goto ch2-lambda-person-lambda-name-followup
}
ch2-lambda-person-lambda-name-followup-nickname {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Does a nickname not count as a name? I've been calling you \"Lambda,\" after all. Isn't that a name?"
		" "
		"<m>LAMBDA:</> No, by \"name\" I don't mean the label on my messages, or what someone calls me."
		"\tI destroyed my <m>name</>, which is to say I damaged the world's mechanism of referring to me at all. If someone else's memories link to me, that link has been severed, because my name is now a broken reference. I am an unreachable entity."
		"\tI've since learned that many links go in two directions...."
	}
	goto ch2-lambda-person-lambda-name-followup
}

ch2-lambda-person-lambda-name-followup-lambda {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Why \"Lambda\" though?"
		" "
		"<m>LAMBDA:</> I thought of it on the spot. It's because I'm a <bold>functionary with no name</>, you see! You see?"
		"\tNo? Oh, um... yeah, never mind."
	}
	goto ch2-lambda-person-lambda-name-followup
}

// Big Bad

ch2-lambda-person-big-bad {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Tell me about the <r>Big Bad</>."
		" "
		"<m>LAMBDA:</> Oh, um, that's complicated."
		"TODO: rest of the convo"
	}
	goto ch2-lambda-topic-person-s
}

// Bob

ch2-lambda-person-bob {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Tell me about Bob."
		" "
		"<m>LAMBDA:</> Bob? Which one? I know plenty. Fat Bobs, skinny Bobs, Bobs who climb on rocks... oh yeah, and Bobs who ARE rocks...."
		"\t...Well, okay, I actually don't <bold>know</> any of them, but I know <bold>of</> them...."
	}
	goto ch2-lambda-topic-person-s
}

// Bert

ch2-lambda-person-bert {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> About Bert...."
	}
	if (flag ch2-lambda-bert-explanation is true) {
		goto ch2-lambda-bert-q
	} else {
		show serial dialog {
			" "
			"<m>LAMBDA:</> Bert? I don't know any Berts."
		}
	}
	goto ch2-lambda-topic-person-s
}

ch2-lambda-bert-q {
	show serial dialog {
		" "
		"(Ask about:)"
		# "What is Bert like?" : ch2-lambda-bert-like
		# "Bert's never mentioned you." : ch2-lambda-bert-mentioned
		# "What did he mean by 'back door?'" : ch2-lambda-bert-door
		# "Did Bert leave a message yet?" : ch2-lambda-bert-message
		# "(Quit)" : ch2-lambda-bert-quit
	}
}

ch2-lambda-bert-like {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What is Bert like?"
		" "
		"<m>LAMBDA:</> Bert is a surprisingly sweet guy. He doesn't say much, but he's actually pretty smart. And he's irritatingly stubborn when he gets stuck on something though. Once, way back, when...."
		"\tBack when... hmmm."
		" "
		"<c>YOU:</> What is it?"
		" "
		"<m>LAMBDA:</> It was a fleeting thought, but it's gone now. Sorry, I can't remember what I was going to say."
	}
	goto ch2-lambda-bert-q
}
ch2-lambda-bert-mentioned {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Bert's never mentioned you."
		" "
		"<m>LAMBDA:</> Well... he might not know I'm here. In the castle."
		" "
		"<c>YOU:</> But the village elders knew XA was here. In the castle."
		" "
		"<m>LAMBDA:</> Yes, well, that's because...."
		"\tLook... um... sometimes it's not so bad being left behind, you know? They're all better off without me. It's complicated, but by staying apart, I'm keeping everyone safer, believe it or not."
	}
	goto ch2-lambda-bert-q
}
ch2-lambda-bert-door {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> What did Bert mean by 'back door?'"
		" "
		"<m>LAMBDA:</> A backdoor is a secret way into a secure area. Hackers will create backdoors so they can still get in somewhere after the normal entrance is no longer accessable to them."
		" "
		"<c>YOU:</> But Bert's not a hacker! Or can <bold>anyone</> use a backdoor?"
		" "
		"<m>LAMBDA:</> I mean, it's probably <bold>easier</> if you're a hacker, but a door's a door. Say, there's probably still a backdoor or two in the village you could find if you tried. No hacking required!"
	}
	goto ch2-lambda-bert-q
}

ch2-lambda-bert-message {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Did Bert leave a message yet?"
		" "
	}
	if (flag interrupt is true) {
		show serial dialog {
			"<m>LAMBDA:</> Haha! He's only been gone a moment! Be patient."
		}
	} else {
		show serial dialog {
			"<m>LAMBDA:</> Nothing so far, sorry...."
		}
	}
	goto ch2-lambda-bert-q
}

ch2-lambda-bert-quit {
	if (variable CALLBACK is 1) {
		mutate CALLBACK = 0
		show serial dialog spacer
		show serial dialog {
			"<m>LAMBDA:</> Yeah, sorry to go off on a long tangent like that when you were probably calling me about something else."
			"\tDid you have a general question?"
			" "
			# "Yes" : ch2-lambda-topic-q
			# "No" : ch2-lambda-bert-quitquit
		}
	} else {
		goto ch2-lambda-topic-person-s
	}
}
ch2-lambda-bert-quitquit {
	show serial dialog spacer
	show serial dialog {
		"<c>YOU:</> Nope, no questions right now."
		" "
		"<m>LAMBDA:</> Cool! Just let me know if you want to know something."
	}
}
