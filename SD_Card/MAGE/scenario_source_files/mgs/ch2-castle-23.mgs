settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-castle-23 {
	show serial dialog spacer
	show serial dialog {
		"TODO: POWER PLANT on_look text\n "
	}
}

look-ch2-rocco {
	show serial dialog spacer
	show serial dialog {
		"You looked at <m>Rocco</>."
		" "
		"TODO: Rocco on_look"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-23 {
	// ch2 room state
	mutate ch2-in-room = 23

	// DEBUG
	register get + thing -> debug-get-thing

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-23 is false) {
		show serial dialog { "Discovered <bold><c>POWER PLANT</>! Room added to map!" }
		set flag ch2-seen-room-23 to true
	} else {
		show serial dialog { "Entering <bold>POWER PLANT</>..." }
	}
	if (
		flag ch2-installed-heatsink is true
		|| flag ch2-carrying-heatsink is true
	) {
		copy script ch2-hide-heatsink
	}
	goto ch2-map-init
}
debug-get-thing {
	set flag ch2-carrying-cactuscooler to true
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-23 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-23 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-23-south {
	copy on_exit-ch2-castle-23
	copy warping-out-fade-out
	load map ch2-castle-22
}

/* ---------- ENTITIES ---------- */

interact-ch2-rocco {
	copy face-player
	if (flag ch2-cactuscooler-delivered is true) {
		show dialog {
			SELF "TODO\n(Love Cactus Cooler! To cool the machines, I mean.)"
		}
	}
	else if (flag ch2-rocco-backstory is false) {
		show dialog {
			SELF "TODO\n(Can't give power supply; bring us Cactus Cooler first.)"
			"(Blah blah blah)"
		}
		set flag ch2-rocco-backstory to true
		set flag ch2-want-cactuscooler to true
		if (flag ch2-carrying-cactuscooler is true) {
			show dialog {
				SELF "Hey, wait a minute! You have a can of Cactus Cooler already! I don't suppose...."
			}
			set flag interrupt to true
			goto interact-ch2-rocco-give-q // jump
		}
	}
	else if (flag ch2-carrying-cactuscooler is true) {
		if (flag interrupt is false) {
			show dialog {
				SELF "Oh, it looks like you found a can of Cactus Cooler somewhere! Willing to give it to us?"
			}
			set flag interrupt to true
		}
		else {
			show dialog {
				SELF "Change your mind? Willing to give up your can of Cactus Cooler?"
			}
		}
		goto interact-ch2-rocco-give-q // jump
	}
	else {
		show dialog {
			SELF "Remember, we'll give up this heat sink if you bring us a substitute. A can of Cactus Cooler will do."
		}
	}
	goto interact-ch2-rocco-wrapup
}

interact-ch2-rocco-give-q {
	show dialog {
		name "" "(Give %Rocco% the Cactus Cooler)?"
		> "Yes" : interact-ch2-rocco-give
		> "No" : interact-ch2-rocco-nogive
	}
}

interact-ch2-rocco-give {
	show dialog {
		SELF "Hey, thanks, buddy!"
		name "" "(Gave %Rocco% the can of Cactus Cooler!)"
		SELF "Hey, that's swell! Go ahead and take that heat sink now. We'll be all right with this stuff instead."
	}
	set flag ch2-cactuscooler-delivered to true
	set flag ch2-heatsink-permission to true
	set flag ch2-carrying-cactuscooler to false
	goto interact-ch2-rocco-wrapup
}

interact-ch2-rocco-nogive {
	show dialog {
		SELF "Aww, come on, now! No Cactus Cooler, no heat sink! That was the deal!"
	}
	goto interact-ch2-rocco-wrapup
}

interact-ch2-rocco-wrapup {
	set entity "%SELF%" interact_script to interact-ch2-rocco
}

/* ---------- HEAT SINK (ADMIN) ---------- */

ch2-hide-heatsink {
	teleport entity "Heat Sink" to geometry hiding-spot
	set entity "Heat Sink" name to " "
}

ch2-touch-heatsink {
	if (flag ch2-heatsink-firstlook is false) {
		show dialog {
			PLAYER "TODO heatsink1"
		}
		set flag ch2-heatsink-firstlook to true
	}
	show dialog {
		PLAYER "TODO heatsink2."
	}
	if (flag ch2-heatsink-permission is false) {
		show dialog {
			PLAYER "But I think the power plant is still using this. I better convince the guys to let me have it."
		}
	} else {
		show dialog {
			name "" "(Pick up the heat sink?)"
			> "Yes" : ch2-touch-heatsink-yes
			> "No" : ch2-touch-heatsink-no
		}
	}
	set entity "%SELF%" interact_script to ch2-touch-heatsink // RESET
}
ch2-touch-heatsink-yes {
	show dialog {
		name "" "(You pick up the heat sink!)"
	}
	copy script ch2-hide-heatsink
	set flag ch2-carrying-heatsink to true
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-heatsink-no {
	show dialog {
		PLAYER "Another time, perhaps."
	}
	set entity "%SELF%" interact_script to ch2-touch-heatsink // RESET
}

ch2-install-heatsink {
	set flag ch2-carrying-heatsink to false
	set flag ch2-installed-heatsink to true
}
look-heatsink {
	show serial dialog spacer
	show serial dialog {
		"on_look heatsink TODO"
	}
}
