settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}

/* ---------- LOOK SCRIPTS ---------- */

on_look-ch2-castle-23 {
	show serial dialog spacer
	show serial dialog {
		"TODO: POWER PLANT on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-23 {
	// ch2 room state
	mutate ch2-in-room = 23

	// entrance text
	show serial dialog spacer
	if (flag ch2-seen-room-23 is false) {
		show serial dialog { "Discovered <bold><c>POWER PLANT</>! Room added to map!" }
		set flag ch2-seen-room-23 to true
	} else {
		show serial dialog { "Entering <bold>POWER PLANT</>..." }
	}
	if (
		flag ch2-installed-heatsink is true
		|| flag ch2-carrying-heatsink is true
	) {
		copy script ch2-hide-heatsink
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-23 {
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-23 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-23-south {
	copy on_exit-ch2-castle-23
	copy warping-out-fade-out
	load map ch2-castle-22
}

/* ---------- HEATSINK (ADMIN) ---------- */

ch2-hide-heatsink {
	teleport entity "Heat Sink" to geometry hiding-spot
	set entity "Heat Sink" name to " "
}

ch2-touch-heatsink {
	if (flag ch2-heatsink-firstlook is false) {
		show dialog {
			PLAYER "TODO heatsink1"
		}
		set flag ch2-heatsink-firstlook to true
	}
	show dialog {
		PLAYER "TODO heatsink2."
	}
	show dialog {
		name "" "(Pick up the heat sink?)"
		> "Yes" : ch2-touch-heatsink-yes
		> "No" : ch2-touch-heatsink-no
	}
	set entity "%SELF%" interact_script to ch2-touch-heatsink // RESET
}
ch2-touch-heatsink-yes {
	show dialog {
		name "" "(You pick up the heat sink!)"
	}
	copy script ch2-hide-heatsink
	set flag ch2-carrying-heatsink to true
	set entity "%SELF%" interact_script to null_script
}
ch2-touch-heatsink-no {
	show dialog {
		PLAYER "Maybe later, then."
	}
	set entity "%SELF%" interact_script to ch2-touch-heatsink // RESET
}

ch2-install-heatsink {
	set flag ch2-carrying-heatsink to false
	set flag ch2-installed-heatsink to true
}
on_look-heatsink {
	show serial dialog spacer
	show serial dialog {
		"on_look heatsink TODO"
	}
}
