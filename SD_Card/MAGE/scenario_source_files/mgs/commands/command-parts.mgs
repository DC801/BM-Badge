settings for serial dialog { wrap 60 }

// REGISTER

register-parts {
	// the default set
	register parts -> command-parts
}

// MAN PAGE

man-parts {
	show serial dialog spacer
	show serial dialog {
		"<c><bold>MGE GENERAL COMMANDS MANUAL - PARTS</>"
		" "
		"Calls Lambda via <bold>lambda-talk</> using the subject line: <bold>PARTS</>."
		" "
		"<m>NOTE</>: Hi, there, birthday mage! This is just something I rigged up quickly so you could have a shortcut to talk to me about the parts list. :)"
	}
}

// COMMAND

command-parts {
	if (
		flag ch2-ring-zero-restored is true // the artifact is full power
		|| variable ch2-in-room is not 0 // OR you're in the castle
	) {
		// `parts` works normally
		show serial dialog spacer
		show serial dialog {
			"<m>LAMBDA:</> For parts, we still need:"
		}
		goto command-parts-print
	} else {
		// otherwise, `parts` cannot reach Lambda
		copy ch2-wireless-error
	}
}
command-parts-print {
	if (variable ch2-storyflag-round is == 0) {
		show serial dialog { "<r>DEBUG:</> You've asked for a parts list but 'ch2-storyflag-round' is still set to '0'!" }
	}
	// ROUND ONE: normal
	if (variable ch2-storyflag-round is == 1) {

		if (flag ch2-installed-heatsink is true) {
			show serial dialog { "\t- [x] heat sink" }
		} else if (flag ch2-carrying-heatsink is true) {
			show serial dialog { "\t- [~] heat sink" }
		} else {
			show serial dialog { "\t- [ ] heat sink" }
		}

		if (flag ch2-installed-powersupply is true) {
			show serial dialog { "\t- [x] power supply" }
		} else if (flag ch2-carrying-powersupply is true) {
			show serial dialog { "\t- [~] power supply" }
		} else {
			show serial dialog { "\t- [ ] power supply" }
		}

		if (flag ch2-installed-monitor is true) {
			show serial dialog { "\t- [x] monitor" }
		} else if (flag ch2-carrying-monitor is true) {
			show serial dialog { "\t- [~] monitor" }
		} else {
			show serial dialog { "\t- [ ] monitor" }
		}
	}
	// ROUND TWO: abstract
	else if (variable ch2-storyflag-round is == 2) {

		if (flag ch2-installed-harddrive is true) {
			show serial dialog { "\t- [x] hard drive" }
		} else if (flag ch2-carrying-harddrive is true) {
			show serial dialog { "\t- [~] hard drive" }
		} else {
			show serial dialog { "\t- [ ] hard drive" }
		}
		if (flag ch2-installed-plate is true) {
			show serial dialog { "\t\t- [x] dinner plate" }
		} else if (flag ch2-carrying-plate is true) {
			show serial dialog { "\t\t- [~] dinner plate" }
		} else {
			show serial dialog { "\t\t- [ ] dinner plate" }
		}
		if (flag ch2-installed-needle is true) {
			show serial dialog { "\t\t- [x] phonograph needle" }
		} else if (flag ch2-carrying-needle is true) {
			show serial dialog { "\t\t- [~] phonograph needle" }
		} else {
			show serial dialog { "\t\t- [ ] phonograph needle" }
		}

		if (flag ch2-installed-keyboard is true) {
			show serial dialog { "\t- [x] keyboard" }
		} else if (flag ch2-carrying-keyboard is true) {
			show serial dialog { "\t- [~] keyboard" }
		} else {
			show serial dialog { "\t- [ ] keyboard" }
		}

		if (flag ch2-installed-mouse is true) {
			show serial dialog { "\t- [x] mouse" }
		} else if (flag ch2-carrying-mouse is true) {
			show serial dialog { "\t- [~] mouse" }
		} else {
			show serial dialog { "\t- [ ] mouse" }
		}
	}
	// ROUND THREE: desperate
	else if (variable ch2-storyflag-round is == 3) {

		if (flag ch2-installed-ramchips is true) {
			show serial dialog { "\t- [x] RAM chips" }
		} else if (flag ch2-carrying-ramchips is true) {
			show serial dialog { "\t- [~] RAM chips" }
		} else {
			show serial dialog { "\t- [ ] RAM chips" }
		}

		if (flag ch2-installed-clock is true) {
			show serial dialog { "\t- [x] clock" }
		} else if (flag ch2-carrying-clock is true) {
			show serial dialog { "\t- [~] clock" }
		} else {
			show serial dialog { "\t- [ ] clock" }
		}

		if (flag ch2-installed-cpu is true) {
			show serial dialog { "\t- [x] cpu" }
		} else if (flag ch2-carrying-cpu is true) {
			show serial dialog { "\t- [~] cpu" }
		} else {
			show serial dialog { "\t- [ ] cpu" }
		}
		if (flag ch2-installed-goldfish is true) {
			show serial dialog { "\t\t- [x] goldfish" }
		} else if (flag ch2-carrying-goldfish is true) {
			show serial dialog { "\t\t- [~] goldfish" }
		} else {
			show serial dialog { "\t\t- [ ] goldfish" }
		}
		if (flag ch2-installed-abacus is true) {
			show serial dialog { "\t\t- [x] abacus" }
		} else if (flag ch2-carrying-abacus is true) {
			show serial dialog { "\t\t- [~] abacus" }
		} else {
			show serial dialog { "\t\t- [ ] abacus" }
		}

	}
	// FINAL ROUND: software
	else if (variable ch2-storyflag-round is == 4) {
		goto command-parts-finished
		set flag temp to true
	}
	// TODO: Lambda shouldn't offer a parts list at 5+ -- do something about it!
	else {
		show serial dialog spacer
		show serial dialog { "<r>DEBUG:</> Your 'ch2-storyflag-round' value is borked! (Current value: '$ch2-storyflag-round$')" }
	}

	// FINISH
	goto command-parts-q
}

command-man-stop {
	show serial dialog spacer
	show serial dialog lambda-over
}

command-parts-q {
	show serial dialog {
		" "
		"(Type the part name for more info, <c>PARTS</> to see the parts list again, or <c>QUIT</> to stop)"

		_ "PARTS" : command-parts

		_ "Q" : command-man-stop
		_ "QUIT" : command-man-stop
		_ "EXIT" : command-man-stop

		// ROUND ONE: normal
		
		_ "MONITOR" : command-parts-monitor

		_ "HEAT SINK" : command-parts-heatsink
		_ "HEATSINK" : command-parts-heatsink

		_ "POWER SUPPLY" : command-parts-powersupply
		_ "POWERSUPPLY" : command-parts-powersupply

		// ROUND TWO: abstract
		
		_ "KEYBOARD" : command-parts-keyboard
		
		_ "MOUSE" : command-parts-mouse
		
		_ "HARD DRIVE" : command-parts-harddrive
		_ "HARDDRIVE" : command-parts-harddrive
		_ "HARD DISK" : command-parts-harddrive
		_ "HARDDISK" : command-parts-harddrive
		_ "HDD" : command-parts-harddrive

		// ROUND THREE: desperate

		_ "RAM CHIPS" : command-parts-ramchips
		_ "RAMCHIPS" : command-parts-ramchips
		_ "RAM" : command-parts-ramchips
		
		_ "CLOCK" : command-parts-clock

		_ "CPU" : command-parts-cpu

		_ "GOLDFISH" : command-parts-goldfish

		_ "ABACUS" : command-parts-abacus
	}
	show serial dialog spacer
	show serial dialog {
		"<m>LAMBDA:</> Not sure what you mean. Want to ask about something else?"
	}
	goto command-parts-q
}

ch2-describe-monitor { show serial dialog ch2-describe-monitor }
ch2-describe-heatsink { show serial dialog ch2-describe-heatsink }
ch2-describe-powersupply { show serial dialog ch2-describe-powersupply }

ch2-describe-keyboard { show serial dialog ch2-describe-keyboard }
ch2-describe-mouse { show serial dialog ch2-describe-mouse }
ch2-describe-harddrive { show serial dialog ch2-describe-harddrive }
ch2-describe-plate { show serial dialog ch2-describe-plate }
ch2-describe-needle { show serial dialog ch2-describe-needle }

ch2-describe-ramchips { show serial dialog ch2-describe-ramchips }
ch2-describe-clock { show serial dialog ch2-describe-clock }
ch2-describe-cpu { show serial dialog ch2-describe-cpu }
ch2-describe-goldfish { show serial dialog ch2-describe-goldfish }
ch2-describe-abacus { show serial dialog ch2-describe-abacus }

command-parts-ahead {
	show serial dialog spacer
	show serial dialog { "<m>LAMBDA:</> Hold on! Don't get ahead of yourself, haha.... For now just focus on the below:" }
	goto command-parts-print
}
command-parts-ahead-confused {
	show serial dialog spacer
	show serial dialog { "<m>LAMBDA:</> I don't know if that will be of any use, but... want to ask about something else?" }
	goto command-parts-q
}

// ROUND ONE: normal

// TODO: different convo for carrying vs installed vs needed

command-parts-monitor {
	show serial dialog spacer
	if (flag ch2-installed-monitor is true) {
		show serial dialog { "<m>LAMBDA:</> You've got the monitor all set up already. If it looks broken, it's only because the mainframe can't boot up yet." }
	} else if (flag ch2-carrying-monitor is true) {
		show serial dialog { "<m>LAMBDA:</> Looks like you've got the monitor already. To install it, all you'll need to do is find the matching ports on the mainframe enclosure. It's all keyed, so there's no plugging anything in the wrong way." "\tOh, and don't forget to plug the monitor into power." }
	} else {
		copy ch2-describe-monitor
	}
	goto command-parts-q
}
command-parts-heatsink {
	show serial dialog spacer
	if (flag ch2-installed-heatsink is true) {
		show serial dialog { "<m>LAMBDA:</> You've got the heat sink in there already. Oh, I guess this means I can turn off the furnace in there now." }
	} else if (flag ch2-carrying-heatsink is true) {
		show serial dialog { "<m>LAMBDA:</> Looks like you've got the heat sink already!" "\tTODO: You can't put the heatsink on until the CPU is in place, dummy! O.o" }
	} else {
		copy ch2-describe-heatsink
	}
	goto command-parts-q
}
command-parts-powersupply {
	show serial dialog spacer
	if (flag ch2-installed-powersupply is true) {
		show serial dialog { "<m>LAMBDA:</> TODO powersupply is already installed." }
	} else if (flag ch2-carrying-powersupply is true) {
		show serial dialog { "<m>LAMBDA:</> TODO: how to install powersupply" }
	} else {
		copy ch2-describe-powersupply
	}
	goto command-parts-q
}

// ROUND TWO: abstract

command-parts-keyboard {
	if (variable ch2-storyflag-round is < 2) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-keyboard is true) {
		show serial dialog { "<m>LAMBDA:</> TODO keyboard is already installed." }
	} else if (flag ch2-carrying-keyboard is true) {
		show serial dialog { "<m>LAMBDA:</> TODO: how to install keyboard" }
	} else {
		copy ch2-describe-keyboard
	}
	goto command-parts-q
}
command-parts-mouse {
	if (variable ch2-storyflag-round is < 2) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-mouse is true) {
		show serial dialog { "<m>LAMBDA:</> TODO mouse is already installed." }
	} else if (flag ch2-carrying-mouse is true) {
		show serial dialog { "<m>LAMBDA:</> TODO: how to install mouse" }
	} else {
		copy ch2-describe-mouse
	}
	goto command-parts-q
}
command-parts-harddrive {
	if (variable ch2-storyflag-round is < 2) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-harddrive is true) {
		show serial dialog { "<m>LAMBDA:</> TODO hard drive is already installed." }
	} else if (flag ch2-carrying-harddrive is true) {
		show serial dialog { "<m>LAMBDA:</> The hard drive will need to be plugged into two cables: first, a power cable from the power supply, and second, a SATA cable off the motherboard." "\tSince that hard drive is... uh, unconventional... just try your best to attach those cables somehow. You'll know if it fits." }
	} else {
		copy ch2-describe-harddrive
		show serial dialog {
			"\tUse the <c>FUSE</> command to combine both items into a <m>HARD DRIVE</>."
		}
	}
	goto command-parts-q
}
command-parts-plate {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead-confused
	}
	show serial dialog spacer
	if (flag ch2-installed-plate is true) {
		show serial dialog { "<m>LAMBDA:</> You've already... um, \"used\" the plate to make a CPU. You're good." }
	} else if (flag ch2-carrying-plate is true) {
		if (flag ch2-carrying-needle is false) {
			show serial dialog { "<m>LAMBDA:</> You've got a good spinnable plate already. So now what you need is a phonograph needle to act as a read head." }
		} else {
			show serial dialog { "<m>LAMBDA:</> Now that you have both the plate and needle, you can use the <c>FUSE</> command to combine them." "\tAh, so it seems the plate has enough iron content in the ceramic for this to actually work, huh?" }
		}
	} else {
		copy ch2-describe-plate
	}
	goto command-parts-q
}
command-parts-needle {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead-confused
	}
	show serial dialog spacer
	if (flag ch2-installed-needle is true) {
		show serial dialog { "<m>LAMBDA:</> You've already... um, \"used\" the needle to make a CPU. You're good." }
	} else if (flag ch2-carrying-needle is true) {
		if (flag ch2-carrying-plate is false) {
			show serial dialog { "<m>LAMBDA:</> You've got a read head -- of sorts -- already. So now what you need is a dinner plate to act as a hard drive platter." }
		} else {
			show serial dialog { "<m>LAMBDA:</> Now that you have both the needle and plate, you can use the <c>FUSE</> command to combine them." "\tOh, wow, it looks like that phonograph needle is electric. Weird... but also serendipitous." }
		}
	} else {
		copy ch2-describe-needle
	}
	goto command-parts-q
}
command-parts-ramchips {
	if (variable ch2-storyflag-round is < 2) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-ramchips is true) {
		show serial dialog { "<m>LAMBDA:</> TODO ramchips is already installed." }
	} else if (flag ch2-carrying-ramchips is true) {
		show serial dialog { "<m>LAMBDA:</> To install RAM... \"chips...\" you'll need to find pairs of matching RAM slots on the motherboard. Usually you'd want to use two or four -- it just can't be an odd number. And then...." "\tWell, it's going to be really freaky, but you need to like <bold>SHOVE</> them in there, all right? They need to really <bold>CRUNCH</> on in there." }
	} else {
		copy ch2-describe-ramchips
	}
	goto command-parts-q
}

// ROUND THREE: desperate

command-parts-clock {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-clock is true) {
		show serial dialog { "<m>LAMBDA:</> The clock's installed already. It's just... you know... clocking away." }
	} else if (flag ch2-carrying-clock is true) {
		show serial dialog { "<m>LAMBDA:</> TODO: how to install clock" }
	} else {
		copy ch2-describe-clock
	}
	goto command-parts-q
}
command-parts-cpu {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead
	}
	show serial dialog spacer
	if (flag ch2-installed-cpu is true) {
		show serial dialog { "<m>LAMBDA:</> Your CPU is in place already, ready to go off and be the brain of the whole operation." } // TODO: "Oh, I guess this means you can install the heat sink now."
	} else if (flag ch2-carrying-cpu is true) {
		show serial dialog { "<m>LAMBDA:</> TODO: how to install cpu" }
	} else {
		copy ch2-describe-cpu
	}
	goto command-parts-q
}
command-parts-goldfish {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead-confused
	}
	show serial dialog spacer
	if (flag ch2-installed-goldfish is true) {
		show serial dialog { "<m>LAMBDA:</> You've already... um, \"used\" the goldfish to make a CPU. You're good." }
	} else if (flag ch2-carrying-goldfish is true) {
		if (flag ch2-carrying-abacus is false) {
			show serial dialog { "<m>LAMBDA:</> You've got the goldfish already, the \"thinking\" part of the CPU, so you'll need to pick up the other half: the abacus." }
		} else {
			show serial dialog { "<m>LAMBDA:</> Now that you have both the goldfish and abacus, you can use the <c>FUSE</> command to combine them." "\tAh... don't worry about the fact that the goldfish is sentient and its identity will be transmogrified by this. It's all going to be fine." }
		}
	} else {
		copy ch2-describe-goldfish
	}
	goto command-parts-q
}
command-parts-abacus {
	if (variable ch2-storyflag-round is < 3) {
		goto command-parts-ahead-confused
	}
	show serial dialog spacer
	if (flag ch2-installed-abacus is true) {
		show serial dialog { "<m>LAMBDA:</> You've already got the abacus combined with the goldfish into a CPU, so don't worry about the abacus half of things now." }
	} else if (flag ch2-carrying-abacus is true) {
		if (flag ch2-carrying-goldfish is false) {
			show serial dialog { "<m>LAMBDA:</> You've got an abacus already, the \"remembering\" part of the CPU, so you'll need to pick up the other half: the goldfish." }
		} else {
			show serial dialog { "<m>LAMBDA:</> Now that you have both the abacus and goldfish, you can use the <c>FUSE</> command to combine them." "\tYeah, it's... um... just kind of a magical invocation that doesn't make a lot of physical sense. But just trust the process." }
		}
	} else {
		copy ch2-describe-abacus
	}
	goto command-parts-q
}

// FINISH

command-parts-finished {
	show serial dialog spacer
	show serial dialog {
		"\tOh, wait, is that everything? Huzzah! (TODO)"
	}
}
