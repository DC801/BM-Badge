settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}
settings for serial dialog { wrap 60 }

ch2-report-inventory {
	set flag temp to false
	show serial dialog spacer
	show serial dialog {
		"<c>INVENTORY:</>"
	}
	if (flag ch2-carrying-clock is true) {
		show serial dialog { "\tclock" }
		set flag temp to true
	}
	if (flag ch2-carrying-cpu is true) {
		show serial dialog { "\tcpu" }
		set flag temp to true
	}
	if (flag ch2-carrying-heatsink is true) {
		show serial dialog { "\theat sink" }
		set flag temp to true
	}
	if (flag ch2-carrying-keyboard is true) {
		show serial dialog { "\tkeyboard" }
		set flag temp to true
	}
	if (flag temp is false) {
		show serial dialog { "\t(nothing)" }
	}
	set flag temp to false
}

ch2-count-flags {
	set flag ch2-storyflags-tallied to false
	mutate ch2-storyflags-tally = 0
	if (flag ch2-installed-clock is true) {
		mutate ch2-storyflags-tally + 1
	}
	if (flag ch2-installed-heatsink is true) {
		mutate ch2-storyflags-tally + 1
	}
	if (flag ch2-installed-keyboard is true) {
		mutate ch2-storyflags-tally + 1
	}
	if (flag ch2-installed-cpu is true) {
		mutate ch2-storyflags-tally + 1
	}
	set flag ch2-storyflags-tallied to true
	set entity "%SELF%" tick_script to null_script
}

ch2-interact-mainframe {
	if (flag ch2-toot-done is false) {
		goto ch2-interact-mainframe-madamada
	}
	set flag temp to false
	if (flag ch2-installed-clock is false) {
		if (flag ch2-carrying-clock is true) {
			show dialog {
				name "" "(Now installing... the grandfather clock!)"
				"(Somehow, that enormous, analog relic works great for a CPU clock!)"
			}
			set flag temp to true
			copy ch2-install-clock
		}
	}
	if (flag ch2-installed-cpu is false) {
		if (flag ch2-carrying-cpu is true) {
			show dialog {
				name "" "(Now installing... the cpu!)"
				"(We have a CPU!)"
			}
			set flag temp to true
			copy ch2-install-cpu
		}
	}
	if (flag ch2-installed-heatsink is false) {
		if (flag ch2-carrying-heatsink is true) {
			show dialog {
				name "" "(Now installing... the heat sink!)"
				"(TODO: heatsink functioning flavor text)"
			}
			set flag temp to true
			copy ch2-install-heatsink
		}
	}
	if (flag ch2-installed-keyboard is false) {
		if (flag ch2-carrying-keyboard is true) {
			show dialog {
				name "" "(Now installing... the keyboard!)"
				"(TODO: keyboard functioning flavor text)"
			}
			set flag temp to true
			copy ch2-install-keyboard
		}
	}
	if (flag temp is false) { // no components were installed
		show dialog {
			name "" "(You don't have any new components to install.)"
		}
	}
	set flag temp to false
	set entity "%SELF%" interact_script to ch2-interact-mainframe
}

ch2-interact-mainframe-madamada {
	show dialog {
		PLAYER "What is this hunk of junk?"
	}
	set entity "%SELF%" interact_script to ch2-interact-mainframe
}
