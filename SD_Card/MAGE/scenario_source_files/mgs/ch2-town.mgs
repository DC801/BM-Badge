settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}

/* ---------- LOOK SCRIPTS ---------- */

on_look-ch2-town {
	show serial dialog spacer
	show serial dialog {
		"   TODO: Town text"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-town {
	if (warp state is cutscene-escort) {
		goto ch2-escort-cutscene
	}
	// SERIAL STUFF (in lockstep with "ch2-common.mgs")
	if (flag ch2-has-artifact is true) {
		set serial control on
	} else {
		set serial control off
	}
	if (flag ch2-toot-done is true) {
		copy ch2-register-manual
	}
	if (flag ch2-map-granted is true) {
		copy ch2-map-enable
	}
	// NORMAL STUFF
	set hex control to on
	set entity Jackob tick_script to yeet
	set entity Alfonso tick_script to yeet
	set entity Bert tick_script to yeet
	// walking out of doorways:
	set player control to off
	if (warp state is warping) {
		copy warping-in-fade-in
	} else if (warp state is exit_from-credits) {
		teleport entity "%PLAYER%" to geometry from-credits-spot
		fade in camera from #000000 over 800ms
	} else if (warp state is walk_from-east) {
		set entity "%PLAYER%" path to walk_from-east
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
		fade in camera from #000000 over 400ms
	} else if (warp state is walk_from-west) {
		set entity "%PLAYER%" path to walk_from-west
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
		fade in camera from #000000 over 400ms
	} else if (warp state is exit_from-bobsclub) {
		walk entity "%PLAYER%" along geometry enter_from-bobsclub over 200ms
	} else if (warp state is exit_from-bakery) {
		walk entity "%PLAYER%" along geometry enter_from-bakery over 200ms
	} else if (warp state is exit_from-lodge) {
		walk entity "%PLAYER%" along geometry enter_from-lodge over 200ms
	} else if (warp state is exit_from-greenhouse) {
		walk entity "%PLAYER%" along geometry enter_from-greenhouse over 200ms
	} else if (warp state is exit_from-magehouse) {
		walk entity "%PLAYER%" along geometry enter_from-magehouse over 200ms
	} else if (warp state is walk_from-oldcouplehouse) {
		walk entity "%PLAYER%" along geometry enter_from-oldcouplehouse over 200ms
	} else if (warp state is exit_from-family) {
		walk entity "%PLAYER%" along geometry enter_from-family over 200ms
	} else if (warp state is exit_from-woprhouse) {
		walk entity "%PLAYER%" along geometry enter_from-woprhouse over 200ms
	} else { // in case warp state is something strange
		goto on_load-ch2-town-fallback
	}
	set player control to on
}

on_load-ch2-town-fallback {
	set player control to on
}

/* ---------- ON_TICK ---------- */

// map loading scripts

on_tick-ch2-town {
	if entity "%PLAYER%" is inside geometry door-bakery-entrance
		then goto load_map-ch2-bakery
	if entity "%PLAYER%" is inside geometry door-family-entrance
		then goto load_map-ch2-family
	if entity "%PLAYER%" is inside geometry door-greenhouse-entrance
		then goto load_map-ch2-greenhouse
	if entity "%PLAYER%" is inside geometry door-magehouse-entrance
		then goto load_map-ch2-magehouse
	if entity "%PLAYER%" is inside geometry door-lodge-entrance
		then goto load_map-ch2-lodge
	if entity "%PLAYER%" is inside geometry door-oldcouplehouse-entrance
		then goto load_map-ch2-oldcouplehouse
	if entity "%PLAYER%" is inside geometry door-woprhouse-entrance
		then goto load_map-ch2-woprhouse
	if entity "%PLAYER%" is inside geometry east-hitbox
		then goto on_walk-ch2-town-east
	if entity "%PLAYER%" is inside geometry west-hitbox
		then goto on_walk-ch2-town-west
}
load_map-ch2-bakery {
	load map bakery
}
load_map-ch2-oldcouplehouse {
	load map ch2-oldcouplehouse
}
load_map-ch2-greenhouse {
	load map greenhouse
}
load_map-ch2-magehouse {
	load map magehouse
}
load_map-ch2-lodge {
	load map lodge
}
load_map-ch2-family {
	load map family
}
load_map-ch2-woprhouse {
	load map woprhouse
}

on_tick-ch2-town-walkfadeout {
	set player control to off
	walk entity "%PLAYER%" to geometry "%ENTITY_PATH%" over 3500ms
	goto null_script
}
on_tick-ch2-town-walkfadein {
	walk entity "%PLAYER%" along geometry "%ENTITY_PATH%" over 500ms
	set player control to on
	goto null_script
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-town {
	// universal behavior
}

on_go-ch2-town-west {
	copy on_exit-ch2-town
	copy warping-out-fade-out
	load map ch2-castle-outside
}

//WALK

on_walk-ch2-town-east {
	set entity "%PLAYER%" path to east-point
	set warp state to walk_from-west
	set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
	fade out camera to #000000 over 500ms
	load map main2
}

on_walk-ch2-town-west {
	set entity "%PLAYER%" path to west-point
	set warp state to walk_from-east
	set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
	fade out camera to #000000 over 500ms
	load map ch2-castle-outside
}

/* ---------- ENTITIES ---------- */

ch2-interact-delmar {
	copy face-player
	show dialog { SELF "...." }
	turn entity "%SELF%" north
}

