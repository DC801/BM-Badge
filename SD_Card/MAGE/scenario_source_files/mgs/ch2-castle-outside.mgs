settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}

const! (
	$CALLBACK = 98
)

/* ---------- LOOK SCRIPTS ---------- */

on_look-ch2-castle-outside {
	show serial dialog spacer
	show serial dialog {
		"   TODO: castle room exterior on_look text"
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-outside {
	mutate CALLBACK = $CALLBACK
	goto ch2-noncastle-init
}

on_load-ch2-castle-outside-RESUME {
	mutate CALLBACK = 0
	set player control to off
	if (warp state is warping) { // warping in
		copy warping-in-fade-in
	} else if (warp state is walk_from-north) {
		walk entity "%PLAYER%" along geometry walk_from-north over 200ms
	} else if (warp state is walk_from-east) {
		set entity "%PLAYER%" path to walk_from-east
		set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadein
			// borrowing this from town scripts
		fade in camera from #000000 over 400ms
	}
	set player control to on
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-outside { // LOOPS HERE
	if entity "%PLAYER%" is inside geometry north-hitbox
		then goto on_walk-castle-outside-north
	if entity "%PLAYER%" is inside geometry east-hitbox
		then goto on_walk-castle-outside-east
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-outside {
	// universal behavior
}

// SERIAL: go

on_go-castle-outside-north {
	copy on_exit-ch2-castle-outside
	copy warping-out-fade-out
	load map ch2-castle-1
}
on_go-castle-outside-east {
	copy on_exit-ch2-castle-outside
	copy warping-out-fade-out
	load map main2
}

// WALK

on_walk-castle-outside-north {
	copy on_exit-ch2-castle-outside
	set warp state to walk_from-south
	load map ch2-castle-1
}

on_walk-castle-outside-east {
	copy on_exit-ch2-castle-outside
	set warp state to walk_from-west
	set entity "%PLAYER%" path to east-point
	set entity "%PLAYER%" tick_script to on_tick-ch2-town-walkfadeout
		// borrowing this from town scripts
	fade out camera to #000000 over 500ms
	load map main2
}

