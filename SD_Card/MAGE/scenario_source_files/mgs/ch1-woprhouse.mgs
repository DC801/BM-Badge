settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label SELF { entity "%SELF%" }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}

const! (
	$CALLBACK = 299
)

on_load-woprhouse {
	if (variable current-chapter is 2) {
		mutate CALLBACK = $CALLBACK
		goto ch2-noncastle-init
	}
	goto on_load-woprhouse-RESUME
}
on_load-woprhouse-RESUME {
	mutate CALLBACK = 0
	if (warp state is warping) {
		copy warping-in-fade-in
	}
	if (flag wopr-backdoor-found is false) {
		set player control to off
		if (warp state is not warped) {
			walk entity "%PLAYER%" along geometry walk_from-north over 600ms
		}
		wait 400ms
		set player control to on
		show dialog {
			PLAYER "Whoa! It looks like I found some kind of back door."
		}
		set flag wopr-backdoor-found to true
	}
}

/* ---------- ON_TICK ---------- */

on_tick-woprhouse {
	if entity "%PLAYER%" is inside geometry north-hitbox
		then goto on_walk-woprhouse-north
}

/* ---------- EXIT STUFF ---------- */

on_go-woprhouse-north {
	copy warping-out-fade-out
	load map main2
}

on_walk-woprhouse-north {
	// triggered by map's on_tick
	if (variable current-chapter is 1) {
		set warp state to exit_from-woprhouse
		load map main
	} else if (variable current-chapter is 2) {
		set warp state to walk_from-woprhouse
		load map main2
	} else {
		show serial dialog { "You're in chapter '$current-chapter$'??" }
	}
}

/* ---------- ENTITIES ---------- */

show_dialog-wopr-start {
	show dialog {
		SELF
		portrait wopr
		"SHALL WE PLAY A GAME?"

		SELF
		portrait wopr
		"PLAY?"
		> "DO NOT PLAY" : goto "restart-wopr"
	}
}

show_dialog-woprbooks {
	show dialog {
		PLAYER "These shelves are full of Vogon poetry! What an odd sort of thing for a computer to collect."
	}
}

restart-wopr {
	set entity "%SELF%" interact_script to show_dialog-wopr-start
}
