settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label Notakon { name Notakon }
}
settings for serial dialog { wrap 60 }

/* ---------- LOOK SCRIPTS ---------- */

on_look-ch2-castle-1 {
	show serial dialog spacer
	show serial dialog {
		"TODO: FOYER on_look text\n "
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-castle-1 {
	// ch2 room state
	mutate ch2-in-room = 1
	set flag interrupt to false
	set flag ch2-seen-room-1 to true

	// entrance text
	if (flag ch2-has-artifact is true) {
		show serial dialog spacer
		show serial dialog {
			"Entering FOYER"
		}
	}

	// misc game state
	if (flag ch2-ring-zero-restored is false) {
		copy disable-hax
	}

	// serial features
	if (flag ch2-has-artifact is true) {
		set serial control on
		copy ch2-register-manual
	} else {
		set serial control off // safety
	}
	if (flag ch2-map-granted is true) {
		copy ch2-map-enable
	}

	// warp-in animation
	if (warp state is warping) {
		copy warping-in-fade-in
	}
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-castle-1 {
	if (flag ch2-ring-zero-restored is true) {
		// normal doorwatch
		goto ch2-castle-1-watch-doors-alone
	} else {
		// watch for hax (and doors)
		goto ch2-castle-1-watch-door-and-hax
	}
}

ch2-castle-1-watch-doors-alone { // LOOPS HERE
	if entity "%PLAYER%" is inside geometry south-hitbox
		then goto on_walk-castle-1-south
}

ch2-castle-1-watch-door-and-hax { // LOOPS HERE
	if entity "%PLAYER%" is inside geometry south-hitbox
		then goto on_walk-castle-1-south
	if button HAX
		then goto ch2-attempt-hax-check
}

/* ---------- EXIT STUFF ---------- */

on_exit-ch2-castle-1 { // sanitize ch2 room state
	mutate ch2-in-room = 0
}

// SERIAL: go

on_go-castle-1-north {
	copy on_exit-ch2-castle-1
	copy warping-out-fade-out
	load map ch2-castle-11
}

on_go-castle-1-south {
	copy on_exit-ch2-castle-1
	copy warping-out-fade-out
	load map ch2-castle-outside
}

// WALK

on_walk-castle-1-south {
	copy on_exit-ch2-castle-1
	set warp state to walk_from-north
	load map ch2-castle-outside
}

/* ---------- TUTORIAL ROOM STUFF ---------- */

// Not going to do shenanigans like this for other rooms because if you've left this room you've seen the tutorial already

ch2-attempt-hax-check {
	if (flag ch2-toot-done is true) {
		// you've been told Ring Zero won't work
		show dialog {
			PLAYER "Oh, yeah. Ring Zero won't work in in the castle until I fix the mainframe."
		}
	} else if (flag ch2-pretoot-hax-attempted is true) {
		// you've already discovered Ring Zero won't work
		show dialog {
			PLAYER "Why doesn't Ring Zero work?"
		}
	} else { // you're attempting to use Ring Zero
		if (flag ch2-pretoot-door-attempted is true) {
			// ...but you tried the door first
			show dialog {
				PLAYER "Weird. I can't get Ring Zero to work, either."
			}
		} else {
			show dialog {
				PLAYER "Weird. I can't get Ring Zero to work."
			}
		}
		set flag ch2-pretoot-hax-attempted to true
	}
	// reset:
	goto on_tick-ch2-castle-1
}

ch2-attempt-door-check {
	if (flag ch2-toot-done is true) {
		// you've been told some doors won't work
		show dialog {
			PLAYER "Oh, yeah. I'll need to move between rooms with the serial console."
		}
	} else if (flag ch2-pretoot-door-attempted is false) {
		// you've already discovered the doors don't work
		if (flag ch2-pretoot-hax-attempted is true) {
			show dialog {
				PLAYER "The doors don't work, either? This is going to be a short quest."
			}
		} else {
			show dialog {
				PLAYER "The doors don't work? This is going to be a short quest."
			}
		}
		set flag ch2-pretoot-door-attempted to true
	} else {
		// being obnoxious now :3
		if (variable ch2-pretoot-door-attempts is 0) {
			show dialog {
				PLAYER "How am I supposed to get around if the doors don't work?"
			}
		} else if (variable ch2-pretoot-door-attempts is 1) {
			show dialog {
				PLAYER "Hmm. Maybe there's something else in this room I can investigate."
			}
		} else if (variable ch2-pretoot-door-attempts is 2) {
			show dialog {
				PLAYER "That's kind of a funny orange spider, isn't it? I wonder if it can help me."
			}
		} else {
			show dialog {
				PLAYER "I should go talk to the spider robot over there."
			}
		}
		mutate ch2-pretoot-door-attempts + 1
	}
	// reset
	set entity "%SELF%" interact_script to ch2-attempt-door-check
}

/* ---------- ENTITIES ---------- */

interact-ch2-xa {
	if (flag ch2-toot-done is false) {
		if (flag ch2-has-artifact is false) {
			set player control off
			show dialog {
				name "???" "..."
				"...huh?"
				"Oh! Ohhh, is the light on? Hello?"
				"Oh my goodness. Is there actually someone there? Hello? Can you hear me?"

				PLAYER "I can hear you."

				name "???" "Oh! Oh! It's a person! Yes, hello! My name is... well...."

				Notakon "Notakon! Yeah, call me Notakon!"
				"I'm speaking to you through the Exa! It's this old intercom thing I set up ages ago. Looks like you're in the castle entrance. I'm surprised the Exa was still..."
				"Wait a minute. Who are you? How did you even get in?"

				PLAYER "I'm %PLAYER%. The village elders sent me here to collect the castle artifact so I could defeat the Big Bad."
				"And I'm not a hacker, I just...\nyou know...\nhack things. With Ring Zero."
				
				Notakon "No, no, that can't be right. The village elders?"
				"They're the ones who told me to PROTECT the artifact! I have to keep it away from everyone else at all costs!"

				PLAYER "Well we're going to need it soon! I don't know if you felt that earthquake last night, but the Big Bad is coming back and I need it to defeat him!"

				Notakon "The Big Bad, coming back? Yes, I see... he might still have the power to trigger an earthquake, even after...."
				"I, um, don't know why the village elders would have changed their minds about this, but...."
				"Hmm. Well, you're welcome to the artifact. It's right over there, to your right."
			}
			// a dumbwaiter-esque thing opens
			// player turns to the right
			show dialog {
				PLAYER "What, right here?"
			}
			// walks up to it
			
			show dialog {
				PLAYER "Huh. Almost looks like one of those old, fancy calculators."
			}
			// picks it up
			show dialog {
				PLAYER "That was easy. I thought there would be a quest or something. Puzzles, at least."
				Notakon "Yeah, well, it doesn't exactly work right now, so...."
			}
			// player turns toward XA
			show dialog {
				PLAYER "...Wait, what?"
				Notakon "That little thing can't do much on its own. The real powerful stuff was handled by the castle mainframe. They were linked wirelessly."
				"But the mainframe was crippled."
			}
			// player walks closer
			show dialog {
				PLAYER "Crippled? What happened?"

				Notakon "Well, ahh, I crippled it."
			}
			// player walks closer
			show dialog {
				PLAYER "What? Why?!"
				Notakon "I couldn't let the Big Bad have access to it again!"
				"And we weren't using it for much otherwise...."
				PLAYER "But now no one can use it!"
				Notakon "Frankly, that's a small price to pay for keeping that power out of the Big Bad's hands. Although...."
				"That was easier to say before the earthquake. The castle's in kind of a bad state now, and with the mainframe down we can't fix anything."
				"It's why you might as well have the portable interface. You could help out around here while I vet your story."
				PLAYER "Vet my story? You don't believe the village elders sent me?"
				Notakon "Well, um... this is all very complicated. I need to be sure that this isn't a trap."
				"If it helps, I don't think you're the Big Bad. He would have caused more mischief in that room, even with Ring Zero disabled."
			}
			if (flag ch2-pretoot-hax-attempted is true) {
				show dialog {
					PLAYER "Yeah, about that... why won't Ring Zero work in here?"
				}
			} else {
				show dialog {
					PLAYER "Ring Zero is disabled? Since when?"
					Notakon "Since you crossed the threshold."
				}
			}
			show dialog {
				Notakon "The castle walls are lined with aluminum. Acts as a faraday cage. And Ring Zero's hex editor relies on some wireless services."
				"Normally, XA bridges the town's network with the castle...."
				"You know what? There's a lot to explain, and it might be easier to chat with the artifact itself. Let me help you set it up."
			}
			set player control on
			set flag ch2-has-artifact to true
		} else {
			show dialog {
				Notakon "Let's get the artifact set up. I can help walk you through it."
			}
		}
		show dialog {
			Notakon "Er, that is, do you want help?"
			> "Yes, please!" : ch2-terminal-setup-start
			> "No, thanks." : ch2-terminal-setup-nevermind
		}
	} else {
		goto ch2-terminal-setup-check
	}
}
/* ---------- XA SERIAL CONSOLE WALKTHROUGH ---------- */

ch2-terminal-setup-check {
	show dialog {
		Notakon "Need help getting the console working?"
		> "Yes" : ch2-terminal-setup-start
		> "No" : ch2-terminal-setup-nevermind
	}
}

ch2-terminal-setup-nevermind {
	if (flag ch2-toot-done is false) {
		show dialog {
			Notakon "Ah, okay! Um, if you change your mind, speak to the exa again and I'll walk you through everything."
			"To finish our conversation, type \"MAN\" into the console."
		}
		set serial control on
	} else {
		show dialog {
			Notakon "Ah, okay. Carry on!\n\nAlso, remember you can speak to me in the console with the command \"MAN\"."
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-xa
}

ch2-terminal-setup-start {
	show dialog {
		Notakon "First, plug the artifact into your computer. (Or, if it's plugged in but not working, try flipping the cable the other direction, or try another USB port.)"
	}
	goto ch2-terminal-plugged-check
}

ch2-terminal-plugged-check {
	show dialog {
		Notakon "Is the artifact plugged in now?"
		> "Yes" : ch2-terminal-plugged-yes
		> "No" : ch2-terminal-plugged-no
		> "I'm on the web" : ch2-terminal-plugged-web
		> "Never mind" : ch2-terminal-setup-nevermind
	}
}

ch2-terminal-plugged-no {
	show dialog {
		Notakon "Well, this won't exactly work wirelessly! You're going to have to find a USB-C cable somewhere, so, um...."
	}
	goto ch2-terminal-plugged-check
}

ch2-terminal-plugged-web {
	show dialog {
		Notakon "Oh, then you're all set already! Your console will be directly below the game screen, in your browser window."
		"You may have to click between the game screen and the console depending on what you want to be doing. Clicking gives focus to what you clicked on."
	}
	goto ch2-terminal-message-test
	set entity "%SELF%" interact_script to interact-ch2-xa
}

ch2-terminal-plugged-yes {
	show dialog {
		Notakon "Great! So now, um, what OS do you have?"
		> "Windows" : ch2-terminal-os-windows
		> "Mac or Linux" : ch2-terminal-os-unix
		> "Something else" : ch2-terminal-os-other
		> "Never mind" : ch2-terminal-setup-nevermind
	}
	set entity "%SELF%" interact_script to interact-ch2-xa
}

ch2-terminal-os-other {
	show dialog {
		Notakon "Oh, I see. Well, um, I'm afraid I can't be very much help then...."
		"But, ahh, do please ask around at DC801, and maybe one of the folks there can help point you in the right direction."
	}
	set entity "%SELF%" interact_script to interact-ch2-xa
}

ch2-terminal-os-windows {
	show dialog {
		Notakon "   TODO: do it on Windows"
	}
	goto ch2-terminal-message-test
}

ch2-terminal-os-unix {
	show dialog {
		Notakon "Good! Ahh, you should have some kind of terminal program already. It might even be called \"Terminal.\" Though, uh, I'm fond of iTerm myself, haha...."
		"   TODO: Mac/Linux tutorial"
	}
	goto ch2-terminal-message-test
}

ch2-terminal-message-test {
	show dialog {
		Notakon "Let's test the serial connection!"
	}
	show serial dialog spacer
	show serial dialog {
		"<m>NOTAKON</>: Testing, testing...."
		"\tCan you see this message?"
	}
	show dialog {
		Notakon "Did you see my serial message?"
		> "Yes" : ch2-terminal-message-test-success
		> "No" : ch2-terminal-message-test-fail
	}
}

ch2-terminal-message-test-fail {
	show dialog {
		Notakon "Hmm. Well, um, let's go over the steps again."
	}
	goto ch2-terminal-setup-start
}

ch2-terminal-message-test-success {
	if (flag ch2-toot-done is false) {
		show dialog {
			Notakon "Excellent! Let's continue our conversation using the terminal. Type \"MAN\" into the console to get things started."
		}
		copy ch2-register-manual
		set flag ch2-toot-done to true // TODO: temporary placement
		set serial control on
	} else {
		show dialog {
			Notakon "Excellent! You're all set!"
		}
	}
	set flag ch2-map-granted to true
	copy ch2-map-enable
	set entity "%SELF%" interact_script to interact-ch2-xa
}

/* ---------- NOTAKON INTRO CONVO PART II ---------- */

ch2-command-man {
	if (flag ch2-saw-bert-secret is true) {
		if (flag ch2-notakon-bert-prelude is false) {
			goto ch2-bert-secret-notakon-intro
		}
	}
	if (flag ch2-toot-done is false) {
		show serial dialog spacer
		show serial dialog {
			"<m>NOTAKON</>: Ahh, this is much more comfortable, isn't it? You can type commands into the console, and interesting things will happen."
			" "
			""
		}
		// TEMP
		set flag ch2-toot-done to true
		goto ch2-command-man-normal-convo
	} else {
		goto ch2-command-man-normal-convo // see other file
	}
}
