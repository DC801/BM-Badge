settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label SELF { entity "%SELF%" }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
}

/* -------- ON_TICK SCRIPTS -------- */
// no zigzagging!

felix-loiter {
	if flag storyflag-catwork is true then goto hiding-cat
	turn entity "%SELF%" south
	wait 2000ms /**/ turn entity "%SELF%" east
	wait 800ms /**/ turn entity "%SELF%" west
	wait 1200ms
}
deut-loiter {
	if flag storyflag-catwork is true then goto hiding-cat
	turn entity "%SELF%" west
	wait 900ms /**/ turn entity "%SELF%" east
	wait 900ms /**/ turn entity "%SELF%" south
	wait 2400ms
}
bmurray-loiter {
	if flag storyflag-catwork is true then goto hiding-cat
	turn entity "%SELF%" south
	wait 1800ms /**/ turn entity "%SELF%" north
	wait 900ms /**/ turn entity "%SELF%" east
	wait 1000ms
}
maru-loiter {
	if flag storyflag-catwork is true then goto hiding-cat
	turn entity "%SELF%" west
	wait 1400ms /**/ turn entity "%SELF%" south
	wait 1800ms /**/ turn entity "%SELF%" north
	wait 400ms /**/ turn entity "%SELF%" east
	wait 400ms
}

hiding-cat {
	teleport entity "%SELF%" to geometry catwalk-hide
	goto null_script // this action absent in original; I guess this looped forever... O.o
}

cat-walking-to-work {
	walk entity "%SELF%" to geometry catwalk-spot over 600ms
	walk entity "%SELF%" to geometry catwalk-door over 500ms
	teleport entity "%SELF%" to geometry catwalk-hide
	goto hiding-cat
}

on_tick-max-neutral {
	if flag storyflag-catwork is true then goto null_script
	if entity "%PLAYER%" type is pipscat then goto on_tick-max-catwatch
	set entity Felix tick_script to felix-loiter
	set entity Maru tick_script to maru-loiter
	set entity "B. Murray" tick_script to bmurray-loiter
	set entity Deuteronomy tick_script to deut-loiter
}
on_tick-max-catwatch {
	if entity "%PLAYER%" type is not pipscat then goto on_tick-max-neutral
	set entity Felix tick_script to face-player
	set entity Maru tick_script to face-player
	set entity "B. Murray" tick_script to face-player
	set entity Deuteronomy tick_script to face-player
	if entity "%PLAYER%" animation is 2 then goto on_tick-max-thumbwatch
}
on_tick-max-thumbwatch {
	if entity "%PLAYER%" is inside geometry catwatch-zone then goto send-cats-to-work
	wait 2000ms
}

/* -------- ON_TICK CUTSCENE -------- */

send-cats-to-work {
	set player control to off
	wait 1300ms
	if entity Felix is not inside geometry felix-spot then goto show_dialog-max-crewmissing
	if entity Deuteronomy is not inside geometry deut-spot then goto show_dialog-max-crewmissing
	if entity Maru is not inside geometry maru-spot then goto show_dialog-max-crewmissing
	if entity "B. Murray" is not inside geometry bmurray-spot then goto show_dialog-max-crewmissing
	turn entity "Max Swagger" toward entity "%PLAYER%"
	play entity "Max Swagger" animation 3 once
	set entity "Max Swagger" animation to 4
	wait 500ms
	show dialog {
		entity "Max Swagger"
		"Oh! Approval from the cat boss himself!"
		Felix "Yeah, all right. We're good to go."
		"Let's get started!"
	}
	wait 300ms
	set entity Felix tick_script to cat-walking-to-work
	wait 400ms
	set entity Deuteronomy tick_script to cat-walking-to-work
	wait 400ms
	set entity Maru tick_script to cat-walking-to-work
	wait 400ms
	set entity "B. Murray" tick_script to cat-walking-to-work
	wait 400ms
	show dialog {
		entity "Max Swagger"
		"Brilliant! Now things can finally move forward!"
	}
	set player control to on
	set flag storyflag-catwork to true
	play entity "Max Swagger" animation 5 once
	set entity "Max Swagger" animation to 0
	copy calculate-hint-tracking-series
	if (variable hint-tracking-check is 6) {
		copy set-hint-none
	}
	goto null_script
}
show_dialog-max-crewmissing {
	show dialog {
		entity "Max Swagger"
		"Oh! Approval from the boss!"
		"But is the crew all here to see it...?"
	}
	set player control to on
	set entity "Max Swagger" tick_script to on_tick-max-neutral
}

/* -------- REGULAR STUFF -------- */

show_dialog-felix {
	if (entity "%SELF%" is glitched) {
		show dialog {
			SELF "Sxas. T'gp mppy hzcvtyr dz slco T'x slwwfntyletyr!"
		}
	} else {
		copy set-hint-max
		if (entity "%PLAYER%" type is pipscat) {
			show dialog { SELF "Okay, boss. We good to go?" }
		} else {
			show dialog { SELF "Can't begin work until the boss approves the plans." }
		}
	}
	// RESET
	set entity "%SELF%" interact_script to show_dialog-felix
}
show_dialog-deut {
	if (entity "%SELF%" is glitched) {
		show dialog { SELF "Abj gung'f n gevc." }
	} else {
		copy set-hint-max
		if (entity "%PLAYER%" type is pipscat) {
			show dialog { SELF "Oh, good! You're here! Gonna give the thumbs up?" }
		} else {
			show dialog { SELF "The boss is usually pretty busy. Not sure where he is right now." }
		}
	}
	// RESET
	set entity "%SELF%" interact_script to show_dialog-deut
}
show_dialog-maru {
	if (entity "%SELF%" is glitched) {
		show dialog { SELF "Qisa." }
	} else if (entity "%PLAYER%" type is pipscat) {
		show dialog { SELF "Meow?" }
	} else {
		// copy set-hint-max // absent from original; intentional? (probably not)
		show dialog { SELF "Meow." }
	}
	// RESET
	set entity "%SELF%" interact_script to show_dialog-maru
}
show_dialog-bmurray {
	if (entity "%SELF%" is glitched) {
		show dialog { SELF "Ib, hi... C ehyq nbun wunhcj qum vux!" }
	} else {
		copy set-hint-max
		if (entity "%PLAYER%" type is pipscat) {
			show dialog { SELF "So is it a yea or a nay? Go ahead, show us what you decided." }
		} else {
			show dialog { SELF "As much as I like to loaf around, I sure hope the boss'll get over here soon so we can start!" }
		}
	}
	// RESET
	set entity "%SELF%" interact_script to show_dialog-bmurray
}

show_dialog-max-start {
	if (entity "%SELF%" is glitched) {
		show dialog {
			SELF "Azr. Vm jvbyzl aolyl'k il huvaoly jvtwspjhapvu!"
		}
	} else if (flag storyflag-catwork is true) {
		copy face-player
		show dialog {
			entity "Max Swagger"
			"Finally! Now work on my fashion shop can progress!"
		}
	} else if (entity "%PLAYER%" type is pipscat) {
		copy face-player
		show dialog {
			entity "Max Swagger"
			"Oh! The cat boss, here at last!"
			"Come on, then! Press square to show us you approve of the plans!"
		}
		copy set-hint-max
		set flag max-backstory to true
	} else if (flag max-backstory is true) {
		copy face-player
		show dialog {
			SELF "All the crew needs to begin now is a thumbs up from their boss!"
			"Where is that cat?"
		}
		copy set-hint-max
	} else {
		set player control off
		wait 200ms /**/ copy face-player
		wait 400ms /**/ show dialog {
			SELF "Ah, yes! I know you!"
			"You're the birthday mage!"
			PLAYER "I guess you could say that."
			SELF "Yes, yes!"
			"Well, you're invited to the grand opening of my new fashion shop!"
			PLAYER "Oh, neat!"
			SELF "...Next year!"
			PLAYER "What? Next year? Your shop isn't ready yet?"
			SELF "Oh, good heavens, no! Not yet!"
			"There's much more to do... so much more!"
			"The interior must be wallpapered and painted, and the new carpet cut and layed...."
			"The new furniture must be brought in from Copenhagen, and that could take weeks alone!"
			"But, of course, before any of that...."
		}
		wait 200ms /**/ turn entity "Max Swagger" toward entity Felix
		wait 400ms /**/ show dialog {
			SELF "The CONSTRUCTION CREW must finish building the interior!!"
		}
		wait 200ms /**/ copy face-player
		wait 400ms /**/ show dialog {
			SELF "They have everything they need to begin and yet they won't do any work!"
		}
		wait 200ms /**/ pan camera to geometry camera-felix over 800ms
		wait 200ms /**/ show dialog {
			Felix "No, we don't have everything."
			"We need the boss to sign off on the plans, first."
		}
		wait 200ms /**/ pan camera to entity "%PLAYER%" over 800ms
		wait 200ms /**/ turn entity "Max Swagger" toward entity Felix
		wait 400ms /**/ show dialog {
			SELF "Yes, of course! Of course the boss must approve first!"
		}
		wait 200ms /**/ copy face-player
		wait 400ms /**/ show dialog {
			SELF "But the boss is nowhere around!"
			"I gave him the plans days ago! What could possibly be taking so long?"
		}
		set flag max-backstory to true
		copy set-hint-max
		set player control on
	}
	// RESET
	set entity "Max Swagger" interact_script to show_dialog-max-start
}
