include!("header.mgs")

on_tick-secretroom {
	if entity "%PLAYER%" is inside geometry door-secretdoor-exit then goto leave-secretroom
}
leave-secretroom {
	set warp state to exit_from-secretroom
	load map lodge
}

on_load-secretroom {
	if (flag tuesdayd is true) {
		teleport entity Jackob to geometry secretroom-hidingplace
		teleport entity Alfonso to geometry secretroom-hidingplace
		teleport entity Bert to geometry secretroom-hidingplace
		register rtfm -> command-rtfm
	} else {
		goto secretroom-cutscene // JUMP
	}
}

secretroom-cutscene {
	set player control to off
	set flag hide-lodge-crowd to true
	teleport entity "%PLAYER%" to geometry secretroom-spawn-point
	walk entity "%PLAYER%" to geometry secretroom-lecture-spot over 1300ms
	show dialog {
		Alfonso "This is the inner sanctum, home to all the knowledge we have collected about the underpinnings of our reality."
		"In truth, we don't have any personal experience with Ring Zero itself, or how hacking actually works."
		"But, perhaps...."
	}
	wait 200ms /**/ turn entity Alfonso north
	wait 800ms /**/ turn entity Alfonso west
	wait 800ms /**/ turn entity Alfonso toward entity "%PLAYER%"
	wait 200ms /**/ show dialog {
		Bert "Perhaps...."
		"If you need help, come here, read these books."
		"Perhaps you will learn what you need."
		Jackob "You're the only one who can fix what's been broken."
		"We will assist you if we can, but the real work will be up to you."
	}
	wait 200ms /**/ walk entity Alfonso to geometry alfonso-exit over 300ms
	wait 200ms /**/ show dialog dialog-secretroom-declaration-alfonso {
		Alfonso "I am the town's bookkeeper and chief scholar, and can track your progress."
	}
	wait 200ms /**/ walk entity Bert to geometry bert-exit over 300ms
	wait 200ms /**/ show dialog dialog-secretroom-declaration-bert {
		Bert "I know the town. The people. I know who needs help."
	}
	wait 200ms /**/ walk entity Jackob to geometry jackob-exit over 300ms
	wait 200ms /**/ show dialog {
		Jackob "And I...."
	}
	wait 800ms /**/ turn entity Jackob west
	wait 800ms /**/ turn entity Jackob east
	wait 800ms /**/ turn entity Jackob west
	wait 200ms /**/ show dialog {
		Jackob "Oh, dear. What was it again?"
	}
	wait 200ms /**/ turn entity Alfonso toward entity Jackob
	wait 200ms /**/ show dialog {
		Alfonso "You're our bookworm!"
	}
	wait 200ms /**/ turn entity Jackob toward entity "%PLAYER%"
	wait 200ms /**/ show dialog {
		Jackob "Ah yes!"
		"Yes, I've read every book about hacking ever to exist, not just what you can find in this room!"
		"I think. I can't quite remember."
	}
	wait 200ms /**/ turn entity Alfonso toward entity "%PLAYER%"
	wait 200ms /**/ set entity Jackob on_tick to look-left-and-right
	show dialog {
		Alfonso "Well, he knows more about Ring Zero than anyone else."
		"Likely he'll be able to help you use the device itself if you get stuck."
	}
	wait 200ms /**/ turn entity Alfonso toward entity Jackob
	wait 1200ms /**/ turn entity Alfonso toward entity "%PLAYER%"
	wait 200ms /**/ show dialog {
		Alfonso "Um... maybe."
	}
	set entity Jackob on_tick to null_script
	turn entity Jackob toward entity "%PLAYER%"
	show dialog {
		Alfonso "*AHEM*"
	}
	show dialog {
		Bert "Good luck."
		Alfonso "Yes, good luck, %PLAYER%."
		"Please talk to us if there's anything we can do to help you."
		"We're counting on you!"
	}
	set flag tuesdayd to true
	copy elders-leave-secretroom
}

elders-leave-secretroom {
	wait 200ms /**/ set entity Bert on_tick to secretdoor-exit-walk
	wait 400ms /**/ set entity Alfonso on_tick to secretdoor-exit-walk
	wait 700ms /**/ set entity "%PLAYER%" on_tick to watch-jackob
	set entity Jackob on_tick to secretdoor-exit-columbo
}

secretdoor-exit-walk {
	walk entity "%SELF%" along geometry "%ENTITY_PATH%" over 1600ms
	teleport entity "%SELF%" to geometry secretroom-hidingplace
	goto null_script
}

secretdoor-exit-columbo {
	walk entity "%SELF%" along geometry "%ENTITY_PATH%" over 1400ms
	turn entity Jackob south
	wait 1000ms /**/ show dialog {
		Jackob alignment TL
		"Oh, one more thing."
	}
	wait 200ms /**/ turn entity Jackob toward entity "%PLAYER%"
	wait 600ms /**/ show dialog {
		Jackob alignment TL
		"To enter the hex editor, simply touch your hat with a finger."
		PLAYER alignment TR
		"What? My hat?"
		"But I thought it was the ring that was magic."
		Jackob alignment TL
		"No, not that hat."
		"The one on the PCB!"
	}
	wait 400ms
	walk entity "%SELF%" to geometry secretroom-exit-point over 300ms
	set player control to on
	set entity "%PLAYER%" on_tick to null_script
	teleport entity "%SELF%" to geometry secretroom-hidingplace
	set hex control to on
	wait 100ms
	register rtfm -> command-rtfm
	show serial dialog spacer
	show serial dialog {
		"<bold>NEW!</> Serial command <c>RTFM</> unlocked!"
		" "
		"Read the books in this room wherever you are! Just type <c>RTFM</>!"
	}
	goto null_script
}

look-left-and-right {
	turn entity "%SELF%" east
	wait 1100ms
	turn entity "%SELF%" west
	wait 1100ms
}

look-left-and-right-fast {
	turn entity "%SELF%" east
	wait 500ms
	turn entity "%SELF%" west
	wait 500ms
}

watch-jackob {
	turn entity "%SELF%" toward entity Jackob
}

show_dialog-hackbook {
	if (entity "%SELF%" is glitched) {
		show dialog {
			entity "Town History" portrait journal
			"ERQ UREEVAT"
		}
	} else {
		show dialog {
			entity "Town History" portrait journal
			"The Big Bad, who goes by the name%#29SC^7\n}ho was found in #A423#Bfjr$bf afte@t\n$#cY B.  Ex"
			"@t Y#C9 B. @t $Y#9 B.  exception EOF"
		}
		// doc: The Big Bad, named aslkdhgasdflkjasd.
		if (flag hackbook-backstory is false) {
			show dialog {
				PLAYER "Um... okay, then."
				"...."
				"No... no, wait a minute...."
				"The Big Bad's name?"
			}
			turn entity "%PLAYER%" south
			show dialog {
				PLAYER "I don't think I've actually ever heard his name before. He must have had a real name!"
				"But everyone just calls him the Big Bad. Why doesn't anyone use his name?"
				"...."
				"I'm gonna have to see what else I can find out about this book."
			}
			set flag hackbook-backstory to true
		} else {
			show dialog { PLAYER "Creepy." }
			if (flag hackbook-endstory is false) {
				show dialog dialog-hackbook-creepy-hint {
					PLAYER "I should really ask Jackob about this book."
				}
			}
		}
		copy set-hint-hackbook	
	}
	set entity "Town History" on_interact to show_dialog-hackbook
}

// BOOKS BOOKS BOOKS

show_dialog-entities1 {
	show dialog {
		entity "Entities 1" portrait journal
		"After many years of research, we have discovered the data structure of all living beings, as well as miscellaneous inanimate objects."
		"In all cases, hackable beings and objects -- or 'entities' -- are driven by 32 bytes of data, appearing as two rows of 16 bytes."
		"[X X X X X X X X X X X X . . . .]\nThe first twelve bytes on the first row are the entity's true name, given in something called \"Ass Key.\""
		"[. . . . . . . . . . . . X X X X]\nAfter the name, the next four bytes are the entity's position in the world, two bytes for X and two bytes for Y."
		"The position of each byte within each pair is backwards relative to what seems to us to be logical -- and from what we understand, a Mr. N. Dian is to blame."
	}
}
show_dialog-entities2 {
	show dialog {
		entity "Entities 2" portrait journal
		"The second row of entity data is far more complicated."
		"[X X o o . . . . . . . . . . . .]\nThe first pair of bytes seems to affect what the entity does when interacted with. Changing these values is frequently counterproductive."
		"[o o X X . . . . . . . . . . . .]\nHowever, changing the second pair of bytes causes unspeakable chaos. We have learned to leave these bytes undisturbed."
		"[. . . . o o o o o . . . . . . .]\nThe next five bytes apparently concern how an entity appears,"
		"[. . . . o o o o X . . . . . . .]\nbut the last byte affects the others in ways we are still trying to understand."
		"[. . . . X X o o o . . . . . . .]\nThe first pair is more useful, seeming to change the entity type, or which among its category the entity appears to be."
		"[. . . . o o X X o . . . . . . .]\nThe second pair appears to do nothing the vast majority of the time, but occasionally it changes the entity's appearance in odd ways --"
		"[. . . . o o X X o . . . . . . .]\nit's almost as if certain entities exist in a series along a parallel dimension, and we can simply choose which dimension is realized."
		"Again, all two-byte pairs are backwards in their manifestation. We are preparing a strongly-worded letter to Mr. N. Dian."
	}
}
show_dialog-entities3 {
	show dialog {
		entity "Entities 3" portrait journal
		"[. . . . . . . . . o o o . . . .]\nThe next three bytes on the second row apparently describe how the entity is behaving."
		"[. . . . . . . . . X o o . . . .]\nThe first byte describes the action the entity is currently performing,"
		"[. . . . . . . . . o X o . . . .]\nwhile the next describes how far along into the action the entity has progressed."
		"[. . . . . . . . . X X o . . . .]\nAlas, not all entities are capable of all actions, though some appear delightfully animated."
		"[. . . . . . . . . o o X . . . .]\nThe next byte first appeared only to represent the cardinal direction the entity is facing, but we have discovered a secret:"
		"[. . . . . . . . . o o X . . . .]\nIf this byte is considered not as a unit but instead as a series of 8 bits, then the largest bit toggles a bizarre disruption in appearance,"
		"[. . . . . . . . . o o X . . . .]\nwhich we have found useful to cause great alarm and panic among our less-enlightened neighbors."
	}
}
show_dialog-entities4 {
	show dialog {
		entity "Entities 4" portrait journal
		"[. . . . . . . . . . . . X X X X]\nThe final four bytes are quite abstract."
		"[. . . . . . . . . . . . X X o o]\nThe first pair seems to us to bond the entity to a physical place -- a beloved path, a favorite chair, etc."
		"[. . . . . . . . . . . . o o X X]\nThe function of the final pair escapes us, at least for the moment."
		"There may yet be undiscovered secrets, and the world may yet change around us, rendering invalid the structure we have already mapped."
		"But we will endeavor to continue our research."
	}
}
show_dialog-bitsbytes {
	show dialog {
		entity "Bits & Bytes" portrait journal
		"A bit is a one or a zero. But combine eight of them, and you can describe numbers up to 255 -- a byte."
		"For simplicity, it is best to think of a byte as a pair of numbers between 0 and 15 -- or 0 and F."
		PLAYER "Hmmm. I think I had this textbook in school. I don't really wanna go over it again."
	}
}
show_dialog-hexediting {
	show dialog {
		entity "Hex Edits 1" portrait journal
		"Triangle, a button thought by most scholars to do nothing, apparently provides a shortcut to an entity's first row of data."
		"Once inside the hex editor, however, this button instead increments the currently- selected byte by one."
		"Its companion, the X button, decrements the currently-selected byte."
		"Both triangle and X thus provide convenient ways of altering data, but when outside the hex editor, we must rely on the standard methods:"
		"Pressing the bit buttons while the correct operator (XOR, ADD, SUB) is selected."
	}
}
show_dialog-hexediting2 {
	show dialog {
		entity "Hex Edits 2" portrait journal
		"Some advanced tricks we have discovered:"
		"Holding PAGE and pressing a MEM button reassigns the MEM shortcut to the currently-selected hex cell."
		"Conveniently, MEM references are relative to the entity the cursor is positioned within."
		"Though we've found little reason to change the mapping more than a handful of times. Our favorite bytes continue to be the same four."
		"In addition, the use of --"
		PLAYER "...?"
		"Huh? That's weird."
		"It looks like there's a page missing."
	}
}
show_dialog-math {
	show dialog {
		Math portrait journal
		"You know what ADDing and SUBtracting does, but XOR is less intuitive -- unless you are looking at the bits making up each byte."
		"In which case, think of XOR as a toggle on a single bit."
		"It's like each byte is actually eight boolean switches! Amazing!"
	}
}
show_dialog-magefacts2 {
	show dialog {
		entity "Mage Facts" portrait journal
		"Mage Facts Magazine, issue 37."
		"What happens when a mage presses the triangle button? Experts agree the answer is \"nothing.\""
		"\"Why would I waste my time pressing a button that doesn't do anything?\" says famous mage, Gorgonzola the Moist. \"I have better things to do with my life!\""
		"94% of all mages we interviewed this week admit they have never pressed the triangle button even once their whole lives."
		"\"My sensei's never used it, so I never felt a need to try it,\" says one young mage, 19, now a student at Underwand Institute of Technology."
	}
}
show_dialog-dejavu {
	show dialog {
		entity "Weird Facts" portrait journal
		"Weird Facts Magazine, super omnibus anniversary edition volume 3 (reprint): DEJA VU!"
		"You've heard of deja vu, but did you know you can trigger it deliberately?"
		"Simply press XOR and MEM3 at the same time, and the room around you will seem to CHANGE!"
		"SPOOKY! It's like walking into the same room, all over again! It's like going back in time!"
		"Seeing something weird? Wondering if it's your mind playing tricks? Want it to go away? Try XOR and MEM3!"
		"Unless you want to embrace the WEIRDNESS, of course!"
	}
}
show_dialog-bookorigins {
	show dialog {
		entity "Book Origins" portrait parchment
		"Meeting minutes: Smarch 32nd, year 1337\nSubject: who wrote the 'Entity' and 'Hex Edits' books?"
		"Alfonso: \"Whoever wrote those books must have had access to the real Ring Zero.\""
		"Bert: \"The Big Bad?\""
		"Jackob: \"But the Big Bad had no accomplices, and there were clearly multiple people doing this research.\""
		"Alfonso: \"Then perhaps the Big Bad was not the first mage to have Ring Zero. Perhaps he will not be the last.\""
		"Bert: \"Or is there more than one ring?\""
	}
}
show_dialog-redletters {
	show dialog {
		entity "Red Letters" portrait parchment
		"Meeting minutes: Smarch 16th, year 1337\nSubject: a rubbing taken from a dilapidated granite plaque found deep in the Fortran Forest:"
		"\"RED LETTERS MARK THE HACKER.\""
		"Alfonso: \"Obviously this is a reference to a calendar. Perhaps the power to hack is granted to mages on specific days -- 'red-letter' days!\""
		"Jackob: \"You mean mages can only become hackers on national holidays? Certainly not!\""
		"Alfonso: \"What else might it mean by 'red letters,' then? Perhaps... when a mage becomes a hacker, it IS a red-letter day!\""
		"Jackob: \"Or perhaps the phrase 'red letters' is a red herring!\""
	}
}
