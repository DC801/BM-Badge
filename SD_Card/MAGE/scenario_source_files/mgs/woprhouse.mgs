include!("header.mgs")

on_load-woprhouse {
	if (flag tuesdayd is true) {
		register rtfm -> command-rtfm;
	}
	if (warp state is warping) {
		// this is here because the "backdoor" dialog must come after the warp-in thing
		copy warping-in-fade-in;
		set flag ch2-manual-warp to true; // this will skip the "init" script's warp stuff
	}
	if (flag wopr-backdoor-found is false) {
		turn player control off;
		if (warp state is not warped) {
			walk entity "%PLAYER%" along geometry walk_from-north over 600ms;
		}
		wait 400ms;
		show dialog {
			PLAYER "Whoa! It looks like I found some kind of back door."
		}
		set flag wopr-backdoor-found to true;
	}
	turn player control on;
	if (variable current-chapter is 2) {
		goto ch2-map-init // JUMP TO SHARED BEHAVIOR
	}
}

/* ---------- ON_TICK ---------- */

on_tick-woprhouse {
	if entity "%PLAYER%" is inside geometry north-hitbox
		then goto on_walk-woprhouse-north;
}

/* ---------- EXIT STUFF ---------- */

on_walk-woprhouse-north {
	// triggered by map's on_tick
	if (variable current-chapter is 1) {
		set warp state to exit_from-woprhouse;
		load map main;
	} else if (variable current-chapter is 2) {
		set warp state to walk_from-woprhouse;
		load map main2;
	} else {
		show serial dialog { "You're in chapter '$current-chapter$'??" }
	}
}

on_go-woprhouse-north {
	copy warping-out-fade-out;
	load map main2;
}

/* ---------- ENTITIES ---------- */

show_dialog-wopr-start {
	show dialog {
		SELF
		portrait wopr
		"SHALL WE PLAY A GAME?"

		SELF
		portrait wopr
		"PLAY?"
		> "DO NOT PLAY" : goto "restart-wopr"
	}
}
restart-wopr {
	set entity "%SELF%" on_interact to show_dialog-wopr-start;
}

show_dialog-woprbooks {
	show dialog {
		PLAYER "These shelves are full of Vogon poetry! What an odd sort of thing for a computer to collect."
	}
}
