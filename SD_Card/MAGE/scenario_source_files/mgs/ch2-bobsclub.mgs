settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label PLAYER { entity "%PLAYER%" alignment BR }
	parameters for label SELF { entity "%SELF%" }
}
settings for serial dialog { wrap 60 }

const! (
	$reset = 0
	$cupboard = 1
	$glasses = 2
	$fridge = 3
)

/* ---------- LOOK SCRIPTS ---------- */

look-ch2-bobsclub {
	show serial dialog spacer
	show serial dialog {
		"You look around at the <m><bold>Bob's Club</>."
		" "
		"You can hear muffled, rhythmic thumping through the floor, though there's no clear source."
		" "
	}
}

/* ---------- ON_LOAD ---------- */

// DEBUG
find-cc { set flag ch1-found-cactuscooler to true }
unfind-cc { set flag ch1-found-cactuscooler to false }
want-cc { set flag ch2-want-cactuscooler to true }
unwant-cc { set flag ch2-want-cactuscooler to false }
carry-cc { set flag ch2-carrying-cactuscooler to true }
uncarry-cc { set flag ch2-carrying-cactuscooler to false }
basement { set flag ch2-cutseen-bob-party to true }
unbasement { set flag ch2-cutseen-bob-party to false }
toot { set flag ch2-toot-done to true }
untoot { set flag ch2-toot-done to false }
reset {
	set flag ch2-carrying-cactuscooler to false
	set flag ch2-want-cactuscooler to false
	set flag ch1-found-cactuscooler to false
	set flag ch2-cutseen-bob-party to false
	set flag ch2-toot-done to false
	mutate ch2-rattle-count = 0
	show serial dialog {
		"RESET"
	}
}
// END DEBUG

on_load-ch2-bobsclub {
	// do unique stuff first (but turn serial / player control off first, if you do)
	mutate ch2-rattle-count = 0
	register find + cc -> find-cc
	register unfind + cc -> unfind-cc
	register want + cc -> want-cc
	register unwant + cc -> unwant-cc
	register basement -> basement
	register unbasement -> unbasement
	register toot -> toot
	register untoot -> untoot
	register carry + cc -> carry-cc
	register uncarry + cc -> uncarry-cc
	register reset -> reset
	set serial control on
	
	if (warp state is warping) {
		// this is here because the "intro" dialog must come after the warp-in thing
		copy warping-in-fade-in
		set flag ch2-manual-warp to true // this will skip the "init" script's warp stuff
	}

	// Have NOT been in the basement
	if (flag ch2-cutseen-bob-party is false) {
		set player control off
		set hex control off
		if (warp state is not warped) {
			walk entity "%PLAYER%" to geometry "walk-spot" over 400ms
		}
		wait 220ms
		// Intro words
		if (flag ch2-saw-emptybobroom is false) {
			set flag ch2-saw-emptybobroom to true
			set entity "%PLAYER%" tick_script to look-left-and-right-fast
			show dialog {
				PLAYER "Huh? That's weird. Where did everyone go?"
				"And what's that muffled thumping noise?"
			}
		}
		else {
			show dialog {
				PLAYER "Huh. Still empty... apart from that muffled thumping noise."
			}
		}
		copy player-look-down
		// CC followup
		if (flag ch2-want-cactuscooler is true) { // You KNOW you need CC
			if (flag ch1-found-cactuscooler is true) {
				show dialog {
					PLAYER "At least I can grab some Cactus Cooler while I'm here. I know I saw some in the fridge yesterday."
				}
			}
			else {
				show dialog {
					PLAYER "There might be Cactus Cooler around somewhere, though. This place has drinks and stuff, right?"
				}
			}
		}
		else { // You DON'T know you need CC
			show dialog {
				PLAYER "Maybe I should take a look around."
			}
		}
	}
	else { // You HAVE been in the basement
		if (flag ch2-want-cactuscooler is true) { // If you know you need CC
			if ( // You've already collected the CC
				flag ch2-carrying-cactuscooler is true
				|| flag ch2-carrying-heatsink is true
				|| flag ch2-installed-heatsink is true
			) {
				// DO NOTHING
			}
			else {
				// REMIND THEM TO GET IT!
				if (flag ch1-found-cactuscooler is true) {
					show dialog {
						PLAYER "Oh, yeah. I found Cactus Cooler in the fridge here yesterday. I should grab some while I'm here."
					}
				}
				else {
					show dialog {
						PLAYER "Oh, right. They had drinks and stuff at the party. I wonder if they'll have a rare soda like Cactus Cooler?"
					}
				}
			}
		}
	}
	goto ch2-map-init
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-bobsclub {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to exit_from-bobsclub
		load map main2
	}
	set map tick_script to on_tick-ch2-bobsclub // reset
}

/* ---------- EXIT STUFF ---------- */

on_go-ch2-bobsclub-stairs {
	copy warping-out-fade-out
	load map ch2-bobsclub-basement
}
on_go-ch2-bobsclub-south {
	copy warping-out-fade-out
	load map main2
}

/* ---------- ENTITIES ---------- */

interact-ch2-bobbox {
	if (flag ch2-cutseen-bob-party is false) {
		show dialog {
			PLAYER "It's a box of unfilled balloons. There used to be a ton more of them though."
		}
	}
	else {
		show dialog {
			PLAYER "I guess they didn't need this box of balloons downstairs."
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-bobbox
}
interact-ch2-bobbarstool {
	if (flag ch2-cutseen-bob-party is false) {
		show dialog {
			PLAYER "Only one stool for the whole bar? Where's the rest?"
		}
	}
	else {
		show dialog {
			PLAYER "They just left this stool by itself, huh? Guess there wasn't room downstairs."
		}
	}
	set entity "%SELF%" interact_script to interact-ch2-bobbarstool
}
interact-ch2-bobbarrel {
	show dialog {
		PLAYER "A beer keg maybe?"
	}
}
interact-ch2-bobcountertop {
	if (flag ch2-cutseen-bob-party is false) {
		show dialog {
			PLAYER "I KNOW there used to be bottles by the sink! Where did everything go?"
		}
	} // (no need to say anything once the basement is discovered)
	set entity "%SELF%" interact_script to interact-ch2-bobcountertop
}

// CALLBACK (ish) STUFF

interact-ch2-bobcupboard {
	if (flag ch2-touched-bobcupboard is false) {
		show dialog {
			PLAYER "Those are some hardy-looking glasses and tumblers."
		}
		set flag ch2-touched-bobcupboard to true
	}
	else {
		show dialog {
			PLAYER "Yup, still glasses and tumblers in there."
		}
	}
	mutate CALLBACK = $cupboard
	goto ch2-bobrattling
}
interact-ch2-bobglasses {
	show dialog {
		PLAYER
		"These glasses look super fancy."
	}
	mutate CALLBACK = $glasses
	goto ch2-bobrattling
}
interact-ch2-bobfridge {
	if ( // You've already collected the CC
		flag ch2-carrying-cactuscooler is true
		|| flag ch2-carrying-heatsink is true
		|| flag ch2-installed-heatsink is true
	) {
		show dialog { PLAYER "Nothing in here." }
	}
	else {
		if (flag ch1-found-cactuscooler is false) {
			show dialog {
				PLAYER "I would have thought there'd be some soda in here, but nothing. Not even a box of baking soda."
			}
		}
		else {
			show dialog {
				PLAYER "I could have sworn there was Cactus Cooler in here yesterday, but it's gone now."
			}
		}
	}
	mutate CALLBACK = $fridge
	goto ch2-bobrattling
}

player-look-down {
	set entity "%PLAYER%" tick_script to null_script
	turn entity "%PLAYER%" south
}

ch2-bobrattling { // Hint that there's more inside the building
	if (flag ch2-cutseen-bob-party is false) {
		set entity "%PLAYER%" tick_script to look-left-and-right-fast
		// naive only
		if (variable ch2-rattle-count is 0) {
			show dialog {
				PLAYER "...Jeez, that weird thumping is actually rattling the shelves! Is that... music? Loud music?"
			}
			copy player-look-down
			show dialog {
				PLAYER "Where the heck is it coming from? This is the only room in the building!"
				"...Right?"
			}
		}
		else if (variable ch2-rattle-count is 1) {
			show dialog {
				PLAYER "...Okay, that thumping is DEFINITELY music. I think the bass just dropped."
			}
			show dialog {
				PLAYER "It sounds like the speaker is nearby, like just beyond a wall. But the only visible doorway is the main entrance."
			}
		}
		else if (variable ch2-rattle-count is 2) {
			show dialog {
				PLAYER "...Wow, I think what I'm hearing is actually dubstep. It's muffled, but it's so loud it must be practically next door."
			}
			copy player-look-down
			show dialog {
				PLAYER "Is it under the floor or something? I keeping looking but there aren't any stairs!"
			}
		}
		// you have the artifact and can get more specific hints
		else if (variable ch2-rattle-count is 3) {
			show dialog {
				PLAYER "...Okay, that music is starting to get on my nerves. But where..."
			}
			copy player-look-down
			show dialog {
				PLAYER "...Oh, that's right! The artifact can identify the exits for each room!"
				"I wonder if it could show me the way to a secret room."
				"...With secret dubstep."
			}
		}
		else if (variable ch2-rattle-count is 4) {
			copy player-look-down
			show dialog {
				PLAYER "...Ah, that's right, I remember now. I can use the artifact to LOOK around the room. Maybe I can find the source of the music that way."
			}
		}
		else {
			copy player-look-down
			show dialog {
				PLAYER "...But enough distractions. I should use the LOOK command in the serial console to discover any secret doors."
			}
		}
		copy player-look-down

		// advance the count
		mutate ch2-rattle-count + 1
		if (flag ch2-toot-done is false) {
			mutate ch2-rattle-count % 3
		}
	}
	goto ch2-bobrattling-wrapup
}

ch2-bobrattling-wrapup {
	if (variable CALLBACK is $cupboard) {
		goto interact-ch2-bobcupboard-RESUME
	}
	else if (variable CALLBACK is $glasses) {
		goto interact-ch2-bobglasses-RESUME
	}
	else if (variable CALLBACK is $fridge) {
		goto interact-ch2-bobfridge-RESUME
	}
	else {
		show serial dialog spacer
		show serial dialog {
			"<r>ERROR</>"
			" "
			"Callback value is set to: $CALLBACK$"
			"\tSource: script 'ch2-bobrattling-wrapup'"
		}
	}
}
interact-ch2-bobcupboard-RESUME {
	mutate CALLBACK = $reset
	set entity "%SELF%" interact_script to interact-ch2-bobcupboard
}
interact-ch2-bobglasses-RESUME {
	mutate CALLBACK = $reset
	set entity "%SELF%" interact_script to interact-ch2-bobglasses
}
interact-ch2-bobfridge-RESUME {
	mutate CALLBACK = $reset
	set entity "%SELF%" interact_script to interact-ch2-bobfridge
}
